IF
	OnCreation()
	Global("d2phase1","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("d2phase1","LOCALS",0)
		SetGlobal("d2phase2","LOCALS",0)
		SetGlobal("d2phase3","LOCALS",0)
		SetGlobal("d2final","LOCALS",0)
END

IF
	See([ANYONE])
	!Global("d2phase1","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("d2phase1","LOCALS",1)
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		ReallyForceSpell(LastSeenBy(),WIZARD_NPC_SYMBOL_STUN)
		ForceSpell(Myself,WIZARD_SUN_FIRE)
		DestroyItem("%w8%u")
END

IF
	See([ANYONE])
	HPLT(Myself,200)
	Global("d2phase1","LOCALS",1)
	!Global("d2phase2","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("d2phase2","LOCALS",1)
		ForceSpell(Myself,CLERIC_AURA_OF_FLAMING_DEATH)
		ReallyForceSpell(Myself,CLERIC_REGENERATE)
		ReallyForceSpell(Myself,WIZARD_ABI_DALZIMS_HORRID_WILTING)
		ReallyForceSpell(LastSeenBy(),WIZARD_NPC_SYMBOL_FEAR)
		DestroyItem("%w8%v")
END

IF
	See([ANYONE])
	HPLT(Myself,100)
	Global("d2phase2","LOCALS",1)
	!Global("d2phase3","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("d2phase3","LOCALS",1)
		ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
		ForceSpell(Myself,WIZARD_IMPROVED_HASTE)
		ReallyForceSpell(LastSeenBy(),WIZARD_NPC_SYMBOL_DEATH)
		ReallyForceSpell(Myself,WIZARD_METEOR_SWARM)
		DestroyItem("%w8%x")
END

IF
	See([ANYONE])
	HPLT(Myself,20)
	Global("d2phase3","LOCALS",1)
	!Global("d2final","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("d2final","LOCALS",1)
		ReallyForceSpell(Myself,CLERIC_STORM_OF_VENGEANCE)
		ReallyForceSpell(Myself,CLERIC_FIRE_STORM)
		ReallyForceSpell(Myself,CLERIC_INSECT_PLAGUE)
		DestroyItem("%w8%y")
END

IF
	!GlobalTimerNotExpired("d2phase1summon","LOCALS")
	Global("d2phase1","LOCALS",1)
	!Global("d2phase2","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobalTimer("d2phase1summon","LOCALS",18)
		ReallyForceSpell(Myself,WIZARD_ANIMATE_DEAD)
		ReallyForceSpell(Myself,WIZARD_ANIMATE_DEAD)
		Continue()
END

IF
	!GlobalTimerNotExpired("d2phase2summon","LOCALS")
	Global("d2phase2","LOCALS",1)
	!Global("d2phase3","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobalTimer("d2phase2summon","LOCALS",36)
		ReallyForceSpell(Myself,CLERIC_AERIAL_SERVANT)
		ReallyForceSpell(Myself,CLERIC_AERIAL_SERVANT)
		Continue()
END

IF
	!GlobalTimerNotExpired("d2phase3summon","LOCALS")
	Global("d2phase3","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobalTimer("d2phase3summon","LOCALS",54)
		ReallyForceSpell(Myself,CLERIC_SUMMON_FALLEN_DEVA)
		ReallyForceSpell(Myself,CLERIC_SUMMON_FALLEN_DEVA)
		Continue()
END

IF
	See(NearestEnemyOf())
	!StateCheck(LastSeenBy(),STATE_HELPLESS)
	!GlobalTimerNotExpired("d2balordispel","LOCALS")
THEN
	RESPONSE #100
		ForceSpellRES("SPWI302",LastSeenBy())
		SetGlobalTimer("d2balordispel","LOCALS",60)
		Continue()
END

IF
	See([GOODCUTOFF.0.0.0.0.SUMMONED])
	!GlobalTimerNotExpired("d2banish","LOCALS")
THEN
	RESPONSE #100
		ForceSpell(LastSeenBy(),WIZARD_DEATH_SPELL)
		SetGlobalTimer("d2banish","LOCALS",45)
		Continue()
END

IF
	Range([PC],5)
	!GlobalTimerNotExpired("d2unholyword","LOCALS")
THEN
	RESPONSE #100
		ForceSpell(Myself,CLERIC_UNHOLY_WORD)
		ReallyForceSpell(Myself,WIZARD_GREATER_MALISON)
		ReallyForceSpell(Myself,WIZARD_CLOUDKILL)
		SetGlobalTimer("d2unholyword","LOCALS",90)
END

IF
	See([GOODCUTOFF])
	!StateCheck(LastSeenBy(),STATE_HELPLESS)
	!GlobalTimerNotExpired("d2balorstun","LOCALS")
THEN
	RESPONSE #100
		ReallyForceSpell(LastSeenBy(),WIZARD_NPC_SYMBOL_STUN)
		SetGlobalTimer("d2balorstun","LOCALS",15)
		Continue()
END

IF
	See(NearestEnemyOf())
	!StateCheck(LastSeenBy(),STATE_HELPLESS)
	!GlobalTimerNotExpired("d2balorconfuse","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("d2balorconfuse","LOCALS",36)
		ForceSpell(LastSeenBy(),WIZARD_DOMINATION)
		Continue()
	RESPONSE #100
		SetGlobalTimer("d2balorconfuse","LOCALS",36)
		ForceSpell(LastSeenBy(),WIZARD_CHAIN_LIGHTNING)
		Continue()
	RESPONSE #100
		SetGlobalTimer("d2balorconfuse","LOCALS",36)
		ForceSpell(LastSeenBy(),CLERIC_IMPLOSION)
		Continue()
	RESPONSE #100
		SetGlobalTimer("d2balorconfuse","LOCALS",36)
		ForceSpell(LastSeenBy(),WIZARD_EMOTION_HOPELESSNESS)
		Continue()
END

IF
	See([PC])
	StateCheck(LastSeenBy(),STATE_HELPLESS)
	WeaponEffectiveVs(LastSeenBy(),MAINHAND)
	WeaponCanDamage(LastSeenBy(),MAINHAND)
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(),30)
END

IF
	See([NOTEVIL])
	StateCheck(LastSeenBy(),STATE_HELPLESS)
	WeaponEffectiveVs(LastSeenBy(),MAINHAND)
	WeaponCanDamage(LastSeenBy(),MAINHAND)
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(),30)
END

IF
	See([GOODCUTOFF])
	Range([GOODCUTOFF],5)
	OR(2)
		!WeaponEffectiveVs(LastSeenBy(),MAINHAND)
		!WeaponCanDamage(LastSeenBy(),MAINHAND)
	WeaponEffectiveVs(SecondNearestEnemyOf(),MAINHAND)
	WeaponCanDamage(SecondNearestEnemyOf(),MAINHAND)
THEN
	RESPONSE #100
		AttackOneRound(SecondNearestEnemyOf())
END

IF
	See([GOODCUTOFF])
	Range([GOODCUTOFF],5)
	OR(2)
		!WeaponEffectiveVs(LastSeenBy(),MAINHAND)
		!WeaponCanDamage(LastSeenBy(),MAINHAND)
	WeaponEffectiveVs(NearestEnemyOf(),MAINHAND)
	WeaponCanDamage(NearestEnemyOf(),MAINHAND)
THEN
	RESPONSE #100
		AttackOneRound(NearestEnemyOf())
END

IF
	See([GOODCUTOFF])
	Range([GOODCUTOFF],5)
	OR(2)
		!WeaponEffectiveVs(LastSeenBy(),MAINHAND)
		!WeaponCanDamage(LastSeenBy(),MAINHAND)
	WeaponEffectiveVs(ThirdNearestEnemyOf(),MAINHAND)
	WeaponCanDamage(ThirdNearestEnemyOf(),MAINHAND)
THEN
	RESPONSE #100
		AttackOneRound(ThirdNearestEnemyOf())
END

IF
	See([GOODCUTOFF])
	Range([GOODCUTOFF],5)
	OR(2)
		!WeaponEffectiveVs(LastSeenBy(),MAINHAND)
		!WeaponCanDamage(LastSeenBy(),MAINHAND)
	WeaponEffectiveVs(TenthNearestEnemyOf(),MAINHAND)
	WeaponCanDamage(TenthNearestEnemyOf(),MAINHAND)
THEN
	RESPONSE #100
		AttackOneRound(TenthNearestEnemyOf())
END

IF
    AttackedBy([ANYONE],DEFAULT)
THEN
	RESPONSE #100
		AttackReevaluate(LastAttackerOf(),30)
END

IF
	See([PC])
	Range(LastSeenBy(),10)
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(),30)
END

IF
	See([NOTEVIL])
	Range(LastSeenBy(),4)
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(),30)
END

IF
	See(NearestEnemyOf())
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(),30)
END

IF
	GlobalTimerNotExpired("d2randomwalk","LOCALS")
	!See([GOODCUTOFF])
	Detect([PC])
THEN
	RESPONSE #100
		RandomWalk()
END

IF
	HPLT(Myself,80)
	!GlobalTimerNotExpired("d2regen","LOCALS")
	!GlobalTimerNotExpired("d2randomwalk","LOCALS")
	!See([GOODCUTOFF])
THEN
	RESPONSE #100
		ReallyForceSpell(Myself,CLERIC_REGENERATE)
		SetGlobalTimer("d2regen","LOCALS",60)
		Continue()
END

IF
	!GlobalTimerNotExpired("d2reset","LOCALS")
	!GlobalTimerNotExpired("d2randomwalk","LOCALS")
	!See([GOODCUTOFF])
THEN
	RESPONSE #100
		SetGlobalTimer("d2balordispel","LOCALS",0)
		SetGlobalTimer("d2banish","LOCALS",0)
		SetGlobalTimer("d2unholyword","LOCALS",0)
		SetGlobalTimer("d2balorconfuse","LOCALS",0)
		ApplySpell(Myself,WIZARD_STONE_SKIN)
		SetGlobalTimer("d2reset","LOCALS",30)
		Continue()
END

IF
	InMyArea(LastSummonerOf())
	!GlobalTimerNotExpired("d2randomwalk","LOCALS")
	!See([GOODCUTOFF])
THEN
	RESPONSE #100
		SetGlobalTimer("d2randomwalk","LOCALS",12)
		MoveToObject(LastSummonerOf())
END

IF
	!GlobalTimerNotExpired("d2randomwalk","LOCALS")
	!See([GOODCUTOFF])
THEN
	RESPONSE #100
		SetGlobalTimer("d2randomwalk","LOCALS",12)
		MoveToObject(Player1)
END