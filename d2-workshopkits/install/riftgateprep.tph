/*
spell arrays/functions for later patching
used for: Riftgate, Nurse, Drifter, dragon scales (restrictarmor.tph)
*/


OUTER_SPRINT spellitemkit @40115
PRINT @12
ACTION_IF ((IS_AN_INT silentmode) AND (silentmode > 0)) BEGIN SILENT END


//
//base game spells (set list to always include)

//insect spells
ACTION_FOR_EACH spell IN ~SPPR319~ ~SPPR517~ ~SPPR717~ BEGIN
  OUTER_TEXT_SPRINT $d2riftgate(~%spell%~) ~insect~    // ~%spell%~ => insect
END

//earthquake spells
ACTION_FOR_EACH spell IN ~SPPR720~ ~ohbquake~ ~SPOGRE01~ BEGIN
  OUTER_TEXT_SPRINT $d2riftgate(~%spell%~) ~quake~     // ~%spell%~ => quake
END

//dispel
ACTION_FOR_EACH spell IN
  ~SPWI326~ ~SPWI302~ ~SPPR303~          // spells
//  ~AX1H07~ ~SW2H10~ ~SW2H19~ ~STAF11~    // items
BEGIN
  OUTER_TEXT_SPRINT $d2riftgate(~%spell%~) ~dispel~    // ~%spell%~ => dispel
END

//breach + lower resist
ACTION_FOR_EACH spell IN ~SPWI513~ ~SPWI514~ ~SPWI805~ BEGIN
  OUTER_TEXT_SPRINT $d2riftgate(~%spell%~) ~breach~    // ~%spell%~ => breach
END

//vorpal
ACTION_FOR_EACH spell IN
  ~ax1h15~ ~halb11~ ~SW2H15~ ~planetar~ ~devaevil~
  ~gith~ ~balor~ ~FINSOL01~ ~FINSOL02~ ~ohbwing~ ~vorpal~
BEGIN
  OUTER_TEXT_SPRINT $d2riftgate(~%spell%~) ~vorpal~    // ~%spell%~ => vorpal
END

//otiluke
ACTION_FOR_EACH spell IN
  ~spwi413~ ~spcl415~ ~SPWI853~ ~SPIN708~ ~SPIN853~ ~SPIN769~
BEGIN
  OUTER_TEXT_SPRINT $d2riftgate(~%spell%~) ~otiluke~    // ~%spell%~ => otiluke
END

//stinking cloud, cloudkill, death fog
ACTION_FOR_EACH spell IN
  ~SPWI213~ ~SPWI502~ ~SPWI614~ ~SPIN979~
BEGIN
  OUTER_TEXT_SPRINT $d2riftgate(~%spell%~) ~cloud~    // ~%spell%~ => cloud
END

//entangle hit effects
ACTION_FOR_EACH spell IN
  ~SW1H58~ ~SW1H59~ ~MOUND~ ~HGNYA01~ ~KUOSPER~
  ~bdbow06~ ~bdshamb~ ~talongf~ ~holdfst~ ~gasp~
BEGIN
  OUTER_TEXT_SPRINT $d2riftgate(~%spell%~) ~entangle~    // ~%spell%~ => entangle
END



//
//get spell info
COPY_EXISTING - ~SPPR319.spl~  ~~
  READ_LONG  0x8  insectspell_name
  READ_SHORT 0x98 insectspell_proj

COPY_EXISTING - ~SPPR517.spl~  ~~
  READ_LONG  0x8  insectswarm_name
  READ_SHORT 0x98 insectswarm_proj

COPY_EXISTING - ~SPPR720.spl~  ~~
  READ_LONG  0x8  earthquake_name
  READ_SHORT 0x98 earthquake_proj    // not unique

COPY_EXISTING - ~SPWI326.spl~  ~~  READ_LONG  0x8  dispel_name
COPY_EXISTING - ~SPWI302.spl~  ~~  READ_LONG  0x8  dispel2_name
COPY_EXISTING - ~SPWI513.spl~  ~~  READ_LONG  0x8  breach_name
COPY_EXISTING - ~SPWI514.spl~  ~~  READ_LONG  0x8  lowerresist_name
COPY_EXISTING - ~SPWI805.spl~  ~~  READ_LONG  0x8  pierceshield_name

COPY_EXISTING - ~SPWI818.spl~  ~~  READ_LONG  0x8  bigby8_name
COPY_EXISTING - ~SPWI918.spl~  ~~  READ_LONG  0x8  bigby9_name
COPY_EXISTING - ~SPWI413.spl~  ~~  READ_LONG  0x8  otiluke_name

COPY_EXISTING - ~SPWI213.spl~  ~~    // stinking cloud
  READ_LONG  0x8  stinkcloud_name
  READ_SHORT 0x98 stinkcloud_proj
COPY_EXISTING - ~SPWI502.spl~  ~~    // cloudkill
  READ_LONG  0x8  cloudkill_name
  READ_SHORT 0x98 cloudkill_proj
COPY_EXISTING - ~SPWI614.spl~  ~~    // death fog
  READ_LONG  0x8  deathfog_name
  READ_SHORT 0x98 deathfog_proj
COPY_EXISTING - ~SPIN979.spl~  ~~    // golem cloud
  READ_LONG  0x8  golemcloud_name
  READ_SHORT 0x98 golemcloud_proj



//
//add spells to d2wkspellprep (excludes from mass spell check)

OUTER_TEXT_SPRINT newfile  ~d2wkspellprep.txt~
ACTION_IF !(FILE_EXISTS ~override/%newfile%~) BEGIN
<<<<<<<< .../temp/newfile
>>>>>>>>
COPY ~.../temp/newfile~  ~override/%newfile%~
END

ACTION_IF !(RESOURCE_CONTAINS ~%newfile%~  ~_SPPR517_~) BEGIN
  ACTION_PHP_EACH d2riftgate AS spell => type BEGIN  APPEND ~%newfile%~  ~_%spell%_~ END
END



//
//scan spells + add to array
COPY_EXISTING_REGEXP - GLOB ~.*\.SPL~  ~~
  PATCH_IF (!(RESOURCE_CONTAINS ~%newfile%~  ~_%SOURCE_RES%_~) AND
            !(~%SOURCE_RES%~ STR_EQ ~SPPR728~) AND    // skip implosion
            !(~%SOURCE_RES%~ STR_EQ ~SAREVEFF~)       // skip Sarevok special
  ) BEGIN

    PATCH_IF (SOURCE_SIZE > 0x99) BEGIN    // skip if no headers
      READ_LONG  0x8  splname
      READ_SHORT 0x98 splproj
      SET check_opcodes = 1
      SET quake_opcode  = 0
      SET damage_opcode = 0
      SET move_opcode   = 0
      SET move2_opcode  = 0
      SET sleep_opcode  = 0

      // match name or projectile (catch clones of spells)
      PATCH_IF ((splname = insectspell_name) OR (splproj = insectspell_proj)) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~insect~    // ~%SOURCE_RES%~ => insect
        SET check_opcodes = 0
      END
      PATCH_IF ((splname = insectswarm_name) OR (splproj = insectswarm_proj)) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~insect~    // ~%SOURCE_RES%~ => insect
        SET check_opcodes = 0
      END
      PATCH_IF (splname = earthquake_name) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~quake~    // ~%SOURCE_RES%~ => quake
        SET check_opcodes = 0
      END

      // stinking cloud, cloudkill, death fog
      PATCH_IF ((splname = stinkcloud_name) OR (splproj = stinkcloud_proj)) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~cloud~    // ~%SOURCE_RES%~ => cloud
        SET check_opcodes = 0
      END
      PATCH_IF ((splname = cloudkill_name) OR (splproj = cloudkill_proj)) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~cloud~    // ~%SOURCE_RES%~ => cloud
        SET check_opcodes = 0
      END
      PATCH_IF ((splname = deathfog_name) OR (splproj = deathfog_proj)) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~cloud~    // ~%SOURCE_RES%~ => cloud
        SET check_opcodes = 0
      END
      PATCH_IF ((splname = golemcloud_name) OR (splproj = golemcloud_proj)) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~cloud~    // ~%SOURCE_RES%~ => cloud
        SET check_opcodes = 0
      END

      // dispel, breach, lower resist
      PATCH_IF (splname = dispel_name) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~dispel~    // ~%SOURCE_RES%~ => dispel
        SET check_opcodes = 0
      END
      PATCH_IF (splname = dispel2_name) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~dispel~    // ~%SOURCE_RES%~ => dispel
        SET check_opcodes = 0
      END
      PATCH_IF (splname = breach_name) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~breach~    // ~%SOURCE_RES%~ => breach
        SET check_opcodes = 0
      END
      PATCH_IF (splname = lowerresist_name) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~breach~    // ~%SOURCE_RES%~ => breach
        SET check_opcodes = 0
      END
      PATCH_IF (splname = pierceshield_name) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~breach~    // ~%SOURCE_RES%~ => breach
        SET check_opcodes = 0
      END

      // bigby, otiluke
      PATCH_IF (splname = bigby8_name) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~bigby~    // ~%SOURCE_RES%~ => bigby
        SET check_opcodes = 0
      END
     PATCH_IF (splname = bigby9_name) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~bigby~    // ~%SOURCE_RES%~ => bigby
        SET check_opcodes = 0
      END
      PATCH_IF (splname = otiluke_name) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~otiluke~    // ~%SOURCE_RES%~ => otiluke
        SET check_opcodes = 0
      END

      PATCH_IF (check_opcodes = 1) BEGIN
        GET_OFFSET_ARRAY spl_headers  SPL_V10_HEADERS
        PHP_EACH spl_headers AS num => header_off BEGIN                    // array of headers
          GET_OFFSET_ARRAY2 spl_effects header_off SPL_V10_HEAD_EFFECTS
          PHP_EACH spl_effects AS num => effect_off BEGIN                  // array of effects on each header
            PATCH_IF (quake_opcode=0 OR damage_opcode=0) BEGIN
              READ_SHORT effect_off opcode                                 // opcode
              PATCH_IF (opcode = 269) BEGIN SET quake_opcode  = 1 END
              PATCH_IF (opcode = 12 ) BEGIN SET damage_opcode = 1 END
              PATCH_IF (opcode = 126) BEGIN SET move_opcode   = 1 END
              PATCH_IF (opcode = 176) BEGIN SET move2_opcode  = 1 END
              PATCH_IF (opcode = 39)  BEGIN SET sleep_opcode  = 1 END
            END
          END
        END
      END

      // check opcodes
      PATCH_IF (quake_opcode=1) BEGIN
        PATCH_IF (damage_opcode=1 OR sleep_opcode=1) BEGIN
          TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~quake~    // ~%SOURCE_RES%~ => quake
        END
      END
      PATCH_IF (damage_opcode=1 AND quake_opcode=0) BEGIN
        PATCH_IF (move_opcode=1 OR move2_opcode=1) BEGIN
          TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~cloud~    // ~%SOURCE_RES%~ => cloud
        END
      END

    END
  END
// end inlined COPY



//
//scan items + add to array
COPY_EXISTING_REGEXP - GLOB ~.*\.ITM~  ~~
  PATCH_IF (!(RESOURCE_CONTAINS ~%newfile%~  ~_%SOURCE_RES%_~) AND
            !(~%SOURCE_RES%~ STR_EQ ~REAVER~)    // skip IWD weapon
  ) BEGIN

    PATCH_IF (SOURCE_SIZE > 0xa8) BEGIN    // skip if no header (0 chance it's a weapon)
      SET kill_opcode      = 0
      SET web_opcode       = 0
      SET grease_overlay   = 0
      SET entangle_overlay = 0

      // check dragon scales (for restrictarmor.tph)
      READ_LONG  0x8 stringref    // unidentified name (as string ref)
      PATCH_IF (stringref=39544 OR stringref=40636 OR stringref=5394) BEGIN
        PATCH_IF !(FILE_CONTAINS ~%functions%/d2_restrict_armor.tpa~ ~%SOURCE_RES%~) BEGIN
          READ_ASCII 0x22 itemtype (2) NULL
          PATCH_IF (~%itemtype%~ STR_EQ ~4A~) BEGIN    // if plate visuals
            TEXT_SPRINT $d2_scalearmor_array(~%SOURCE_RES%~) ~dragonplate~    // ~%SOURCE_RES%~ => dragonplate
          END ELSE BEGIN
            TEXT_SPRINT $d2_scalearmor_array(~%SOURCE_RES%~) ~dragonscale~    // ~%SOURCE_RES%~ => dragonscale
          END
        END
      END

      GET_OFFSET_ARRAY itm_headers  ITM_V10_HEADERS
      PHP_EACH itm_headers AS num => header_off BEGIN                    // array of headers
        GET_OFFSET_ARRAY2 itm_effects header_off ITM_V10_HEAD_EFFECTS
        PHP_EACH itm_effects AS num => effect_off BEGIN                  // array of effects on each header
          PATCH_IF (kill_opcode = 0) BEGIN
            READ_SHORT effect_off opcode                                 // opcode
            PATCH_IF (opcode = 13)  BEGIN  SET kill_opcode      = 1 END
            PATCH_IF (opcode = 55)  BEGIN  SET kill_opcode      = 1 END
            PATCH_IF (opcode = 157) BEGIN  SET web_opcode       = 1 END
            PATCH_IF (opcode = 158) BEGIN  SET grease_overlay   = 1 END
            PATCH_IF (opcode = 154) BEGIN  SET entangle_overlay = 1 END
          END
        END
      END

      // add to arrays
      PATCH_IF (kill_opcode = 1) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~vorpal~    // ~%SOURCE_RES%~ => vorpal
      END
      PATCH_IF (web_opcode=1 OR grease_overlay=1 OR entangle_overlay=1) BEGIN
        TEXT_SPRINT $d2riftgate(~%SOURCE_RES%~) ~entangle~    // ~%SOURCE_RES%~ => entangle
      END

    END
  END
// end inlined COPY



//
//functions
DEFINE_PATCH_FUNCTION insect_immunity INT_VAR timing=9 duration=0 BEGIN
  PHP_EACH d2riftgate AS spell => type BEGIN
    PATCH_IF (~%type%~ STR_EQ ~insect~) BEGIN
      LPF d2_remove_effects_on_cast STR_VAR resource=~%spell%~ insert=last END
      LPF d2_protect_on_castequip INT_VAR timing=timing duration=duration STR_VAR resource=~%spell%~ END
    END
  END
END

DEFINE_PATCH_FUNCTION riftgate_immunity STR_VAR block = ~quake bigby otiluke cloud entangle~ BEGIN
  PHP_EACH d2riftgate AS spell => type BEGIN
    PATCH_IF !(~%block%~ STRING_CONTAINS_REGEXP ~%type%~) BEGIN
      LPF d2_protect_on_castequip INT_VAR timing=9 STR_VAR resource=~%spell%~ END
    END
  END
END

DEFINE_PATCH_FUNCTION dispel_immunity INT_VAR block_opcode=1 BEGIN    // block_opcode = dispel opcode
  PHP_EACH d2riftgate AS spell => type BEGIN
    PATCH_IF (~%type%~ STR_EQ ~dispel~) BEGIN
      LPF d2_protect_on_castequip INT_VAR timing=9 STR_VAR resource=~%spell%~ END
    END
  END
  PATCH_IF (block_opcode > 0) BEGIN
    SET dispel_string=25820
    PATCH_IF (GAME_IS ~bg2ee eet~)  BEGIN  SET dispel_string=14056  END
    PATCH_IF (GAME_IS ~iwdee~)      BEGIN  SET dispel_string=35499  END
    LPF CLONE_EFFECT INT_VAR check_headers=0 multi_match=1 opcode=101 parameter2=58 STR_VAR insert=last END
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=58 opcode=267 parameter1=dispel_string END
  END
END

DEFINE_PATCH_FUNCTION breach_immunity INT_VAR block_opcode=1 BEGIN    // block_opcode = magic resist opcode
  PHP_EACH d2riftgate AS spell => type BEGIN
    PATCH_IF (~%type%~ STR_EQ ~breach~) BEGIN
      LPF d2_protect_on_castequip INT_VAR timing=9 STR_VAR resource=~%spell%~ END
    END
  END
  PATCH_IF (block_opcode > 0) BEGIN
    LPF CLONE_EFFECT INT_VAR check_headers=0 multi_match=1 opcode=101 parameter2=166 STR_VAR insert=last END
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=166 opcode=169 parameter2=106 END
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=166 opcode=169 parameter2=63 END
  END
END

DEFINE_PATCH_FUNCTION vorpal_immunity_npc BEGIN
  PHP_EACH d2riftgate AS spell => type BEGIN
    PATCH_IF (~%type%~ STR_EQ ~vorpal~) BEGIN
      LPF d2_protect_on_castequip INT_VAR timing=9 STR_VAR resource=~%spell%~ END
    END
  END
END


//
//for Autoimmune (blocked by Bigby or Otiluke)
DEFINE_ACTION_FUNCTION bigby_blocked STR_VAR resource = ~~ BEGIN
  ACTION_PHP_EACH d2riftgate AS spell => type BEGIN
    ACTION_IF (~%type%~ STR_EQ ~bigby~) BEGIN
      ACTION_FOR_EACH spell2 IN ~%spell%~ ~%spell%A~ BEGIN
        COPY_EXISTING ~%spell2%.spl~  ~override~
          LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=39 opcode=318 parameter2=0 special=0 STR_VAR resource=~%resource%~ END
        IF_EXISTS BUT_ONLY
      END
    END
  END
END

DEFINE_ACTION_FUNCTION otiluke_blocked STR_VAR resource = ~~ BEGIN
  ACTION_PHP_EACH d2riftgate AS spell => type BEGIN
    ACTION_IF (~%type%~ STR_EQ ~otiluke~) BEGIN
      ACTION_FOR_EACH spell2 IN ~%spell%~ ~%spell%A~ BEGIN
        COPY_EXISTING ~%spell2%.spl~  ~override~
          LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=185 opcode=318 parameter2=0 special=0 STR_VAR resource=~%resource%~ END
        IF_EXISTS BUT_ONLY
      END
    END
  END
END


//
//function for d2!web
DEFINE_PATCH_FUNCTION entangle_hit_effects INT_VAR timing=9 duration=0 STR_VAR mode=remove BEGIN
  PHP_EACH d2riftgate AS item => type BEGIN
    PATCH_IF !(~entangle ground206~ STRING_CONTAINS_REGEXP ~%type%~) BEGIN
      PATCH_IF (FILE_EXISTS_IN_GAME ~%item%.itm~) BEGIN
        PATCH_IF (~%mode%~ STR_EQ ~remove~) BEGIN
          LPF d2_remove_effects_on_cast STR_VAR resource=~%item%~ END
        END
        PATCH_IF (~%mode%~ STR_EQ ~protect~) BEGIN
          LPF d2_protect_on_castequip INT_VAR timing=timing duration=duration STR_VAR resource=~%item%~ END
        END
      END
    END
  END
END


OUTER_SET riftgateprep = 1


/*
*/