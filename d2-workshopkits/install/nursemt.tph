//Nurse MT


ACTION_IF (nonursemt = 0) BEGIN


INCLUDE ~%MOD_FOLDER%/lib/a7#add_kit_ex.tpa~    // weidu error if this file isn't included here


//Remove specialist bonus slots
COPY ~%passives%/fewerspells1.SPL~  ~override/%e4%.SPL~
COPY ~%passives%/opcode206.SPL~     ~override/%e5%.SPL~    // only relevant for 5e casting mod
  LPF ALTER_EFFECT STR_VAR resource=~d5z0slt~ END
  LPF CLONE_EFFECT STR_VAR resource=~D5ZMSLT~ END


//Ability tables (don't include base class abilities)
COPY ~%files%/kit6.2DA~  ~override/%nurse%M.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e4%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e5%~ END

COPY ~%files%/kit0.2DA~  ~override/%nurse%T.2DA~
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 multi=1 increment=5 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=5 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=11 multi=1 increment=4 limit=19 STR_VAR resource=~%s2%~ END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 13
    mixed = RESOLVE_STR_REF (@101601)
    lower = RESOLVE_STR_REF (@101602)
    help = RESOLVE_STR_REF (@101603)
  STR_VAR
    kit_name = ~D2%kitname%MT~
    clab_base_m = ~%nurse%M.2DA~    // -1 spell slots added to Mage table
    clab_base_t = ~%nurse%T.2DA~    // kit abilities added to Thief table
    clsrcreq = ~1 1 1 1 1 1 1~      // Usable by all races, if base class is usable
    thiefskl = ~40 15~              // start with 40, gain 15 per level
    clascolr = ~25 28 57 2 24~      // inquisitor colors
  RET 
    kit_id
END


//Usability - no studded leather or hide armor
COPY_EXISTING_REGEXP GLOB ~.*\.ITM~  ~override~
  READ_LONG  0x8 stringref    // check unidentified name
  PATCH_IF ((stringref = 6664) OR     // if Studded Leather
            (stringref = 6665) OR 
            (stringref = 6666) OR
            (stringref = 22714) OR    // if hide armor
            (stringref = 22715) OR
            (stringref = 40488)
    ) BEGIN
    LPF ADD_ITEM_EQEFFECT
      INT_VAR
        opcode = 319
        target = 1
        parameter1 = IDS_OF_SYMBOL (kit ~D2%kitname%MT~)
        parameter2 = 9
        timing = 2
    END
  END
BUT_ONLY


//Gains archer effects from items
OUTER_TEXT_SPRINT kit_group ~archer~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%MT~

ACTION_PHP_EACH item_kit_effects AS item => class BEGIN
  ACTION_IF !(~%kit_group%~ STRING_CONTAINS_REGEXP ~%class%~) BEGIN
    ACTION_FOR_EACH res IN ~SPL~ ~ITM~ BEGIN
      ACTION_IF (FILE_EXISTS_IN_GAME ~%item%.%res%~) BEGIN
        COPY_EXISTING ~%item%.%res%~  ~override~
          PHP_EACH kit_ids_array AS class2 => eff_to_copy BEGIN
            PATCH_IF (~%class%~ STR_EQ ~%class2%~) BEGIN
              LPF CLONE_EFFECT 
                INT_VAR 
                  silent            = 1
                  match_opcode      = 177 
                  match_parameter1  = %eff_to_copy%
                  match_parameter2  = 9 
                  parameter1        = IDS_OF_SYMBOL (kit ~%ids_name%~)
              END
            END
          END
        BUT_ONLY
      END
    END
  END
END


END


/*
*/