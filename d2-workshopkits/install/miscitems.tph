/*
This file adds effects for items from the "Misc items" component.
Will also patch certain kit abilities, but only if the kit is installed.

Caryll runes are in a separate file (miscitemsrunes.tph).
*/


//Crowfeather Cloak (cloak)
COPY ~%files%/crowfeather.ITM~  ~override/%m1%.ITM~
  SAY NAME1 @70105 SAY UNIDENTIFIED_DESC @70205
  SAY NAME2 @70105 SAY DESC @70205
  WRITE_ASCII 0x3a ~IBDCLK05~ #8    // icon
  WRITE_ASCII 0x58 ~CDCLCK01~ #8    // description image

ACTION_IF (FILE_EXISTS ~override/%visceralattack%.SPL~) BEGIN

  COPY_EXISTING ~%visceralattack%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%m1%a~ END
  BUT_ONLY

  COPY_EXISTING ~%visceralremove%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%m1%a~ END
  BUT_ONLY

  COPY ~%abilities%/castspellself.EFF~  ~override/%m1%a.EFF~
    WRITE_ASCII 0x30 ~%m1%a~ #8
  COPY_EXISTING ~%visceralattack%.SPL~  ~override/%m1%a.SPL~
    LPF DELETE_EFFECT INT_VAR match_opcode=142 END    // remove portrait icon
    LPF DELETE_EFFECT INT_VAR match_opcode=50 END     // remove color pulse
    LPF DELETE_EFFECT INT_VAR match_opcode=174 END    // remove sounds
    WRITE_LONG 0x8 ~-1~                               // remove spell name

  COPY_EXISTING ~%m1%.ITM~  ~override~    // add repeating effect
    LPF CLONE_EFFECT 
      INT_VAR multi_match=1 opcode=272 parameter1=3 parameter2=3
      STR_VAR resource=~%m1%e~
    END
  BUT_ONLY

  COPY ~%abilities%/castspellself.EFF~  ~override/%m1%e.EFF~
    WRITE_ASCII 0x30 ~%m1%e~ #8
  COPY ~%abilities%/useeffonkit.SPL~    ~override/%m1%e.SPL~
    LPF CLONE_EFFECT 
      INT_VAR multi_match=1 opcode=206 timing=0 duration=2 probability1=100 
      STR_VAR resource=~%m1%e~ insert=~last~ 
    END
    LPF ALTER_EFFECT 
      INT_VAR match_opcode=177 probability1=5 parameter1=IDS_OF_SYMBOL (kit ~D2CROWHUNTER~)
      STR_VAR resource=~%m1%a~
    END
    PATCH_IF (crowhunterft > 0) BEGIN
      LPF CLONE_EFFECT 
        INT_VAR multi_match=1 match_opcode=177 parameter1=IDS_OF_SYMBOL (kit ~D2CROWHUNTERFT~)
      END
    END
    PATCH_IF (crowhunterfmt > 0) BEGIN
      LPF CLONE_EFFECT 
        INT_VAR multi_match=1 match_opcode=177 parameter1=IDS_OF_SYMBOL (kit ~D2CROWHUNTERFMT~)
      END
    END
    PATCH_IF (crowhuntermt > 0) BEGIN
      LPF CLONE_EFFECT 
        INT_VAR multi_match=1 match_opcode=177 parameter1=IDS_OF_SYMBOL (kit ~D2CROWHUNTERMT~)
      END
    END

END



//Etherbomb Songbook (quick item)
COPY ~%icons%/etherbomb.BAM~  ~override/%m2%.BAM~
COPY ~%files%/etherbomb.ITM~  ~override/%m2%.ITM~
  SAY NAME1 @70106 SAY UNIDENTIFIED_DESC @70206
  SAY NAME2 @70106 SAY DESC @70206
  WRITE_ASCII 0x3a ~%m2%~ #8       // icon
  WRITE_ASCII 0x76 ~%m2%~ #8       // icon (ability)
  WRITE_ASCII 0x58 ~CBOOK89~ #8    // description image

  LPF ALTER_EFFECT INT_VAR match_opcode=326 parameter2=~%lore80%~ STR_VAR resource=~%m2%e~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 parameter2=~%lore79%~ STR_VAR resource=~%m2%s~ END

COPY ~%abilities%/powderkegcast.SPL~  ~override/%m2%e.SPL~
  SAY NAME1 @615
  LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%m2%f~ END    // hits enemies
  LPF DELETE_EFFECT INT_VAR match_opcode=363 END

COPY ~%abilities%/powderkegcast.SPL~  ~override/%m2%s.SPL~
  SAY NAME1 @615
  LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%m2%t~ END    // hits all
  LPF DELETE_EFFECT INT_VAR match_opcode=363 END

COPY - ~%files%/etherbomb.PRO~        ~%workspace%/%m2%p.PRO~               // projectile
ADD_PROJECTILE ~%workspace%/%m2%p.PRO~

COPY ~%files%/etherbomb.SPL~          ~override/%m2%f.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%m2%f~ END    // hits enemies
  WRITE_SHORT 0x98 ~%m2%p~    // projectile

COPY ~%files%/etherbomb.SPL~          ~override/%m2%t.SPL~
  LPF DELETE_EFFECT INT_VAR match_opcode=318 END                            // hits all
  WRITE_SHORT 0x98 ~%m2%p~    // projectile

ACTION_IF (FILE_EXISTS ~override/%powderkeg%.2DA~) BEGIN

  COPY_EXISTING ~%cleareffects%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%m2%y~ END
  BUT_ONLY

  COPY_EXISTING ~%m2%.ITM~  ~override~
    LPF CLONE_EFFECT 
      INT_VAR multi_match=1 opcode=177 parameter1=IDS_OF_SYMBOL (kit ~D2POWDERKEG~) parameter2=9
      STR_VAR resource=~%m2%u~
    END
  BUT_ONLY

COPY ~%abilities%/castspellself.EFF~   ~override/%m2%u.EFF~
  WRITE_ASCII 0x30 ~%m2%u~ #8
COPY ~%abilities%/changebardsong.SPL~  ~override/%m2%u.SPL~    // change Bard Song
  LPF DELETE_EFFECT INT_VAR check_headers=0 END
  LPF DELETE_EFFECT INT_VAR match_opcode=174 END
  LPF ALTER_EFFECT INT_VAR match_opcode=251 STR_VAR resource=~%m2%v~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~%cleareffects%~ END

COPY ~%abilities%/powderkegdisable.SPL~    ~override/%m2%x.SPL~
COPY ~%abilities%/powderkegnosong.SPL~     ~override/%m2%y.SPL~
  LPF ALTER_EFFECT INT_VAR match_timing=4 STR_VAR resource=~%m2%v~ END
COPY ~%abilities%/powderkegcast.SPL~       ~override/%m2%v.SPL~
  SAY NAME1 @615
  LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%m2%f~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=146 STR_VAR resource=~%m2%x~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=146 STR_VAR resource=~%m2%y~ END

END



//Ether Booster (amulet)
COPY ~%files%/etherbooster.EFF~     ~override/%m3%e.EFF~
COPY ~%files%/etherbooster.ITM~     ~override/%m3%.ITM~
  SAY NAME1 @70107 SAY UNIDENTIFIED_DESC @70207
  SAY NAME2 @70107 SAY DESC @70207
  WRITE_ASCII 0x3a ~IAMUL24~ #8       // icon
  WRITE_ASCII 0x58 ~PAMUL26~ #8    // description image

ACTION_IF (FILE_EXISTS ~override/%unleashmagic%.SPL~) BEGIN

  COPY_EXISTING ~%unleashmagic%.SPL~  ~override~
    LPF CLONE_EFFECT 
      INT_VAR check_globals=0 multi_match=1 match_timing=0 opcode=272 parameter1=3 parameter2=3 duration=19
      STR_VAR resource=~%unleashmagic%e~
    END
    LPF CLONE_EFFECT INT_VAR match_opcode=272 opcode=177 timing=1 parameter1=0 parameter2=2 END
  BUT_ONLY

  COPY ~%abilities%/castspellself.EFF~  ~override/%unleashmagic%e.EFF~
    WRITE_ASCII 0x30 ~%unleashmagic%e~ #8
  COPY ~%abilities%/useeffonkit.SPL~    ~override/%unleashmagic%e.SPL~
    LPF ALTER_EFFECT 
      INT_VAR parameter1=IDS_OF_SYMBOL (kit ~D2WARMAGICIAN~)
      STR_VAR resource=~%m3%s~ 
    END
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%m3%e~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=321 parameter2=0 STR_VAR resource=~%unleashmagic%e~ END

/*
NOTE: op182 applies resource 2 UNLESS resource1 is equipped (only works with amulets)
*/

  COPY ~%abilities%/fireweaponbadge.EFF~     ~override/%m3%s.EFF~
    WRITE_ASCII 0x30 ~%m3%~ #8
    WRITE_ASCII 0x70 ~%m3%x~ #8
  COPY ~%abilities%/castspellself.EFF~       ~override/%m3%x.EFF~
    WRITE_ASCII 0x30 ~%m3%x~ #8
  COPY ~%abilities%/removespelleffects.SPL~  ~override/%m3%x.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%unleashmagic%e~ END

END



//Prisoner's Chain (ring)
COPY ~%icons%/prisonchain.BAM~  ~override/%m4%.BAM~
COPY ~%files%/prisonchain.ITM~  ~override/%m4%.ITM~
  SAY NAME1 @70108 SAY UNIDENTIFIED_DESC @70208
  SAY NAME2 @70108 SAY DESC @70208
  WRITE_ASCII 0x3a ~%m4%~ #8       // icon
  WRITE_ASCII 0x58 ~CRING29~ #8    // description image



//Cruciform (amulet)
COPY ~%icons%/cruciformb.BAM~  ~override/%m5%b.BAM~
COPY ~%icons%/cruciformc.BAM~  ~override/%m5%c.BAM~
COPY ~%files%/cruciform.ITM~   ~override/%m5%.ITM~
  SAY NAME1 @70109 SAY UNIDENTIFIED_DESC @70209
  SAY NAME2 @70109 SAY DESC @70209
  WRITE_ASCII 0x3a ~%m5%b~ #8    // icon
  WRITE_ASCII 0x58 ~%m5%c~ #8    // description image

ACTION_IF (FILE_EXISTS ~override/%bloodletter%.SPL~) BEGIN

  COPY_EXISTING ~%bloodletter%.SPL~  ~override~
    LPF CLONE_EFFECT 
      INT_VAR check_globals=0 multi_match=1 match_timing=0 opcode=272 parameter1=3 parameter2=3 duration=25
      STR_VAR resource=~%bloodletter%e~
    END
    LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=272 STR_VAR resource=~%bloodletter%f~ END
    LPF CLONE_EFFECT INT_VAR match_opcode=272 opcode=177 timing=1 parameter1=0 parameter2=2 END
  BUT_ONLY

  COPY ~%abilities%/castspellself.EFF~  ~override/%bloodletter%e.EFF~    // +1 bleed damage
    WRITE_ASCII 0x30 ~%bloodletter%e~ #8
  COPY ~%abilities%/useeffonkit.SPL~    ~override/%bloodletter%e.SPL~
    LPF ALTER_EFFECT 
      INT_VAR parameter1=IDS_OF_SYMBOL (kit ~D2CONFESSOR~)
      STR_VAR resource=~%m5%s~ 
    END
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%m5%e~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=321 parameter2=0 STR_VAR resource=~%bloodletter%e~ END

  COPY ~%abilities%/castspellself.EFF~  ~override/%bloodletter%f.EFF~    // +1 bleed damage
    WRITE_ASCII 0x30 ~%bloodletter%f~ #8
  COPY ~%abilities%/useeffonkit.SPL~    ~override/%bloodletter%f.SPL~
    LPF ALTER_EFFECT 
      INT_VAR parameter1=IDS_OF_SYMBOL (kit ~D2CONFESSOR~)
      STR_VAR resource=~%m5%s~ 
    END
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%m5%f~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=321 parameter2=0 STR_VAR resource=~%bloodletter%f~ END

/*
NOTE: op182 applies resource 2 UNLESS resource1 is equipped (only works with amulets)
*/

  COPY ~%abilities%/fireweaponbadge.EFF~     ~override/%m5%s.EFF~
    WRITE_ASCII 0x30 ~%m5%~ #8
    WRITE_ASCII 0x70 ~%m5%x~ #8
  COPY ~%abilities%/castspellself.EFF~       ~override/%m5%x.EFF~
    WRITE_ASCII 0x30 ~%m5%x~ #8
  COPY ~%abilities%/removespelleffects.SPL~  ~override/%m5%x.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%bloodletter%e~ END
    LPF CLONE_EFFECT STR_VAR resource=~%bloodletter%f~ END

  COPY ~%abilities%/hiteffectmelee.EFF~   ~override/%m5%e.EFF~
    WRITE_LONG 0x28 7
    WRITE_ASCII 0x30 ~%m5%a~ #8
  COPY ~%abilities%/hiteffectmelee.EFF~   ~override/%m5%f.EFF~
    WRITE_LONG 0x28 7
    WRITE_ASCII 0x30 ~%m5%b~ #8

  COPY_EXISTING ~%confessorbleed%.EFF~  ~override/%m5%a.EFF~
    WRITE_ASCII 0x30 ~%m5%a~ #8
  COPY_EXISTING ~%confessorbleed%.SPL~  ~override/%m5%a.SPL~
    LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%m5%a~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=206 STR_VAR resource=~%m5%a~ END
    LPF DELETE_EFFECT INT_VAR match_opcode=139 END

  COPY_EXISTING ~%confessorbleed%.EFF~  ~override/%m5%b.EFF~
    WRITE_ASCII 0x30 ~%m5%b~ #8
  COPY_EXISTING ~%confessorbleed%.SPL~  ~override/%m5%b.SPL~
    LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%m5%b~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=206 STR_VAR resource=~%m5%b~ END
    LPF DELETE_EFFECT INT_VAR match_opcode=139 END

END



//Golem Ring (ring)
COPY ~%icons%/ironflesh.BAM~  ~override/%m6%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@616) STR_VAR bam=~%m6%i~ RET icon END
COPY ~%icons%/golemring.BAM~  ~override/%m6%.BAM~
COPY ~%files%/golemring.ITM~  ~override/%m6%.ITM~
  SAY NAME1 @70110 SAY UNIDENTIFIED_DESC @70210
  SAY NAME2 @70110 SAY DESC @70210
  WRITE_ASCII 0x3a ~%m6%~ #8       // icon
  WRITE_ASCII 0x76 ~%m6%~ #8       // icon (ability)
  WRITE_ASCII 0x58 ~CRING26~ #8    // description image

  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@304) END    // "Activated Golem Ring"

  LPF cd_apply_batch STR_VAR array_name = cd_immunity_haste_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_slow_arrays END

  PATCH_IF (GAME_IS ~bgee iwdee~) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=267 END
    LPF d2_string_immunities END
  END
  ELSE BEGIN
    LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=40 opcode=267 parameter1=335 END
  END

  PATCH_IF (haste_immunity > 0) BEGIN 
    LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=16 opcode=328 parameter2=haste_immunity END
  END
  PATCH_IF (slow_immunity > 0) BEGIN 
    LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=40 opcode=328 parameter2=slow_immunity END
  END



//Calamity Ring (ring)
COPY ~%icons%/calamity.BAM~  ~override/%m7%.BAM~
COPY ~%files%/calamity.ITM~  ~override/%m7%.ITM~
  SAY NAME1 @70111 SAY UNIDENTIFIED_DESC @70211
  SAY NAME2 @70111 SAY DESC @70211
  WRITE_ASCII 0x3a ~%m7%~ #8       // icon
  WRITE_ASCII 0x58 ~CRING36~ #8    // description image



//Stone Ring (ring)
COPY ~%icons%/stonering.BAM~      ~override/%m8%.BAM~
COPY ~%files%/stoneringstun.EFF~  ~override/%m8%a.EFF~    // stun
COPY ~%files%/stoneringpush.EFF~  ~override/%m8%b.EFF~    // wing buffet
COPY ~%files%/stonering.ITM~      ~override/%m8%.ITM~
  SAY NAME1 @70112 SAY UNIDENTIFIED_DESC @70212
  SAY NAME2 @70112 SAY DESC @70212
  WRITE_ASCII 0x3a ~%m8%~ #8       // icon
  WRITE_ASCII 0x58 ~CRING30~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%m8%a~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=249 STR_VAR resource=~%m8%b~ END



//Flynn's Ring (ring)
COPY ~%files%/flynnsring.EFF~  ~override/%m9%a.EFF~    // block item effects
  WRITE_ASCII 0x30 ~%m9%~ #8
COPY ~%files%/flynnsring.EFF~  ~override/%m9%b.EFF~    // block spell effects
  WRITE_ASCII 0x30 ~%m9%s~ #8

COPY ~%icons%/flynnsring.BAM~  ~override/%m9%.BAM~
COPY ~%files%/flynnsring.ITM~  ~override/%m9%.ITM~
  SAY NAME1 @70113 SAY UNIDENTIFIED_DESC @70213
  SAY NAME2 @70113 SAY DESC @70213
  WRITE_ASCII 0x3a ~%m9%~ #8       // icon
  WRITE_ASCII 0x58 ~CRING06~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%m9%~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%m9%s~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=272 STR_VAR resource=~%m9%s~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=183 STR_VAR resource=~%m9%a~ END

COPY ~%abilities%/castspellself.EFF~  ~override/%m9%s.EFF~
  WRITE_ASCII 0x30 ~%m9%s~ #8
COPY ~%files%/flynnsring.SPL~  ~override/%m9%s.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%m9%s~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=183 STR_VAR resource=~%m9%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=206 STR_VAR resource=~%m9%s~ END



//Ether Limiter (amulet)
COPY ~%files%/etherlimiter.ITM~  ~override/%ma%.ITM~
  SAY NAME1 @70128 SAY UNIDENTIFIED_DESC @70228
  SAY NAME2 @70128 SAY DESC @70228
  WRITE_ASCII 0x3a ~IAMUL19~ #8    // icon
  WRITE_ASCII 0x58 ~CAMUL20~ #8    // description image

//Shockwave
ACTION_IF (FILE_EXISTS ~override/%shockwave%.SPL~) BEGIN

  COPY_EXISTING ~%shockwave%.SPL~  ~override~
    LPF ALTER_EFFECT STR_VAR match_resource=~%shockwaveeffects%~ resource=~%shockwave%e~ END
  BUT_ONLY

  COPY ~%abilities%/useeffonkit.SPL~  ~override/%shockwave%e.SPL~
    LPF ALTER_EFFECT 
      INT_VAR parameter1=IDS_OF_SYMBOL (kit ~D2WARHOUND~)
      STR_VAR resource=~%ma%s~ 
    END
    PATCH_IF (warhoundfm > 0) BEGIN
      LPF CLONE_EFFECT 
        INT_VAR parameter1=IDS_OF_SYMBOL (kit ~D2WARHOUNDFM~)
      END
    END
    LPF CLONE_EFFECT STR_VAR resource=~%ma%a~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=321 parameter2=0 STR_VAR resource=~%shockwave%e~ END

  COPY ~%abilities%/fireweaponbadge.EFF~     ~override/%ma%s.EFF~
    WRITE_ASCII 0x30 ~%ma%~ #8
    WRITE_ASCII 0x70 ~%ma%t~ #8
  COPY ~%abilities%/castspellself.EFF~       ~override/%ma%t.EFF~
    WRITE_ASCII 0x30 ~%ma%t~ #8
  COPY ~%abilities%/removespelleffects.SPL~  ~override/%ma%t.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%shockwave%e~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%ma%a~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=326 STR_VAR resource=~%shockwaveeffects%~ END

  COPY ~%abilities%/castspelltarget.EFF~  ~override/%ma%a.EFF~
    WRITE_ASCII 0x30 ~%ma%a~ #8
  COPY_EXISTING ~%shockwaveeffects%.SPL~  ~override/%ma%a.SPL~
    LPF ALTER_EFFECT STR_VAR match_resource=~%shockwaveblast%~ resource=~%ma%b~ END

  COPY ~%abilities%/castspelltarget.EFF~  ~override/%ma%b.EFF~
    WRITE_ASCII 0x30 ~%ma%b~ #8
  COPY_EXISTING ~%shockwaveblast%.SPL~    ~override/%ma%b.SPL~
    LPF DELETE_EFFECT INT_VAR match_parameter2=63 END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=318 timing=0 duration=0 parameter2=52 STR_VAR resource=~%ma%b~ END

  COPY_EXISTING ~%warhoundremove%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%ma%a~ END
  BUT_ONLY

END

//Incinerate
ACTION_IF (FILE_EXISTS ~override/%incinerate%.SPL~) BEGIN

  COPY_EXISTING ~%incinerate%.SPL~  ~override~
    LPF ALTER_EFFECT STR_VAR match_resource=~%incinerateeffects%~ resource=~%incinerate%e~ END
  BUT_ONLY

  COPY ~%abilities%/useeffonkit.SPL~  ~override/%incinerate%e.SPL~
    LPF ALTER_EFFECT 
      INT_VAR parameter1=IDS_OF_SYMBOL (kit ~D2WARHOUND~)
      STR_VAR resource=~%ma%u~ 
    END
    PATCH_IF (warhoundfm > 0) BEGIN
      LPF CLONE_EFFECT 
        INT_VAR parameter1=IDS_OF_SYMBOL (kit ~D2WARHOUNDFM~)
      END
    END
    LPF CLONE_EFFECT STR_VAR resource=~%ma%e~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=321 parameter2=0 STR_VAR resource=~%incinerate%e~ END

  COPY ~%abilities%/fireweaponbadge.EFF~     ~override/%ma%u.EFF~
    WRITE_ASCII 0x30 ~%ma%~ #8
    WRITE_ASCII 0x70 ~%ma%v~ #8
  COPY ~%abilities%/castspellself.EFF~       ~override/%ma%v.EFF~
    WRITE_ASCII 0x30 ~%ma%v~ #8
  COPY ~%abilities%/removespelleffects.SPL~  ~override/%ma%v.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%incinerate%e~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%ma%e~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=326 STR_VAR resource=~%incinerateeffects%~ END

  COPY ~%abilities%/castspelltarget.EFF~   ~override/%ma%e.EFF~
    WRITE_ASCII 0x30 ~%ma%e~ #8
  COPY_EXISTING ~%incinerateeffects%.SPL~  ~override/%ma%e.SPL~
    LPF ALTER_EFFECT STR_VAR match_resource=~%incinerateblast%~ resource=~%ma%f~ END

  COPY ~%abilities%/castspelltarget.EFF~  ~override/%ma%f.EFF~
    WRITE_ASCII 0x30 ~%ma%f~ #8
  COPY_EXISTING ~%incinerateblast%.SPL~    ~override/%ma%f.SPL~
    LPF DELETE_EFFECT INT_VAR match_parameter2=63 END
    LPF CLONE_EFFECT INT_VAR opcode=318 timing=0 dicenumber=0 dicesize=0 savingthrow=0 parameter2=52 special=0 STR_VAR resource=~%ma%f~ END

  COPY_EXISTING ~%warhoundremove%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%ma%e~ END
  BUT_ONLY

END



//Gold Scarab (amulet)
COPY ~%icons%/goldscarab.BAM~  ~override/%mb%.BAM~
COPY ~%files%/goldscarab.ITM~  ~override/%mb%.ITM~
  SAY NAME1 @70131 SAY UNIDENTIFIED_DESC @70231
  SAY NAME2 @70131 SAY DESC @70231
  WRITE_ASCII 0x3a ~%mb%~ #8       // icon
  WRITE_ASCII 0x58 ~CAMUL16~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=232 STR_VAR resource=~%mb%e~ END
//  PATCH_IF (GAME_IS ~iwdee~) BEGIN
//    WRITE_LONG 0x34 10000    // higher price for IWDEE
//  END

COPY ~%files%/goldscarab.SPL~  ~override/%mb%e.SPL~



//Winged Sword Insignia (ring)
COPY ~%icons%/wingedsword.BAM~  ~override/%mc%.BAM~
COPY ~%files%/wingedsword.ITM~  ~override/%mc%.ITM~
  SAY NAME1 @70132 SAY UNIDENTIFIED_DESC @70232
  SAY NAME2 @70132 SAY DESC @70232
  WRITE_ASCII 0x3a ~%mc%~ #8       // icon
  WRITE_ASCII 0x58 ~CRING38~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%mc%e~ END

COPY ~%abilities%/castspellself.EFF~  ~override/%mc%e.EFF~    // +1 damage bonus
  WRITE_ASCII 0x30 ~%mc%e~ #8
COPY ~%abilities%/damageup1.SPL~      ~override/%mc%e.SPL~
  LPF ALTER_EFFECT INT_VAR probability1=33 END



//Red Tearstone Ring (ring)
COPY ~%icons%/redtearstoneicon.BAM~      ~override/%md%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@70133) STR_VAR bam=~%md%i~ RET icon END

COPY ~%icons%/redtearstone.BAM~  ~override/%md%.BAM~
COPY ~%files%/redtearstone.ITM~  ~override/%md%.ITM~
  SAY NAME1 @70133 SAY UNIDENTIFIED_DESC @70233
  SAY NAME2 @70133 SAY DESC @70233
  WRITE_ASCII 0x3a ~%md%~ #8       // icon
  WRITE_ASCII 0x58 ~CMISC8O~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=232 STR_VAR resource=~%md%e~ END

COPY ~%files%/redtearstone.SPL~  ~override/%md%e.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%md%e~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END



//Hunter Bone (quick item)
COPY ~%icons%/hunterbone.BAM~      ~override/%me%.BAM~
COPY ~%icons%/hunterbonedesc.BAM~  ~override/%me%d.BAM~
COPY ~%files%/hunterbone.ITM~      ~override/%me%.ITM~
  SAY NAME1 @70134 SAY UNIDENTIFIED_DESC @70234
  SAY NAME2 @70134 SAY DESC @70234
  WRITE_ASCII 0x3a ~%me%~ #8     // icon
  WRITE_ASCII 0x76 ~%me%~ #8     // icon (ability)
  WRITE_ASCII 0x58 ~%me%d~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%me%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%me%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@312) END    // "Activated Hunter Bone"
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=321 STR_VAR resource=~%me%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=321 STR_VAR resource=~%me%b~ END

//clone Blur
COPY_EXISTING ~SPWI201.SPL~  ~override/%me%a.SPL~
  LPF ALTER_SPELL_HEADER  INT_VAR header=1 new_header_type=0 END
  LPF DELETE_SPELL_HEADER INT_VAR header_type=1 END
  LPF DELETE_SPELL_HEADER INT_VAR header_type=2 END
  LPF ALTER_SPELL_HEADER  INT_VAR new_header_type=1 END

  LPF d2_subspell_remove_effects END
  LPF DELETE_EFFECT INT_VAR match_opcode=139 END
  LPF DELETE_EFFECT INT_VAR match_opcode=321 END
  LPF DELETE_EFFECT INT_VAR match_opcode=174 END
  LPF ALTER_EFFECT INT_VAR match_timing=0 duration=20 END

//clone Haste
COPY_EXISTING ~SPWI305.SPL~  ~override/%me%b.SPL~
  LPF ALTER_SPELL_HEADER  INT_VAR header=1 new_header_type=0 END
  LPF DELETE_SPELL_HEADER INT_VAR header_type=1 END
  LPF DELETE_SPELL_HEADER INT_VAR header_type=2 END
  LPF ALTER_SPELL_HEADER  INT_VAR new_header_type=1 END

  LPF d2_subspell_remove_effects END
  LPF DELETE_EFFECT INT_VAR match_opcode=139 END
  LPF DELETE_EFFECT INT_VAR match_opcode=141 END
  LPF DELETE_EFFECT INT_VAR match_opcode=215 END
  LPF DELETE_EFFECT INT_VAR match_opcode=61 END
  LPF DELETE_EFFECT INT_VAR match_opcode=174 END
  LPF DELETE_EFFECT INT_VAR match_opcode=93 END
  LPF DELETE_EFFECT INT_VAR match_opcode=206 END
  LPF DELETE_EFFECT INT_VAR match_timing=4 END
  LPF ALTER_EFFECT INT_VAR match_timing=0 duration=20 END



//Jade Feather (amulet)
COPY ~%icons%/jadefeather.BAM~  ~override/%mf%.BAM~
COPY ~%files%/jadefeather.ITM~  ~override/%mf%.ITM~
  SAY NAME1 @70135 SAY UNIDENTIFIED_DESC @70235
  SAY NAME2 @70135 SAY DESC @70235
  WRITE_ASCII 0x3a ~%mf%~ #8        // icon
  WRITE_ASCII 0x58 ~BDCFEATH~ #8    // description image

  LPF cd_apply_batch STR_VAR array_name = cd_immunity_kill_target_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_slay_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_petrification_arrays END

  PATCH_IF (GAME_IS ~bgee iwdee~) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=267 END
    LPF d2_string_immunities END
  END

  PATCH_IF (death_immunity > 0) BEGIN 
    LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=13 opcode=328 parameter2=death_immunity END
  END

//Black Remedy
ACTION_IF (FILE_EXISTS ~override/%blackremedy%.SPL~) BEGIN

  COPY_EXISTING ~%blackremedy%.SPL~  ~override~
    LPF ALTER_EFFECT STR_VAR match_resource=~%blackremedyeffects%~ resource=~%blackremedy%e~ END
  BUT_ONLY

  COPY ~%abilities%/useeffonkit.SPL~  ~override/%blackremedy%e.SPL~
    LPF ALTER_EFFECT 
      INT_VAR parameter1=IDS_OF_SYMBOL (kit ~D2MEDIC~)
      STR_VAR resource=~%mf%s~ 
    END
    LPF CLONE_EFFECT STR_VAR resource=~%mf%a~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=321 parameter2=0 STR_VAR resource=~%blackremedy%e~ END

  COPY ~%abilities%/fireweaponbadge.EFF~     ~override/%mf%s.EFF~
    WRITE_ASCII 0x30 ~%mf%~ #8
    WRITE_ASCII 0x70 ~%mf%t~ #8
  COPY ~%abilities%/castspellself.EFF~       ~override/%mf%t.EFF~
    WRITE_ASCII 0x30 ~%mf%t~ #8
  COPY ~%abilities%/removespelleffects.SPL~  ~override/%mf%t.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%blackremedy%e~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%mf%a~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=326 STR_VAR resource=~%blackremedyeffects%~ END

  COPY ~%abilities%/castspelltarget.EFF~    ~override/%mf%a.EFF~
    WRITE_ASCII 0x30 ~%mf%a~ #8
  COPY_EXISTING ~%blackremedyeffects%.SPL~  ~override/%mf%a.SPL~
    LPF ALTER_EFFECT STR_VAR match_resource=~%blackremedyblast%~ resource=~%mf%b~ END

  COPY ~%abilities%/castspelltarget.EFF~    ~override/%mf%b.EFF~
    WRITE_ASCII 0x30 ~%mf%b~ #8
  COPY_EXISTING ~%blackremedyblast%.SPL~    ~override/%mf%b.SPL~
    LPF ALTER_SPELL_HEADER INT_VAR projectile=~%remedy15%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%mf%b~ END

  COPY_EXISTING ~%medicremove%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%mf%a~ END
  BUT_ONLY

END

//White Remedy
ACTION_IF (FILE_EXISTS ~override/%whiteremedy%.SPL~) BEGIN

  COPY_EXISTING ~%whiteremedy%.SPL~  ~override~
    LPF ALTER_EFFECT STR_VAR match_resource=~%whiteremedyeffects%~ resource=~%whiteremedy%e~ END
  BUT_ONLY

  COPY ~%abilities%/useeffonkit.SPL~  ~override/%whiteremedy%e.SPL~
    LPF ALTER_EFFECT 
      INT_VAR parameter1=IDS_OF_SYMBOL (kit ~D2MEDIC~)
      STR_VAR resource=~%mf%u~ 
    END
    LPF CLONE_EFFECT STR_VAR resource=~%mf%e~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=321 parameter2=0 STR_VAR resource=~%whiteremedy%e~ END

  COPY ~%abilities%/fireweaponbadge.EFF~     ~override/%mf%u.EFF~
    WRITE_ASCII 0x30 ~%mf%~ #8
    WRITE_ASCII 0x70 ~%mf%v~ #8
  COPY ~%abilities%/castspellself.EFF~       ~override/%mf%v.EFF~
    WRITE_ASCII 0x30 ~%mf%v~ #8
  COPY ~%abilities%/removespelleffects.SPL~  ~override/%mf%v.SPL~
    LPF ALTER_EFFECT STR_VAR resource=~%whiteremedy%e~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%mf%e~ END
    LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=326 STR_VAR resource=~%whiteremedyeffects%~ END

  COPY ~%abilities%/castspelltarget.EFF~    ~override/%mf%e.EFF~
    WRITE_ASCII 0x30 ~%mf%e~ #8
  COPY_EXISTING ~%whiteremedyeffects%.SPL~  ~override/%mf%e.SPL~
    LPF ALTER_EFFECT STR_VAR match_resource=~%whiteremedyblast%~ resource=~%mf%f~ END

  COPY ~%abilities%/castspelltarget.EFF~    ~override/%mf%f.EFF~
    WRITE_ASCII 0x30 ~%mf%f~ #8
  COPY_EXISTING ~%whiteremedyblast%.SPL~    ~override/%mf%f.SPL~
    LPF ALTER_SPELL_HEADER INT_VAR projectile=~%remedy15%~ END
    LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%mf%f~ END

  COPY_EXISTING ~%medicremove%.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%mf%e~ END
  BUT_ONLY

END



//Colorless Demon Soul (consumable)
COPY ~%icons%/demonsoul.BAM~      ~override/%mg%.BAM~
COPY ~%icons%/demonsouldesc.BAM~  ~override/%mg%d.BAM~
COPY ~%files%/demonsoul.ITM~      ~override/%mg%.ITM~
  SAY NAME1 @70136 SAY UNIDENTIFIED_DESC @70236
  SAY NAME2 @70136 SAY DESC @70236
  WRITE_ASCII 0x3a ~%mg%~ #8     // icon
  WRITE_ASCII 0x76 ~%mg%~ #8     // icon (ability)
  WRITE_ASCII 0x58 ~%mg%d~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@313) END    // "Consumed Demon Soul"



//Shaman Bone Blade (quick item)
COPY ~%icons%/shamanbone.BAM~      ~override/%mh%.BAM~
COPY ~%icons%/shamanbonedesc.BAM~  ~override/%mh%d.BAM~
COPY ~%files%/shamanbone.ITM~      ~override/%mh%.ITM~
  SAY NAME1 @70137 SAY UNIDENTIFIED_DESC @70237
  SAY NAME2 @70137 SAY DESC @70237
  WRITE_ASCII 0x3a ~%mh%~ #8     // icon
  WRITE_ASCII 0x76 ~%mh%~ #8     // icon (ability)
  WRITE_ASCII 0x58 ~%mh%d~ #8    // description image
  LPF ALTER_EFFECT INT_VAR match_opcode=324 STR_VAR resource=~%mh%~ END    // undead/golems immune
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%mh%b~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%mh%a~ END

COPY ~%files%/shamanbonea.SPL~  ~override/%mh%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=318 match_duration=2 STR_VAR resource=~%mh%b~ END    // block berserk
  LPF ALTER_EFFECT INT_VAR match_opcode=318 match_duration=0 STR_VAR resource=~%mh%a~ END    // elf resistance

COPY ~%files%/shamanboneb.SPL~  ~override/%mh%b.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@311) END    // "Berserk"



//Add Evasion checks for IWDEE
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  LAF d2_evasion_check INT_VAR insert=1 STR_VAR spell=~%m2%f~ END    // Etherbomb Songbook
  LAF d2_evasion_check INT_VAR insert=0 STR_VAR spell=~%m2%t~ END
  LAF d2_evasion_check INT_VAR insert=1 STR_VAR spell=~%ma%b~ END    // Ether Limiter
  LAF d2_evasion_check INT_VAR insert=1 STR_VAR spell=~%ma%f~ END
END



//Set variables for external use
OUTER_TEXT_SPRINT crowfeather    ~%m1%~
OUTER_TEXT_SPRINT etherbomb      ~%m2%~
OUTER_TEXT_SPRINT etherbooster   ~%m3%~
OUTER_TEXT_SPRINT prisonchain    ~%m4%~
OUTER_TEXT_SPRINT cruciform      ~%m5%~
OUTER_TEXT_SPRINT golemring      ~%m6%~
OUTER_TEXT_SPRINT calamity       ~%m7%~
OUTER_TEXT_SPRINT stonering      ~%m8%~
OUTER_TEXT_SPRINT flynnring      ~%m9%~
OUTER_TEXT_SPRINT etherlimiter   ~%ma%~
OUTER_TEXT_SPRINT goldscarab     ~%mb%~
OUTER_TEXT_SPRINT wingedsword    ~%mc%~
OUTER_TEXT_SPRINT redtearstone   ~%md%~
OUTER_TEXT_SPRINT hunterbone     ~%me%~
OUTER_TEXT_SPRINT jadefeather    ~%mf%~
OUTER_TEXT_SPRINT demonsoul      ~%mg%~
OUTER_TEXT_SPRINT shamanbone     ~%mh%~

OUTER_TEXT_SPRINT bleeding1      ~%m5%a~
OUTER_TEXT_SPRINT bleeding2      ~%m5%b~


/*
*/