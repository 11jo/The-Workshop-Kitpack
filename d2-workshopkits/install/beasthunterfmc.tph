//Beast Hunter (f/m/c)


ACTION_IF (nobeasthunterfmc = 0) BEGIN


INCLUDE ~%MOD_FOLDER%/lib/a7#add_kit_ex.tpa~    // weidu error if this file isn't included here


//Remove specialist bonus slots
COPY ~%passives%/fewerspells1.SPL~  ~override/%e4%.SPL~
COPY ~%passives%/opcode206.SPL~     ~override/%e5%.SPL~         // only relevant for 5e casting mod
  LPF ALTER_EFFECT STR_VAR resource=~d5z0slt~ END
  LPF CLONE_EFFECT STR_VAR resource=~D5ZMSLT~ END


//Holy Symbol stuff
COPY ~%passives%/opcode206.SPL~  ~override/%e9%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~cdhlysym~ END

ACTION_IF (FILE_EXISTS_IN_GAME ~cdhlysym.SPL~) BEGIN
  COPY_EXISTING ~cdhlysym.SPL~   ~override/%m9%.SPL~
END


//FnP spheres
COPY ~%passives%/d5fpclsp.spl~  ~override/%e6%.SPL~
  LPF ALTER_EFFECT STR_VAR match_resource=~d5spdes#~ resource=~d5smear#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5smdea#~ resource=~d5smdre#~ END
  LPF ALTER_EFFECT INT_VAR parameter1=0 parameter2=2 END


//Ability tables (don't include base class abilities)
COPY ~%files%/kit0.2DA~  ~override/%beasthunter%F.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=4 limit=13 STR_VAR resource=~%s2%~ END

COPY ~%files%/kit6.2DA~  ~override/%beasthunter%C.2DA~
  PATCH_IF (GAME_INCLUDES ~bg2~) BEGIN
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e9%~ END
    LPF d2_kit_ability_adder INT_VAR level=14 passive=1 STR_VAR resource=~%m9%~ END
  END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e6%~ END

COPY ~%files%/kit6.2DA~  ~override/%beasthunter%M.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e4%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e5%~ END


//Set variable for proficiencies
ACTION_IF ((vanillaclericprofs = 0) OR
           (FILE_EXISTS_IN_GAME ~RASPHERE.marker~) OR
           (FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~) OR
           (MOD_IS_INSTALLED ~Faiths_and_Powers.tp2~ (ID_OF_LABEL ~Faiths_and_Powers.tp2~ ~D5_FNP_WEAPON_USABILITY~))
          ) BEGIN

  OUTER_TEXT_SPRINT multikitprofs  ~2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 3~
END
ELSE BEGIN
  OUTER_TEXT_SPRINT multikitprofs  ~0 0 0 0 2 2 0 2 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 0 0 0 2 2 2 2 3~
END


//Re-enable the F/M/C class (i.e. if disabled by FnP multiclass)
COPY_EXISTING - ~clsrcreq.2DA~  ~~
  COUNT_2DA_ROWS 3 clsrcreq_rows
  READ_2DA_ENTRIES_NOW ~clsrcreq_table~ 3

COPY_EXISTING - ~clastext.2DA~  ~~
  COUNT_2DA_ROWS 3 clastext_rows
  READ_2DA_ENTRIES_NOW ~clastext_table~ 3
  FOR (i = 1; i < clastext_rows; ++i) BEGIN
    READ_2DA_ENTRY_FORMER ~clastext_table~ i 1 classid
    PATCH_IF (classid = 17) BEGIN
      READ_2DA_ENTRY_FORMER ~clastext_table~ i 0 classname1
      INNER_ACTION BEGIN
        COPY_EXISTING - ~clsrcreq.2DA~  ~~
          FOR (r = 1; r < clsrcreq_rows; ++r) BEGIN
            READ_2DA_ENTRY_FORMER ~clsrcreq_table~ r 0 classname2
            PATCH_IF (~%classname1%~ STRING_EQUAL_CASE ~%classname2%~) BEGIN
              SET_2DA_ENTRY_LATER ~make_edits~ r 3 1
            END
          END
      END
    END
  END

COPY_EXISTING ~clsrcreq.2DA~  ~override~
  SET_2DA_ENTRIES_NOW ~make_edits~ 3
BUT_ONLY


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 17
    mixed = RESOLVE_STR_REF (@100101)
    lower = RESOLVE_STR_REF (@100102)
    help = RESOLVE_STR_REF (@100103)
  STR_VAR
    kit_name = ~D2%kitname%FMC~
//  unusable = ~0x00010000~               // Stalker usability (no effect for multiclasses)
    clab_base_f = ~%beasthunter%F.2DA~    // kit abilities added to Fighter table
    clab_base_m = ~%beasthunter%M.2DA~    // -1 spell slots added to Mage table
    clab_base_p = ~%beasthunter%C.2DA~    // no abilities added to Cleric table
    clsrcreq = ~1 1 1 1 1 1 1~            // Usable by all races, if base class is usable
    weapprof = ~%multikitprofs%~
    clascolr = ~28 93 91 91 93~           // Archer colors
  RET 
    kit_id
END


//Usability - restrict to studded leather or lighter
COPY_EXISTING_REGEXP GLOB ~.*\.ITM~  ~override~
  PATCH_IF (!(~%SOURCE_RES%~ STRING_EQUAL_CASE ~secret05~) AND    // skip Big Metal Unit
            !(~%SOURCE_RES%~ STRING_EQUAL_CASE ~d2tec05~)) BEGIN
    READ_ASCII 0x22 itemtype (2) NULL                             // equipped appearance
    PATCH_IF ((~%itemtype%~ STRING_EQUAL_CASE ~3A~) OR            // chain or splint
              (~%itemtype%~ STRING_EQUAL_CASE ~4A~)) BEGIN        // plate mail
      LPF ADD_ITEM_EQEFFECT
        INT_VAR
          opcode = 319
          target = 1
          parameter1 = IDS_OF_SYMBOL (kit ~D2%kitname%FMC~)
          parameter2 = 9
          timing = 2
      END
    END
  END
BUT_ONLY


//Set variables for external use
OUTER_TEXT_SPRINT kitid    ~%kitid%C~
OUTER_TEXT_SPRINT kitname  ~%kitname%FMC~
OUTER_SET beasthunterfmc = 1


END


/* 
*/