//Beast Hunter (f/m/c)


ACTION_IF (nobeasthunterfmc = 0) BEGIN


//Remove specialist bonus slots
COPY ~%passives%/fewerspells1.SPL~  ~override/%e4%.SPL~
COPY ~%passives%/opcode206.SPL~     ~override/%e5%.SPL~    // only relevant for 5e casting mod
  LPF ALTER_EFFECT STR_VAR resource=~d5z0slt~ END
  LPF CLONE_EFFECT STR_VAR resource=~D5ZMSLT~ END


//Holy Symbol stuff
COPY ~%passives%/opcode206.SPL~  ~override/%e9%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~cdhlysym~ END

ACTION_IF (FILE_EXISTS_IN_GAME ~BELT12.ITM~) BEGIN
  COPY ~%passives%/useeff.SPL~  ~override/%m9%.SPL~
      LPF ALTER_EFFECT INT_VAR multi_match=1 parameter1=33 parameter2=8 STR_VAR resource=~%m9%a~ END  // Lathander
      LPF CLONE_EFFECT INT_VAR multi_match=1 parameter1=49 parameter2=8 STR_VAR resource=~%m9%a~ END
      LPF CLONE_EFFECT INT_VAR multi_match=1 parameter1=50 parameter2=8 STR_VAR resource=~%m9%a~ END
      LPF CLONE_EFFECT INT_VAR multi_match=1 parameter1=34 parameter2=8 STR_VAR resource=~%m9%b~ END  // Helm
      LPF CLONE_EFFECT INT_VAR multi_match=1 parameter1=17 parameter2=8 STR_VAR resource=~%m9%b~ END
      LPF CLONE_EFFECT INT_VAR multi_match=1 parameter1=18 parameter2=8 STR_VAR resource=~%m9%b~ END
      LPF CLONE_EFFECT INT_VAR multi_match=1 parameter1=19 parameter2=8 STR_VAR resource=~%m9%b~ END
      LPF CLONE_EFFECT INT_VAR multi_match=1 parameter1=35 parameter2=8 STR_VAR resource=~%m9%c~ END  // Talos
      LPF CLONE_EFFECT INT_VAR multi_match=1 parameter1=51 parameter2=8 STR_VAR resource=~%m9%c~ END

  COPY ~%abilities%/applyeffects.EFF~   ~override/%m9%a.EFF~
    WRITE_ASCII 0x30 ~%m9%a~ #8
  COPY ~%abilities%/applyeffects.EFF~   ~override/%m9%b.EFF~
    WRITE_ASCII 0x30 ~%m9%b~ #8
  COPY ~%abilities%/applyeffects.EFF~   ~override/%m9%c.EFF~
    WRITE_ASCII 0x30 ~%m9%c~ #8

  COPY ~%files%/holysymbol_core.SPL~    ~override/%m9%a.SPL~
    LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~BELT12~ END  // Lathander
  COPY ~%files%/holysymbol_core.SPL~    ~override/%m9%b.SPL~
    LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~BELT13~ END  // Helm
  COPY ~%files%/holysymbol_core.SPL~    ~override/%m9%c.SPL~
    LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~BELT14~ END  // Talos
END


//FnP spheres
COPY ~%passives%/d5fpclsp.spl~  ~override/%e6%.SPL~
  LPF ALTER_EFFECT STR_VAR match_resource=~d5spdes#~ resource=~d5smear#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5smdea#~ resource=~d5smdre#~ END
  LPF ALTER_EFFECT INT_VAR parameter1=0 parameter2=2 END


//Multiclass import fix
LAF d2_mc_import_fix INT_VAR amount=1  STR_VAR spell=~%s1%~ new_spell=~%#1%~ END
LAF d2_mc_import_fix INT_VAR amount=2  STR_VAR spell=~%s1%~ new_spell=~%#2%~ END
LAF d2_mc_import_fix INT_VAR amount=3  STR_VAR spell=~%s1%~ new_spell=~%#3%~ END
LAF d2_mc_import_fix INT_VAR amount=4  STR_VAR spell=~%s1%~ new_spell=~%#4%~ END
LAF d2_mc_import_fix INT_VAR amount=5  STR_VAR spell=~%s1%~ new_spell=~%#5%~ END
LAF d2_mc_import_fix INT_VAR amount=6  STR_VAR spell=~%s1%~ new_spell=~%#6%~ END
LAF d2_mc_import_fix INT_VAR amount=7  STR_VAR spell=~%s1%~ new_spell=~%#7%~ END
LAF d2_mc_import_fix INT_VAR amount=8  STR_VAR spell=~%s1%~ new_spell=~%#8%~ END
LAF d2_mc_import_fix INT_VAR amount=9  STR_VAR spell=~%s1%~ new_spell=~%#9%~ END
LAF d2_mc_import_fix INT_VAR amount=10 STR_VAR spell=~%s1%~ new_spell=~%#a%~ END
LAF d2_mc_import_fix INT_VAR amount=11 STR_VAR spell=~%s1%~ new_spell=~%#b%~ END
LAF d2_mc_import_fix INT_VAR amount=12 STR_VAR spell=~%s1%~ new_spell=~%#c%~ END
LAF d2_mc_import_fix INT_VAR amount=13 STR_VAR spell=~%s1%~ new_spell=~%#d%~ END

LAF d2_mc_import_fix INT_VAR amount=1  STR_VAR spell=~%s2%~ new_spell=~%!1%~ END
LAF d2_mc_import_fix INT_VAR amount=2  STR_VAR spell=~%s2%~ new_spell=~%!2%~ END
LAF d2_mc_import_fix INT_VAR amount=3  STR_VAR spell=~%s2%~ new_spell=~%!3%~ END


//Array for import fix
ACTION_CLEAR_ARRAY ~import_fix_array~
ACTION_DEFINE_ASSOCIATIVE_ARRAY import_fix_array BEGIN
  ~%#1%~  => 2     // hunter tools
  ~%#2%~  => 6
  ~%#3%~  => 10
  ~%#4%~  => 14
  ~%#5%~  => 18
  ~%#6%~  => 22
  ~%#7%~  => 26
  ~%#8%~  => 30
  ~%#9%~  => 34
  ~%#a%~  => 38
  ~%#b%~  => 42
  ~%#c%~  => 46
  ~%#d%~  => 50
  ~%!1%~  => 5     // quicksilver
  ~%!2%~  => 9
  ~%!3%~  => 13
END


//Ability tables (don't include base class abilities)
COPY ~%files%/kit0.2DA~  ~override/%beasthunter%F.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=4 limit=13 STR_VAR resource=~%s2%~ END

  //import fix (BG2, ToB, SoD)
  PHP_EACH import_fix_array AS file => level BEGIN
    LPF d2_kit_ability_adder INT_VAR level=level passive=1 STR_VAR resource=~%file%~ END
  END

COPY ~%files%/kit6.2DA~  ~override/%beasthunter%C.2DA~
  PATCH_IF (holysymbols > 0) BEGIN
    LPF d2_kit_ability_adder INT_VAR level=14 passive=1 STR_VAR resource=~%m9%~ END    // holy symbol (level 14)
  END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e9%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e6%~ END

COPY ~%files%/kit6.2DA~  ~override/%beasthunter%M.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e4%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e5%~ END


//Set variable for proficiencies
ACTION_IF ((vanillaclericprofs = 0) OR
           (FILE_EXISTS_IN_GAME ~RASPHERE.marker~) OR
           (FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~) OR
           (MOD_IS_INSTALLED ~Faiths_and_Powers.tp2~ (ID_OF_LABEL ~Faiths_and_Powers.tp2~ ~D5_FNP_WEAPON_USABILITY~))
          ) BEGIN

  OUTER_TEXT_SPRINT multikitprofs  ~2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 3~
END
ELSE BEGIN
  OUTER_TEXT_SPRINT multikitprofs  ~0 0 0 0 2 2 0 2 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 0 0 0 2 2 2 2 3~
END


//Re-enable the F/M/C class (i.e. if disabled by FnP multiclass)
COPY_EXISTING - ~clsrcreq.2DA~  ~~
  COUNT_2DA_ROWS 3 clsrcreq_rows
  READ_2DA_ENTRIES_NOW ~clsrcreq_table~ 3

COPY_EXISTING - ~clastext.2DA~  ~~
  COUNT_2DA_ROWS 3 clastext_rows
  READ_2DA_ENTRIES_NOW ~clastext_table~ 3
  FOR (i = 1; i < clastext_rows; ++i) BEGIN
    READ_2DA_ENTRY_FORMER ~clastext_table~ i 1 classid
    PATCH_IF (classid = 17) BEGIN
      READ_2DA_ENTRY_FORMER ~clastext_table~ i 0 classname1
      INNER_ACTION BEGIN
        COPY_EXISTING - ~clsrcreq.2DA~  ~~
          FOR (r = 1; r < clsrcreq_rows; ++r) BEGIN
            READ_2DA_ENTRY_FORMER ~clsrcreq_table~ r 0 classname2
            PATCH_IF (~%classname1%~ STRING_EQUAL_CASE ~%classname2%~) BEGIN
              SET_2DA_ENTRY_LATER ~make_edits~ r 3 1
            END
          END
      END
    END
  END

COPY_EXISTING ~clsrcreq.2DA~  ~override~
  SET_2DA_ENTRIES_NOW ~make_edits~ 3
BUT_ONLY


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 17
    mixed = RESOLVE_STR_REF (@100101)
    lower = RESOLVE_STR_REF (@100102)
    help = RESOLVE_STR_REF (@100103)
  STR_VAR
    kit_name = ~D2%kitname%FMC~
//  unusable = ~0x00010000~               // Stalker usability (no effect for multiclasses)
    clab_base_f = ~%beasthunter%F.2DA~    // kit abilities added to Fighter table
    clab_base_m = ~%beasthunter%M.2DA~    // -1 spell slots added to Mage table
    clab_base_p = ~%beasthunter%C.2DA~    // holy symbol added to Cleric table
    clsrcreq = ~1 1 1 1 1 1 1~            // Usable by all races, if base class is usable
    weapprof = ~%multikitprofs%~
    clascolr = ~28 93 91 91 93~           // Archer colors
  RET 
    kit_id
END


//Usability - restrict to studded leather or lighter
COPY_EXISTING_REGEXP GLOB ~.*\.ITM~  ~override~
  PATCH_IF (!(~%SOURCE_RES%~ STRING_EQUAL_CASE ~secret05~) AND    // skip Big Metal Unit
            !(~%SOURCE_RES%~ STRING_EQUAL_CASE ~d2tec05~)) BEGIN
    READ_ASCII 0x22 itemtype (2) NULL                             // equipped appearance
    PATCH_IF ((~%itemtype%~ STRING_EQUAL_CASE ~3A~) OR            // chain or splint
              (~%itemtype%~ STRING_EQUAL_CASE ~4A~)) BEGIN        // plate mail
      LPF ADD_ITEM_EQEFFECT
        INT_VAR
          opcode = 319
          target = 1
          parameter1 = IDS_OF_SYMBOL (kit ~D2%kitname%FMC~)
          parameter2 = 9
          timing = 2
      END
    END
  END
BUT_ONLY


//DoF compatibility
OUTER_TEXT_SPRINT kitid    ~%kitid%C~
OUTER_TEXT_SPRINT kitname  ~%kitname%FMC~
INCLUDE ~%install%/dofspheres.tph~
OUTER_TEXT_SPRINT kitid    ~d2wk1~
OUTER_TEXT_SPRINT kitname  ~BEASTHUNTER~


//Set Variable
OUTER_SET beasthunterfmc = 1

END ELSE BEGIN

OUTER_SET beasthunterfmc = 0

END


/* 
*/