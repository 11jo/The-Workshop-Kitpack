/*
Fixes some things if this mod is installed after FnP.
*/


//FnP adds spheres to the base Paladin clab file.
//This will remove the default spheres, so kit spheres can be used.

ACTION_IF (FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~) BEGIN
  COPY_EXISTING ~%kitid%.2DA~  ~override~
    COUNT_2DA_ROWS 3 rows
    READ_2DA_ENTRIES_NOW ~kit_table~ 3
    FOR (r = (rows - 1); r > 0; r -= 1) BEGIN
      READ_2DA_ENTRY_FORMER ~kit_table~ r 1 entry
      PATCH_IF !(~%entry%~ STRING_MATCHES_REGEXP ~AP_D5SP.*~) BEGIN     // if entry DOES start with AP_D5SP
        PATCH_IF (!(~%entry%~ STRING_EQUAL_CASE ~AP_D5SPUNI~) AND
                  !(~%entry%~ STRING_EQUAL_CASE ~AP_D5SPMEM~)) BEGIN    // skip universal abilities
          REMOVE_2DA_ROW r 3
        END
      END
    END
  BUT_ONLY IF ~AP_D5SP.*~    // only if relevant entries
END



//Archetype fix (paladins)

ACTION_IF (FILE_EXISTS ~override/d5padlg.BCS~) BEGIN

  ACTION_IF !(VARIABLE_IS_SET ~fnp_archetypes~) BEGIN
    COPY_EXISTING - ~d5padlg.BCS~  ~~
      DECOMPILE_AND_PATCH BEGIN
        COUNT_REGEXP_INSTANCES ~D5PADEF~ fnp_archetypes
      END
  END


ACTION_IF (fnp_archetypes > 0) BEGIN

<<<<<<<< .../temp/archetypes
IF
    NumberOfTimesTalkedTo(0)
    Kit(LastSummonerOf(),D2%kitname%)
THEN
    RESPONSE #100
        SetGlobal("D5PADEF","GLOBAL",1)
        SetGlobal("D5PAINQ","GLOBAL",1)
        SetGlobal("D5PAZEA","GLOBAL",1)
        Continue()
END
>>>>>>>>

COPY - ~.../temp/archetypes~  ~%workspace%/archetypes.BAF~
  EVALUATE_BUFFER

EXTEND_TOP ~d5padlg.BCS~  ~%workspace%/archetypes.BAF~


END
END


/*
*/