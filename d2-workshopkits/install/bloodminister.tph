//Blood Minister (d2wk9)


//Add passive effects
COPY ~%passives%/noitemtype.SPL~     ~override/%e1%.SPL~
  LPF ALTER_EFFECT INT_VAR opcode=181 special=RESOLVE_STR_REF (@300) END    // "Cannot use item."

COPY ~%passives%/bloodminister.SPL~  ~override/%e2%.SPL~
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_level_drain_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_poison_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_disease_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_poison_resistance_arrays END
  PATCH_IF (GAME_IS ~bgee iwdee~) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=267 END
    LPF d2_string_immunities END
  END


//Add Blood Transfusion
COPY ~%icons%/bloodtransfusion.BAM~      ~override/%s1%.BAM~
COPY ~%abilities%/bloodtransfusion.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @112
  SAY UNIDENTIFIED_DESC @212
  LPF ALTER_EFFECT INT_VAR header=0 match_opcode=214 STR_VAR resource=~%s1%a~ END    // set 2da list to use
  LPF ALTER_EFFECT INT_VAR header=1 match_opcode=214 STR_VAR resource=~%s1%b~ END
  LPF ALTER_EFFECT INT_VAR header=2 match_opcode=214 STR_VAR resource=~%s1%c~ END
  LPF ALTER_EFFECT INT_VAR header=3 match_opcode=214 STR_VAR resource=~%s1%d~ END
  WRITE_ASCII 0x3a ~%s1%~ #8    // icon BAMs
  LPF ALTER_SPELL_HEADER STR_VAR icon=~%s1%~ END

COPY ~%abilities%/bloodtransfusion.2DA~  ~override/%s1%a.2DA~    // make 2da lists
	APPEND ~%s1%a.2DA~  ~healing_blood   %s1%a 3~
COPY_EXISTING ~%s1%a.2DA~    ~override/%s1%b.2DA~
	APPEND ~%s1%b.2DA~  ~beast_blood     %s1%b 3~
COPY_EXISTING ~%s1%b.2DA~    ~override/%s1%c.2DA~
	APPEND ~%s1%c.2DA~  ~madmans_blood   %s1%c 3~
COPY_EXISTING ~%s1%c.2DA~    ~override/%s1%d.2DA~
	APPEND ~%s1%d.2DA~  ~lead_elixir     %s1%d 3~


//Add blood types
COPY ~%icons%/bloodtypea.BAM~         ~override/%s1%a.BAM~
COPY ~%icons%/bloodtypeb.BAM~         ~override/%s1%b.BAM~
COPY ~%icons%/bloodtypec.BAM~         ~override/%s1%c.BAM~
COPY ~%icons%/bloodtyped.BAM~         ~override/%s1%d.BAM~
COPY ~%icons%/bloodtypeportrait.BAM~  ~override/%s1%i.BAM~

LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@603) STR_VAR bam=~%s1%i~ RET icon END
COPY ~%abilities%/bloodtypea.SPL~  ~override/%s1%a.SPL~    // Healing Blood
  SAY NAME1 @603
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@306) END         // "Regenerating"
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=324 STR_VAR resource=~%s1%a~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%c~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%d~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=146 STR_VAR resource=~%s1%x~ END    // remove poison
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=146 STR_VAR resource=~%s1%y~ END    // remove disease
  LPF CLONE_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF ALTER_SPELL_HEADER STR_VAR icon=~%s1%a~ END                                         // icon BAM
  WRITE_ASCII 0x3a ~%s1%a~ #8

LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@604) STR_VAR bam=~%s1%i~ RET icon END
COPY ~%abilities%/bloodtypeb.SPL~  ~override/%s1%b.SPL~    // Beast Blood
  SAY NAME1 @604
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=324 STR_VAR resource=~%s1%b~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%c~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%d~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=248 STR_VAR resource=~%s1%u~ END    // +1 damage (v3.2 update)
  LPF CLONE_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  WRITE_ASCII 0x3a ~%s1%b~ #8    // icon BAMs
  LPF ALTER_SPELL_HEADER STR_VAR icon=~%s1%b~ END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_poison_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_disease_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_poison_resistance_arrays END
  PATCH_IF (GAME_IS ~bgee iwdee~) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=267 END
    LPF d2_string_immunities END
  END

LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@605) STR_VAR bam=~%s1%i~ RET icon END
COPY ~%abilities%/bloodtypec.SPL~  ~override/%s1%c.SPL~    // Madman's Blood
  SAY NAME1 @605
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=324 STR_VAR resource=~%s1%c~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%c~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%d~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=146 STR_VAR resource=~%s1%z~ END    // remove level drain
  LPF CLONE_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  WRITE_ASCII 0x3a ~%s1%c~ #8    // icon BAMs
  LPF ALTER_SPELL_HEADER STR_VAR icon=~%s1%c~ END

LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@606) STR_VAR bam=~%s1%i~ RET icon END
COPY ~%abilities%/bloodtyped.SPL~  ~override/%s1%d.SPL~    // Lead Elixir
  SAY NAME1 @606
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=324 STR_VAR resource=~%s1%d~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%c~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%d~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  WRITE_ASCII 0x3a ~%s1%d~ #8    // icon BAMs
  LPF ALTER_SPELL_HEADER STR_VAR icon=~%s1%d~ END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_level_drain_arrays END
  PATCH_IF (GAME_IS ~bgee iwdee~) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=267 END
    LPF d2_string_immunities END
  END


//Add blood type subspells
COPY_EXISTING ~SPPR212.SPL~  ~override/%s1%x.SPL~    // clone Slow Poison
  LPF d2_subspell_remove_effects END
  LPF d2_subspell_remove_visuals INT_VAR string=0 END
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=329 opcode=11 END    // IWDEE: change slow poison to cure poison

COPY_EXISTING ~SPPR317.SPL~  ~override/%s1%y.SPL~    // clone Cure Disease
  LPF d2_subspell_remove_effects END
  LPF d2_subspell_remove_visuals INT_VAR string=0 END
  LPF DELETE_EFFECT INT_VAR match_opcode=17 END      // remove HP heal (IWD)

COPY_EXISTING ~SPPR417.SPL~  ~override/%s1%z.SPL~    // clone Lesser Restoration
  LPF d2_subspell_remove_effects END
  LPF d2_subspell_remove_visuals INT_VAR string=0 END
  LPF DELETE_EFFECT INT_VAR match_opcode=93 END      // remove fatigue

COPY ~%abilities%/castspellself.EFF~  ~override/%s1%u.EFF~    // +1 damage bonus (Beast Blood)
  WRITE_ASCII 0x30 ~%s1%u~ #8
COPY ~%abilities%/damageup1.SPL~      ~override/%s1%u.SPL~


//removed by Restoration or Greater Restoration
OUTER_SPRINT restoration @700
COPY_EXISTING_REGEXP GLOB ~.*\.SPL~  ~override~
  READ_LONG   0x8 stringref
  READ_STRREF 0x8 spellname
  PATCH_IF ((~%SOURCE_RES%~ STRING_EQUAL_CASE ~cdpr417~) OR     // if Lesser Restoration
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~sppr417~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~spin581~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~ohbresto~) OR    // if Greater Restoration
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~sppr713~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~spwish07~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~spwish46~) OR
            (stringref = 26226) OR (stringref = 55844) OR (stringref = 35957) OR    // match stringref
            (stringref = 26310) OR (stringref = 35603) OR (stringref = 36401) OR
            !(~%spellname%~ STRING_CONTAINS_REGEXP ~%restoration%~)                 // check spell name
  ) BEGIN
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 match_timing=1 opcode=321 parameter2=0 STR_VAR resource=~%s1%a~ END
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 match_timing=1 opcode=321 parameter2=0 STR_VAR resource=~%s1%b~ END
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 match_timing=1 opcode=321 parameter2=0 STR_VAR resource=~%s1%c~ END
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 match_timing=1 opcode=321 parameter2=0 STR_VAR resource=~%s1%d~ END
  END
BUT_ONLY


//Add Holy Symbol
COPY ~%files%/holysymbol_core.SPL~    ~override/%m9%.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~%m9%~ END
COPY ~%icons%/holysymbol_blood.BAM~   ~override/%m9%.BAM~

ACTION_IF (FILE_EXISTS_IN_GAME ~belt12.itm~) BEGIN
  COPY_EXISTING ~belt12.itm~          ~override/%m9%.ITM~    // clone Lathander symbol
    WRITE_LONG 0x1e (THIS BAND BNOT BIT1)                    // remove restrictions
    WRITE_BYTE 0x29 (THIS BAND BNOT BIT6)
END
ELSE BEGIN
  COPY ~%files%/holysymbol_core.ITM~  ~override/%m9%.ITM~    // or add new
END

COPY_EXISTING ~%m9%.ITM~  ~override~
  SAY NAME1 @70100 SAY UNIDENTIFIED_DESC @70200
  SAY NAME2 @70100 SAY DESC @70200
  WRITE_ASCII 0x3a ~%m9%~ #8       // icon
  WRITE_ASCII 0x58 ~CMISC98~ #8    // description image
BUT_ONLY


//Make clab table
COPY ~inlined/d2clab_cleric~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=2 limit=20 STR_VAR resource=~%s1%~ END

  PATCH_IF (holysymbols > 0) BEGIN    // add Holy Symbol
    LPF d2_kit_ability_adder INT_VAR level=25 passive=1 STR_VAR resource=~%m9%~ END
  END

  REPLACE_TEXTUALLY ~AP_CDHLYSYM~  ~****~    // remove default Holy Symbol
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 3
    mixed = RESOLVE_STR_REF (@100901)
    lower = RESOLVE_STR_REF (@100902)
    help = RESOLVE_STR_REF (@100903)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~          // Usable by all races, if base class is usable
    clascolr = ~171 174 190 171 171~    // Necromancer colors
  RET 
    kit_id
END


//Weapon restrictions

/*
For DoF sphere system.
Or if installed after FnP. If installed before, will use FnP default Cleric usability.
*/

ACTION_IF ((FILE_EXISTS_IN_GAME ~RASPHERE.marker~) OR
           (FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~) OR
           (MOD_IS_INSTALLED ~Faiths_and_Powers.tp2~ (ID_OF_LABEL ~Faiths_and_Powers.tp2~ ~D5_FNP_WEAPON_USABILITY~))
          ) BEGIN

ACTION_DEFINE_ASSOCIATIVE_ARRAY weapontype BEGIN    // weapon => usable
  15 => 0    // bows
  16 => 1    // daggers
  17 => 1    // maces, clubs
  18 => 1    // slings
  19 => 1    // small swords (short sword, wakizashi)
  20 => 0    // large swords (long sword, bastard sword, two handed sword, ninjato)
  21 => 1    // hammers
  22 => 1    // morning stars
  23 => 1    // flails
  24 => 1    // darts
  25 => 0    // axes
  26 => 1    // quarterstaff
  27 => 1    // crossbows
  28 => 1    // fist
  29 => 0    // spears
  30 => 0    // halberds
END

END


//Higher level healing for FnP

/*
Added this way instead of using the FnP function because it's too 
much of a pain to re-arrange the way I've already set everything up.
*/

COPY ~%passives%/d5fpclsp.spl~  ~override/%e6%.SPL~
  LPF DELETE_EFFECT INT_VAR multi_match=10 END
  LPF ALTER_EFFECT INT_VAR parameter1=0 parameter2=2 END
BUT_ONLY

COPY_EXISTING ~%kitid%.2DA~  ~override~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e6%~ END
BUT_ONLY


//Set variables for external use
OUTER_TEXT_SPRINT bloodminister     ~%kitid%~
OUTER_TEXT_SPRINT bloodtransfusion  ~%s1%~
OUTER_TEXT_SPRINT healingblood      ~%s1%a~
OUTER_TEXT_SPRINT beastblood        ~%s1%b~
OUTER_TEXT_SPRINT madmanblood       ~%s1%c~
OUTER_TEXT_SPRINT leadelixir        ~%s1%d~
OUTER_TEXT_SPRINT fnpclass          ~cleric~
OUTER_TEXT_SPRINT fnpdeity          ~druid_lost_spheres~


/*
*/