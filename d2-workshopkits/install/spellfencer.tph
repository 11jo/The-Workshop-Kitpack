//Spell Fencer (d2wkh)


//passive effects
COPY ~%passives%/acbonus1.SPL~         ~override/%e1%.SPL~
COPY ~%passives%/savebonus4death.SPL~  ~override/%e2%.SPL~
  LPF ALTER_EFFECT INT_VAR opcode=37 parameter1=1 END    // change to +1 to Save vs. Spell

//for 5e casting mod (blocks effect)
COPY ~%passives%/opcode206.SPL~        ~override/%e5%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~d5z0slt~ END    // -1 to spell slots to mage kits


//Add Flash Cast
COPY ~%passives%/hiteffect1melee.SPL~  ~override/%e3%.SPL~     // melee effect
  LPF ALTER_EFFECT STR_VAR resource=~%e3%a~ END
  LPF CLONE_EFFECT INT_VAR opcode=139 parameter1=RESOLVE_STR_REF (@318) END    // "Gained Passive Ability: Flash Cast"

COPY ~%icons%/flashcast.BAM~           ~override/%e3%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@621) STR_VAR bam=~%e3%i~ RET icon END

COPY ~%abilities%/castspellself.EFF~   ~override/%e3%a.EFF~
  WRITE_ASCII 0x30 ~%e3%a~ #8
COPY ~%abilities%/castingspeed1.SPL~   ~override/%e3%a.SPL~    // +2 casting speed
  LPF ALTER_EFFECT INT_VAR target=1 parameter1=2 END
  LPF CLONE_EFFECT INT_VAR opcode=142 parameter2=%icon% END


//Critical miss removes bonus
COPY ~%passives%/criticalmisseffect.SPL~   ~override/%e4%.SPL~     // critical miss effect
  LPF ALTER_EFFECT STR_VAR resource=~%e4%a~ END

COPY ~%abilities%/removespelleffects.SPL~  ~override/%e4%a.SPL~    // remove crit bonus
  LPF ALTER_EFFECT STR_VAR resource=~%e3%a~ END


//block effect (until 5 fighter levels)
COPY ~%passives%/opcode206.SPL~            ~override/%e6%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%e3%a~ END

COPY ~%abilities%/removespelleffects.SPL~  ~override/%e7%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%e6%~ END


//Ability tables
COPY ~inlined/d2clab.2DA~  ~override/%kitid%F.2DA~
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 multi=1 increment=5 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e6%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 STR_VAR resource=~%e7%~ END

COPY ~inlined/d2clab.2DA~  ~override/%kitid%M.2DA~
  LPF d2_kit_ability_adder INT_VAR level=4 passive=1 multi=1 increment=4 limit=20 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e5%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 STR_VAR resource=~%e3%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 STR_VAR resource=~%e4%~ END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 7
    mixed = RESOLVE_STR_REF (@101701)
    lower = RESOLVE_STR_REF (@101702)
    help = RESOLVE_STR_REF (@101703)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_base_f = ~%kitid%F.2DA~     // added to fighter table
    clab_base_m = ~%kitid%M.2DA~     // added to mage table
    clsrcreq = ~1 1 1 1 1 1 1~       // Usable by all races, if base class is usable
    weapprof = ~2 2 1 2 2 2 2 1 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 1 1 1 1 1 2 2 2 3~
    clascolr = ~93 99 169 93 72~     // swashbuckler colors
  RET 
    kit_id
END


//Usability - restrict to chain mail
ACTION_DEFINE_ASSOCIATIVE_ARRAY d2_restrict_armor_array BEGIN
  ~D2%kitname%~ => ~chain~
END


//Gains swashbuckler/blade effects from items
OUTER_TEXT_SPRINT kit_group ~blade~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~

ACTION_PHP_EACH item_kit_effects AS item => class BEGIN
  ACTION_IF !(~%kit_group%~ STRING_CONTAINS_REGEXP ~%class%~) BEGIN
    ACTION_FOR_EACH res IN ~SPL~ ~ITM~ BEGIN
      ACTION_IF (FILE_EXISTS_IN_GAME ~%item%.%res%~) BEGIN
        COPY_EXISTING ~%item%.%res%~  ~override~
          PHP_EACH kit_ids_array AS class2 => eff_to_copy BEGIN
            PATCH_IF (~%class%~ STR_EQ ~%class2%~) BEGIN
              LPF CLONE_EFFECT 
                INT_VAR 
                  silent            = 1
                  match_opcode      = 177 
                  match_parameter1  = %eff_to_copy%
                  match_parameter2  = 9 
                  parameter1        = IDS_OF_SYMBOL (kit ~%ids_name%~)
              END
            END
          END
        BUT_ONLY
      END
    END
  END
END


//Set variables for external use
OUTER_TEXT_SPRINT spellfencer  ~%kitid%~


/* 
*/