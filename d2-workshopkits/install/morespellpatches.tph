/*
Additional patching of spells or items
Use for mass patching that attempts a CLONE_EFFECT on every file.
*/


ACTION_IF ((nonurse = 0) OR
           (noconfessor = 0)
  ) BEGIN


OUTER_SET patch_spell = 0
OUTER_SET patch_item  = 0


PRINT @6

COPY_EXISTING_REGEXP GLOB ~.*\.SPL~  ~override~

  GET_OFFSET_ARRAY spl_headers  SPL_V10_HEADERS
  PHP_EACH spl_headers AS num => header_off BEGIN
    GET_OFFSET_ARRAY2 spl_effects header_off SPL_V10_HEAD_EFFECTS
    PHP_EACH spl_effects AS num => effect_off BEGIN
      PATCH_IF (patch_spell = 0) BEGIN
        READ_SHORT effect_off opcode
        PATCH_IF (opcode = 101) BEGIN
          SET patch_spell = 1
        END
      END
    END
  END

  PATCH_IF (patch_spell = 1) BEGIN
    PATCH_IF (nonurse = 0) BEGIN
      LPF nurse_feeblemind_string END
    END

    PATCH_IF (noconfessor = 0) BEGIN
      LPF confessor_entangle_spells END
    END

    SET patch_spell = 0
  END

BUT_ONLY



PRINT @7

COPY_EXISTING_REGEXP GLOB ~.*\.ITM~  ~override~

  GET_OFFSET_ARRAY itm_equip  ITM_V10_GEN_EFFECTS
  PHP_EACH itm_equip AS num => equip_off BEGIN
    PATCH_IF (patch_item = 0) BEGIN
      READ_SHORT equip_off opcode
      PATCH_IF (opcode = 101) BEGIN
        SET patch_item = 1
      END
    END
  END

  PATCH_IF (patch_item = 0) BEGIN
    GET_OFFSET_ARRAY itm_headers  ITM_V10_HEADERS
    PHP_EACH itm_headers AS num => header_off BEGIN
      GET_OFFSET_ARRAY2 itm_effects header_off ITM_V10_HEAD_EFFECTS
      PHP_EACH itm_effects AS num => effect_off BEGIN
        PATCH_IF (patch_item = 0) BEGIN
          READ_SHORT effect_off opcode
          PATCH_IF (opcode = 101) BEGIN
            SET patch_item = 1
          END
        END
      END
    END
  END

  PATCH_IF (patch_item = 1) BEGIN
    PATCH_IF (nonurse = 0) BEGIN
      LPF nurse_feeblemind_string END
    END

    PATCH_IF (noconfessor = 0) BEGIN
      LPF confessor_entangle_items END
    END

    SET patch_item = 0
  END

BUT_ONLY


END


/*
*/