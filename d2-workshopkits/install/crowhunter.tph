//Crow Hunter (d2wka)


//Add passive effects
COPY ~%passives%/damagevstype.SPL~  ~override/%e1%.SPL~
  LPF ALTER_EFFECT INT_VAR parameter1=1 parameter2=3 special=1 END    // +1 vs. humanoids
COPY ~%passives%/damagevstype.SPL~  ~override/%e7%.SPL~
  LPF ALTER_EFFECT INT_VAR parameter1=1 parameter2=3 special=2 END    // +2 vs. humanoids
  LPF d2_remove_effects_on_cast STR_VAR resource=~%e1%~ END
COPY ~%passives%/damagevstype.SPL~  ~override/%e8%.SPL~
  LPF ALTER_EFFECT INT_VAR parameter1=1 parameter2=3 special=3 END    // +3 vs. humanoids
  LPF d2_remove_effects_on_cast STR_VAR resource=~%e7%~ END

COPY ~%passives%/useeff.SPL~           ~override/%e3%.SPL~    // for hla traps
  LPF ALTER_EFFECT STR_VAR resource=~%e3%~ END
COPY ~%passives%/settraps1second.EFF~  ~override/%e3%.EFF~
  WRITE_LONG 0x1c 200
  WRITE_LONG 0x24 9
  WRITE_LONG 0x28 0

COPY ~%passives%/opcode206.SPL~   ~override/%e6%.SPL~    // set snares unusable
  LPF ALTER_EFFECT STR_VAR resource=~SPCL412~ END
  LPF CLONE_EFFECT STR_VAR resource=~D5CL412~ END


//remove set snares (for multiclass kits)
COPY ~%abilities%/applyeffects.SPL~  ~override/%e2%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%e2%a~ END

COPY ~%passives%/removespell.SPL~    ~override/%e2%a.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~SPCL412~ END

LAF d2_recast_at_interval STR_VAR spell=~%e2%~ new_spell=~%@2%~ END


//Add Splintering Shot
COPY ~%icons%/calledshot.BAM~           ~override/%s1%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@113) STR_VAR bam=~%s1%i~ RET icon END

COPY ~%icons%/splinteringshot.BAM~      ~override/%s1%.BAM~
COPY ~%abilities%/splinteringshot.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @113
  SAY UNIDENTIFIED_DESC @213
  WRITE_ASCII 0x3a ~%s1%~ #8
  WRITE_ASCII 0x76 ~%s1%~ #8
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=249 STR_VAR resource=~%s1%a~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/castspelltarget.EFF~        ~override/%s1%a.EFF~
  WRITE_ASCII 0x30 ~%s1%a~ #8
COPY ~%abilities%/splinteringshottarget.SPL~  ~override/%s1%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%s1%s~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~%s1%b~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~%s1%s~ END

COPY - ~%abilities%/invisible15enemy.PRO~ ~%workspace%/%s1%s.PRO~
ADD_PROJECTILE ~%workspace%/%s1%s.PRO~
COPY ~%abilities%/castspelltarget.EFF~        ~override/%s1%s.EFF~
  WRITE_ASCII 0x30 ~%s1%s~ #8
COPY ~%abilities%/splinteringshotburst.SPL~   ~override/%s1%s.SPL~
  WRITE_SHORT 0x98 ~%s1%s~    // projectile

COPY ~%icons%/bleed.BAM~                ~override/%s1%b.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@302) STR_VAR bam=~%s1%b~ RET icon END
COPY ~%abilities%/castspelltarget.EFF~  ~override/%s1%b.EFF~
  WRITE_ASCII 0x30 ~%s1%b~ #8
COPY ~%abilities%/bleed10rounds.SPL~    ~override/%s1%b.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@302) END    // "Bleeding"
  LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%s1%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~%s1%c~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=206 STR_VAR resource=~%s1%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  WRITE_SHORT 0x98 ~%s1%s~    // projectile

COPY ~%abilities%/castspelltarget.EFF~  ~override/%s1%c.EFF~    // bleed damage
  WRITE_ASCII 0x30 ~%s1%c~ #8
COPY ~%abilities%/bleeddamage1.SPL~     ~override/%s1%c.SPL~


//Bleeding removed by Heal or Greater Restoration
ACTION_PHP_EACH d2restoration AS spell => type BEGIN
  ACTION_IF !(~%type%~ STRING_CONTAINS_REGEXP ~heal~) BEGIN    // if 'type' DOES contain 'heal'
    ACTION_IF (FILE_EXISTS_IN_GAME ~%spell%.spl~) BEGIN
      COPY_EXISTING ~%spell%.spl~  ~override~
        LPF d2_remove_effects_on_cast STR_VAR resource=~%s1%b~ insert=last END
      BUT_ONLY
    END
  END
END


//Add Visceral Attack
COPY ~%icons%/visceralattack.BAM~      ~override/%s2%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@114) STR_VAR bam=~%s2%i~ RET icon END
COPY ~%abilities%/visceralattack.SPL~  ~override/%s2%.SPL~
  SAY NAME1 @114
  SAY UNIDENTIFIED_DESC @214
  WRITE_ASCII 0x3a ~SPWI601B~ #8
  WRITE_ASCII 0x76 ~SPWI601B~ #8
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s2%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s2%b~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s2%a~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/castspellself.EFF~          ~override/%s2%a.EFF~    // cure/haste
  WRITE_ASCII 0x30 ~%s2%a~ #8
COPY ~%abilities%/visceralattackeffects.SPL~  ~override/%s2%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=14023 END      // "Hasted" in all games

COPY ~%abilities%/castspellself.EFF~          ~override/%s2%b.EFF~       // remove effects
  WRITE_ASCII 0x30 ~%s2%b~ #8
COPY ~%abilities%/removespelleffects.SPL~     ~override/%s2%b.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%s2%~ END


//Make clab table
COPY ~inlined/d2clab_thief_notraps~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=4 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=9 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder INT_VAR level=13 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder INT_VAR level=18 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 STR_VAR resource=~%e7%~ END
  LPF d2_kit_ability_adder INT_VAR level=10 passive=1 STR_VAR resource=~%e8%~ END
  LPF d2_kit_ability_adder INT_VAR level=14 passive=1 STR_VAR resource=~%e3%~ END    // for hla traps

  //prevent gaining/using traps (for custom multiclass)
  LPF d2_kit_ability_adder INT_VAR passive=1 multi=1 increment=1 STR_VAR resource=~%@2%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e6%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 4
    mixed = RESOLVE_STR_REF (@101001)
    lower = RESOLVE_STR_REF (@101002)
    help = RESOLVE_STR_REF (@101003)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~                  // Usable by all races, if base class is usable
    thiefscl = ~100 100 100 100 100 100 0 0~    // no points in set traps
    thiefskl = ~40 20~                          // start with 40, gain 20 per level
    clascolr = ~93 182 136 184 93~              // use Stalker colors
  RET 
    kit_id
END


//Gains stalker effects from items
OUTER_TEXT_SPRINT kit_group ~stalker~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~

ACTION_PHP_EACH item_kit_effects AS item => class BEGIN
  ACTION_IF !(~%kit_group%~ STRING_CONTAINS_REGEXP ~%class%~) BEGIN
    ACTION_FOR_EACH res IN ~SPL~ ~ITM~ BEGIN
      ACTION_IF (FILE_EXISTS_IN_GAME ~%item%.%res%~) BEGIN
        COPY_EXISTING ~%item%.%res%~  ~override~
          PHP_EACH kit_ids_array AS class2 => eff_to_copy BEGIN
            PATCH_IF (~%class%~ STR_EQ ~%class2%~) BEGIN
              LPF CLONE_EFFECT 
                INT_VAR 
                  silent            = 1
                  match_opcode      = 177 
                  match_parameter1  = %eff_to_copy%
                  match_parameter2  = 9 
                  parameter1        = IDS_OF_SYMBOL (kit ~%ids_name%~)
              END
            END
          END
        BUT_ONLY
      END
    END
  END
END


//Set variables for external use
OUTER_TEXT_SPRINT crowhunter        ~%kitid%~
OUTER_TEXT_SPRINT splinteringshot   ~%s1%~
OUTER_TEXT_SPRINT visceralattack    ~%s2%~
OUTER_TEXT_SPRINT crowhunterbleed   ~%s1%b~


/*
*/