/*
Scripts for conditional items
*/


//SoD scripts
ACTION_IF (GAME_INCLUDES ~sod~) BEGIN


//Add Calamity Ring to SoD (if not obtained)
<<<<<<<< .../temp/variables
IF
	PartyHasItem("%calamity%")
	!Global("d2SoDCalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2SoDCalamityRing","GLOBAL",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/variables~  ~%workspace%/variables.BAF~
  EVALUATE_BUFFER

ACTION_FOR_EACH script IN ~%bg1%2300~ ~bd0108~ BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%script%.BCS~) BEGIN
    EXTEND_TOP ~%script%.BCS~  ~%workspace%/variables.BAF~
  END
END


<<<<<<<< .../temp/calamity
IF
	!Global("d2SoDCalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2SoDCalamityRing","GLOBAL",1)
		ActionOverride("Container1",CreateItem("%calamity%",0,0,0))
		Continue()
END
>>>>>>>>

COPY - ~.../temp/calamity~  ~%workspace%/calamity.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~bd0108.BCS~     ~%workspace%/calamity.BAF~


END



//BG2EE scripts
ACTION_IF (GAME_INCLUDES ~bg2~) BEGIN


//Add Calamity Ring to ToB (if not obtained)
<<<<<<<< .../temp/variables
IF
	PartyHasItem("%calamity%")
	!Global("d2ToBCalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2ToBCalamityRing","GLOBAL",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/variables~  ~%workspace%/variables.BAF~
  EVALUATE_BUFFER
EXTEND_TOP ~AR0700.BCS~  ~%workspace%/variables.BAF~
EXTEND_TOP ~AR2807.BCS~  ~%workspace%/variables.BAF~
EXTEND_TOP ~AR4500.BCS~  ~%workspace%/variables.BAF~


<<<<<<<< .../temp/dragon
IF
	!Global("d2ToBCalamityRing","GLOBAL",1)
	!Global("d2BG2CalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2BG2CalamityRing","GLOBAL",1)
		GiveItemCreate("%calamity%","Dragblac",0,0,0)    // BG2EE: target script name in CRE file
		Continue()
END
>>>>>>>>

COPY - ~.../temp/dragon~    ~%workspace%/dragon.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~AR2807.BCS~  ~%workspace%/dragon.BAF~


<<<<<<<< .../temp/calamity
IF
	!Global("d2ToBCalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2ToBCalamityRing","GLOBAL",1)
		ActionOverride("Container2",CreateItem("%calamity%",0,0,0))
		Continue()
END
>>>>>>>>

COPY - ~.../temp/calamity~  ~%workspace%/calamity.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~AR4500.BCS~     ~%workspace%/calamity.BAF~


END



//IWDEE: Scripts or dialogues
ACTION_IF (GAME_IS ~iwdee~) BEGIN


//Add items to Gerth's store (by script)
<<<<<<<< .../temp/gerthstore
IF
	!Global("d2gerthhunterrune","GLOBAL",1)
	GlobalGT("MIREK_YETI","AR2100",0)
THEN
	RESPONSE #100
        SetGlobal("d2gerthhunterrune","GLOBAL",1)
		AddStoreItem("KUGERTH","%hunter_rune%",1,1)
		Continue()
END

IF
	!Global("d2gerthflynnring","GLOBAL",1)
	GlobalGT("Chapter","GLOBAL",2)
THEN
	RESPONSE #100
        SetGlobal("d2gerthflynnring","GLOBAL",1)
		AddStoreItem("KUGERTH","%flynnring%",1,1)
		Continue()
END

IF
	!Global("d2gerthetherlimiter","GLOBAL",1)
	GlobalGT("Chapter","GLOBAL",3)
THEN
	RESPONSE #100
        SetGlobal("d2gerthetherlimiter","GLOBAL",1)
		AddStoreItem("KUGERTH","%etherlimiter%",1,1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/gerthstore~  ~%workspace%/gerthstore.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~ar2100.BCS~    ~%workspace%/gerthstore.BAF~
EXTEND_BOTTOM ~ar2113.BCS~    ~%workspace%/gerthstore.BAF~



//Add Cosmic Watcher Badge (if Flozem leaves)
<<<<<<<< .../temp/flozem_badge
ADD_TRANS_ACTION ~DFLOZEM~ BEGIN 4 END    // state list
                           BEGIN 0 1 END  // transition list
  ~CreateItem("%badge_cosmic%",0,0,0)~
>>>>>>>>

COPY - ~.../temp/flozem_badge~  ~%workspace%/flozem_badge.d~
  EVALUATE_BUFFER
COMPILE ~%workspace%/flozem_badge.d~



//Add Milkweed rune (by dialogue)
<<<<<<<< .../temp/oldjed
EXTEND_BOTTOM ~DOLDJED~ 14 15 16

IF ~ GlobalGT("Siren_Quest","GLOBAL",2)  // did Siren quest
     GlobalGT("Arbor_Total","GLOBAL",3)  // did Arboretum quest
     Global("d2JedMilkweed","GLOBAL",0)
   ~ DO ~ SetGlobal("d2JedMilkweed","GLOBAL",1)
          GiveItemCreate("%milkweed%",LastTalkedToBy,0,0,0) 
        ~ GOTO JedMilkweed

END


APPEND ~DOLDJED~

IF ~~ BEGIN JedMilkweed
  SAY @50201  // Jed says stuff
  COPY_TRANS ~DOLDJED~ 14    // Jed goes to sleep
END

END
>>>>>>>>

COPY - ~.../temp/oldjed~  ~%workspace%/oldjed.d~
  EVALUATE_BUFFER
COMPILE ~%workspace%/oldjed.d~



//Add Calamity Ring to HoW (if not obtained)
<<<<<<<< .../temp/variables
IF
	PartyHasItem("%calamity%")
	!Global("d2HoWCalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HoWCalamityRing","GLOBAL",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/variables~  ~%workspace%/variables.BAF~
  EVALUATE_BUFFER
EXTEND_TOP ~AR2100.BCS~  ~%workspace%/variables.BAF~
EXTEND_TOP ~AR9600.BCS~  ~%workspace%/variables.BAF~
EXTEND_TOP ~AR9604.BCS~  ~%workspace%/variables.BAF~


<<<<<<<< .../temp/calamity
IF
	!Global("d2HoWCalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HoWCalamityRing","GLOBAL",1)
		GiveItemCreate("%calamity%","Icasaracht",0,0,0)    // IWDEE: target actor name in ARE file
		Continue()
END
>>>>>>>>

COPY - ~.../temp/calamity~  ~%workspace%/calamity.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~AR9604.BCS~  ~%workspace%/calamity.BAF~


END


/*
*/