/*
Scripts for conditional items
*/


//
//BG1 scripts
ACTION_IF (GAME_INCLUDES ~bg1~) BEGIN


<<<<<<<< .../temp/aecdemonsoul
IF
	!Global("d2aecdemonsoul","GLOBAL",1)
	Dead("cultd1")  // Cult Guard
	Dead("cultd2")  // Cult Guard
	Dead("cultd3")  // Cult Guard
	Dead("cultd4")  // Cult Guard
	Dead("cultd5")  // Cult Guard
	Dead("cultd6")  // Cult Guard
	!Dead("tanar")  // Aec'Letec
THEN
	RESPONSE #100
		SetGlobal("d2aecdemonsoul","GLOBAL",1)
		GiveItemCreate("%demonsoul%","TANAR",1,0,0)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/aecdemonsoul~  ~%workspace%/aecdemonsoul.BAF~
  EVALUATE_BUFFER
EXTEND_TOP ~%bg1%1002.BCS~      ~%workspace%/aecdemonsoul.BAF~


END


//
//SoD scripts
ACTION_IF (GAME_INCLUDES ~sod~) BEGIN


//Add Calamity Ring to SoD (if not obtained)
<<<<<<<< .../temp/variables
IF
	PartyHasItem("%calamity%")
	!Global("d2SoDCalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2SoDCalamityRing","GLOBAL",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/variables~  ~%workspace%/variables.BAF~
  EVALUATE_BUFFER

ACTION_FOR_EACH script IN ~%bg1%2300~ ~bd0108~ BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%script%.BCS~) BEGIN
    EXTEND_TOP ~%script%.BCS~  ~%workspace%/variables.BAF~
  END
END


<<<<<<<< .../temp/calamity
IF
	!Global("d2SoDCalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2SoDCalamityRing","GLOBAL",1)
		ActionOverride("Container1",CreateItem("%calamity%",0,0,0))
		Continue()
END
>>>>>>>>

COPY - ~.../temp/calamity~  ~%workspace%/calamity.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~bd0108.BCS~  ~%workspace%/calamity.BAF~


END


//
//BG2EE scripts
ACTION_IF (GAME_INCLUDES ~bg2~) BEGIN


//Add Calamity Ring to ToB (if not obtained)
<<<<<<<< .../temp/variables
IF
	PartyHasItem("%calamity%")
	!Global("d2ToBCalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2ToBCalamityRing","GLOBAL",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/variables~  ~%workspace%/variables.BAF~
  EVALUATE_BUFFER
EXTEND_TOP ~AR0700.BCS~  ~%workspace%/variables.BAF~
EXTEND_TOP ~AR2807.BCS~  ~%workspace%/variables.BAF~
EXTEND_TOP ~AR4500.BCS~  ~%workspace%/variables.BAF~


<<<<<<<< .../temp/dragon
IF
	!Global("d2ToBCalamityRing","GLOBAL",1)
	!Global("d2BG2CalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2BG2CalamityRing","GLOBAL",1)
		GiveItemCreate("%calamity%","Dragblac",0,0,0)    // BG2EE: target script name in CRE file
		Continue()
END
>>>>>>>>

COPY - ~.../temp/dragon~    ~%workspace%/dragon.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~AR2807.BCS~  ~%workspace%/dragon.BAF~


<<<<<<<< .../temp/calamity
IF
	!Global("d2ToBCalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2ToBCalamityRing","GLOBAL",1)
		ActionOverride("Container2",CreateItem("%calamity%",0,0,0))
		Continue()
END
>>>>>>>>

COPY - ~.../temp/calamity~  ~%workspace%/calamity.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~AR4500.BCS~  ~%workspace%/calamity.BAF~


END


//
//IWDEE scripts
ACTION_IF (GAME_IS ~iwdee~) BEGIN


//Add items to Gerth's store (by script)
<<<<<<<< .../temp/gerthstore
IF
	!Global("d2gerthhunterrune","GLOBAL",1)
	GlobalGT("MIREK_YETI","AR2100",0)
THEN
	RESPONSE #100
		SetGlobal("d2gerthhunterrune","GLOBAL",1)
		AddStoreItem("KUGERTH","%hunter_rune%",1,1)
		Continue()
END

IF
	!Global("d2gerthflynnring","GLOBAL",1)
	GlobalGT("Chapter","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("d2gerthflynnring","GLOBAL",1)
		AddStoreItem("KUGERTH","%flynnring%",1,1)
		Continue()
END

IF
	!Global("d2gerthetherlimiter","GLOBAL",1)
	GlobalGT("Chapter","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("d2gerthetherlimiter","GLOBAL",1)
		AddStoreItem("KUGERTH","%etherlimiter%",1,1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/gerthstore~  ~%workspace%/gerthstore.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~ar2100.BCS~    ~%workspace%/gerthstore.BAF~
EXTEND_BOTTOM ~ar2113.BCS~    ~%workspace%/gerthstore.BAF~


//
//HoW scripts
<<<<<<<< .../temp/variables
IF
	PartyHasItem("%calamity%")
	!Global("d2HoWCalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HoWCalamityRing","GLOBAL",1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/variables~  ~%workspace%/variables.BAF~
  EVALUATE_BUFFER
EXTEND_TOP ~AR2100.BCS~  ~%workspace%/variables.BAF~
EXTEND_TOP ~AR9600.BCS~  ~%workspace%/variables.BAF~
EXTEND_TOP ~AR9604.BCS~  ~%workspace%/variables.BAF~


<<<<<<<< .../temp/calamity
IF
	!Global("d2HoWCalamityRing","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2HoWCalamityRing","GLOBAL",1)
		GiveItemCreate("%calamity%","Icasaracht",0,0,0)    // IWDEE: target actor name in ARE file
		Continue()
END
>>>>>>>>

COPY - ~.../temp/calamity~  ~%workspace%/calamity.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~AR9604.BCS~  ~%workspace%/calamity.BAF~


END


//
//moon rune chicken
LAF d2_color_string INT_VAR stringref=RESOLVE_STR_REF (@60101) STR_VAR color=cloud RET newstring END

<<<<<<<< .../temp/d2moonrunehint
IF
	!GlobalTimerNotExpired("d2moonrunehint","GLOBAL")
	!See([EVILCUTOFF])
	Range([PC],8)
	HasItem("%moon_rune%",Myself)
THEN
	RESPONSE #100
		SetGlobalTimer("d2moonrunehint","GLOBAL",300)
		DisplayStringHeadNoLog(Myself,"%newstring%")
		PlaySound("AMB_E39B")
END
>>>>>>>>

COPY - ~.../temp/d2moonrunehint~  ~inlined/d2wkmrc.BAF~
  EVALUATE_BUFFER
  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    REPLACE_TEXTUALLY ~!See~  ~Global("AR2003_visited","GLOBAL",1) !See~    // visited tower (IWDEE)
  END
COMPILE ~inlined/d2wkmrc.BAF~


/*
*/