//Nurse (d2wkg)


//Add passive effects
COPY ~%passives%/hitdamage1ranged.SPL~  ~override/%e1%.SPL~
  LPF DELETE_EFFECT INT_VAR match_opcode=286 END


//Add Medical Darts (item)
COPY ~%icons%/medicaldarticon.BAM~  ~override/%s1%i.BAM~
COPY ~%icons%/medicaldartdesc.BAM~  ~override/%s1%d.BAM~
COPY ~%abilities%/medicaldart.ITM~  ~override/%s1%e.ITM~
  SAY NAME1 @619
  SAY NAME2 @619
  WRITE_ASCII 0x3a ~%s1%i~ #8    // icon
  WRITE_ASCII 0x76 ~%s1%i~ #8
  WRITE_ASCII 0x58 ~%s1%d~ #8
  LPF ALTER_EFFECT INT_VAR match_opcode=324 STR_VAR resource=~%s1%e~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=177 match_parameter1=255 STR_VAR resource=~%s1%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=177 match_parameter1=199 STR_VAR resource=~%s1%a~ END


//Add Medical Darts (effects)
COPY ~%icons%/medicaldarts.BAM~     ~override/%s1%.BAM~
COPY ~%abilities%/medicaldart.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @126
  SAY UNIDENTIFIED_DESC @226
  WRITE_ASCII 0x3a ~%s1%~ #8
  WRITE_ASCII 0x76 ~%s1%~ #8
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%x~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=111 STR_VAR resource=~%s1%e~ END

COPY ~%abilities%/castspelltarget.EFF~  ~override/%s1%a.EFF~    // heal
  WRITE_ASCII 0x30 ~%s1%a~ #8
COPY ~%abilities%/medicaldarta.SPL~     ~override/%s1%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@307) END    // "Healed"
  LPF CD_EXTEND-O-MATIC INT_VAR step_size=3 level_cap=15 min_lev_alt=3 END
  LPF ALTER_EFFECT INT_VAR header=1 match_opcode=17 parameter1=2 END
  LPF ALTER_EFFECT INT_VAR header=2 match_opcode=17 parameter1=3 END
  LPF ALTER_EFFECT INT_VAR header=3 match_opcode=17 parameter1=4 END
  LPF ALTER_EFFECT INT_VAR header=4 match_opcode=17 parameter1=5 END

COPY ~%abilities%/castspelltarget.EFF~  ~override/%s1%b.EFF~    // poison
  WRITE_ASCII 0x30 ~%s1%b~ #8
COPY ~%abilities%/medicaldartb.SPL~     ~override/%s1%b.SPL~
  LPF CD_EXTEND-O-MATIC INT_VAR step_size=3 level_cap=15 min_lev_alt=3 END
  LPF ALTER_EFFECT INT_VAR header=1 match_opcode=12 parameter1=2 END
  LPF ALTER_EFFECT INT_VAR header=2 match_opcode=12 parameter1=3 END
  LPF ALTER_EFFECT INT_VAR header=3 match_opcode=12 parameter1=4 END
  LPF ALTER_EFFECT INT_VAR header=4 match_opcode=12 parameter1=5 END


//variables
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  OUTER_SET feeblemind = 14407
  OUTER_SET confuse    = 37604
END
ELSE BEGIN
  ACTION_IF (GAME_IS ~bgee~) BEGIN
    OUTER_SET feeblemind = 23744
    OUTER_SET confuse    = 25807
  END
  ELSE BEGIN
    OUTER_SET feeblemind = 23744
    OUTER_SET confuse    = 14782
  END
END


/*moved to morespellpatches.tph
//block feeblemind string in spells/items that block feeblemind
COPY_EXISTING_REGEXP GLOB ~.*\.SPL~  ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode=267 match_parameter1=~%feeblemind%~ END
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=76 opcode=267 parameter1=~%feeblemind%~ END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~.*\.ITM~  ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode=267 match_parameter1=~%feeblemind%~ END
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=76 opcode=267 parameter1=~%feeblemind%~ END
BUT_ONLY
*/


//Add Injection Shot
COPY ~%icons%/injectionshoti.BAM~     ~override/%s2%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@127) STR_VAR bam=~%s2%i~ RET icon END

COPY ~%icons%/injectionshot.BAM~      ~override/%s2%.BAM~
COPY ~%abilities%/injectionshot.SPL~  ~override/%s2%.SPL~
  SAY NAME1 @127
  SAY UNIDENTIFIED_DESC @227
  WRITE_ASCII 0x3a ~%s2%~ #8
  WRITE_ASCII 0x76 ~%s2%~ #8
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%x~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=249 STR_VAR resource=~%s0%x~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=249 STR_VAR resource=~%s2%e~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/castspelltarget.EFF~  ~override/%s2%e.EFF~
  WRITE_ASCII 0x30 ~%s2%e~ #8
COPY ~%abilities%/applyeffects.SPL~     ~override/%s2%e.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s2%b~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s2%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=324 timing=0 parameter2=55 STR_VAR resource=~%s2%e~ END

COPY ~%abilities%/injectionshota.SPL~   ~override/%s2%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%s2%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=~%feeblemind%~ END    // "Feebleminded"
COPY ~%abilities%/injectionshotb.SPL~   ~override/%s2%b.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=~%confuse%~ END       // "Confused"


//Remove effects
COPY ~%abilities%/castspellself.EFF~  ~override/%s0%x.EFF~
  WRITE_ASCII 0x30 ~%s0%x~ #8
COPY ~%abilities%/removespelleffects.SPL~  ~override/%s0%x.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s2%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=112 STR_VAR resource=~%s1%e~ END


//Clone base thief 2da file
COPY_EXISTING ~CLABTH01.2DA~  ~override/%kitid%.2DA~
  LPF 2da_find_row_delete STR_VAR row_name = ~QD_MULTI~ END
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 multi=1 increment=5 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=5 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=11 multi=1 increment=4 limit=19 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 4
    mixed = RESOLVE_STR_REF (@101601)
    lower = RESOLVE_STR_REF (@101602)
    help = RESOLVE_STR_REF (@101603)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~    // Usable by all races, if base class is usable
    weapprof = ~1 1 2 0 1 0 0 2 0 1 1 0 0 1 1 1 0 1 0 0 0 0 1 2 0 2 2 2 1 1 1 1~
    thiefskl = ~40 15~            // start with 40, gain 15 per level
    clascolr = ~25 28 57 2 24~    // inquisitor colors
  RET 
    kit_id
END


//Usability - no studded leather or hide armor
COPY_EXISTING_REGEXP GLOB ~.*\.ITM~  ~override~
  READ_LONG  0x8 stringref    // check unidentified name
  PATCH_IF ((stringref = 6664) OR     // if Studded Leather
            (stringref = 6665) OR 
            (stringref = 6666) OR
            (stringref = 22714) OR    // if hide armor
            (stringref = 22715) OR
            (stringref = 40488)
    ) BEGIN
    LPF ADD_ITEM_EQEFFECT
      INT_VAR
        opcode = 319
        target = 1
        parameter1 = IDS_OF_SYMBOL (kit ~D2%kitname%~)
        parameter2 = 9
        timing = 2
    END
  END
BUT_ONLY


//Gains archer effects from items
OUTER_TEXT_SPRINT kit_group ~archer~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~

ACTION_PHP_EACH item_kit_effects AS item => class BEGIN
  ACTION_IF !(~%kit_group%~ STRING_CONTAINS_REGEXP ~%class%~) BEGIN
    ACTION_FOR_EACH res IN ~SPL~ ~ITM~ BEGIN
      ACTION_IF (FILE_EXISTS_IN_GAME ~%item%.%res%~) BEGIN
        COPY_EXISTING ~%item%.%res%~  ~override~
          PHP_EACH kit_ids_array AS class2 => eff_to_copy BEGIN
            PATCH_IF (~%class%~ STR_EQ ~%class2%~) BEGIN
              LPF CLONE_EFFECT 
                INT_VAR 
                  silent            = 1
                  match_opcode      = 177 
                  match_parameter1  = %eff_to_copy%
                  match_parameter2  = 9 
                  parameter1        = IDS_OF_SYMBOL (kit ~%ids_name%~)
              END
            END
          END
        BUT_ONLY
      END
    END
  END
END


//function to block feeblemind string in spells/items
DEFINE_PATCH_FUNCTION nurse_feeblemind_string BEGIN

  LPF DELETE_EFFECT INT_VAR match_opcode=267 match_parameter1=~%feeblemind%~ END
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=76 opcode=267 parameter1=~%feeblemind%~ END

END


//Set variables for external use
OUTER_TEXT_SPRINT nurse          ~%kitid%~
OUTER_TEXT_SPRINT medicaldarts   ~%s1%~
OUTER_TEXT_SPRINT injectionshot  ~%s2%~


/*
*/