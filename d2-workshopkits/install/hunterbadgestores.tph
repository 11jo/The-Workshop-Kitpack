/*
Add Hunter Badges to stores
*/


ACTION_IF (nobeasthunter = 0) BEGIN


//BGEE stores
ACTION_IF (GAME_INCLUDES ~bg1~) BEGIN
  COPY_EXISTING ~sto0706.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_spark%~ flags=~identified~ END
  BUT_ONLY

  COPY_EXISTING ~TEM0131.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_pkeg%~ flags=~identified~ END
  BUT_ONLY
END

//SoD stores
ACTION_IF (GAME_INCLUDES ~sod~) BEGIN
  COPY_EXISTING ~BDKAZZRE.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_spark%~ position=~AFTER bdamul24~ flags=~identified~ END
  BUT_ONLY

  COPY_EXISTING ~BDHEROD.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_pkeg%~ flags=~identified~ END
  BUT_ONLY

  COPY_EXISTING ~BDPOLVI.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_old%~ flags=~identified~ END
  BUT_ONLY
END



//BG2EE stores
ACTION_IF (GAME_INCLUDES ~bg2~) BEGIN
  COPY_EXISTING ~TEMTALOS.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_spark%~ flags=~identified~ END
  BUT_ONLY

  COPY_EXISTING ~TRGENI01.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_radiant%~ flags=~identified~ END
  BUT_ONLY

  COPY_EXISTING ~RIBALD3.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_cosmic%~ flags=~identified~ END
  BUT_ONLY

  COPY_EXISTING ~BERNARD2.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_pkeg%~ flags=~identified~ END
  BUT_ONLY

  COPY_EXISTING ~udduer01.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_saw%~ flags=~identified~ END
  BUT_ONLY

  COPY_EXISTING ~PPSTOR01.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_old%~ flags=~identified~ END
  BUT_ONLY

//ToB stores
  ACTION_IF (nohunterbadgesToB = 0) BEGIN
    COPY_EXISTING ~25SPELL.STO~  ~override~
      LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 stack=3 STR_VAR item_name=~%badge_pkeg%~ flags=~identified~ END
      LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 stack=3 STR_VAR item_name=~%badge_cosmic%~ flags=~identified~ END
    BUT_ONLY

    COPY_EXISTING ~HGKAR01.STO~  ~override~
      LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 stack=3 STR_VAR item_name=~%badge_saw%~ flags=~identified~ END
      LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 stack=3 STR_VAR item_name=~%badge_radiant%~ flags=~identified~ END
    BUT_ONLY

    COPY_EXISTING ~AMBAR01.STO~  ~override~
      LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 stack=3 STR_VAR item_name=~%badge_old%~ flags=~identified~ END
      LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 stack=3 STR_VAR item_name=~%badge_spark%~ flags=~identified~ END
    BUT_ONLY
  END
END



//IWDEE stores
ACTION_IF (GAME_IS ~iwdee~) BEGIN

//Add badge to Gerth's store (by script)
<<<<<<<< .../temp/gerthbadge
IF
	!Global("d2gerthsparkhunter","GLOBAL",1)
	GlobalGT("Chapter","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("d2gerthsparkhunter","GLOBAL",1)
		AddStoreItem("KUGERTH","%badge_spark%",1,1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/gerthbadge~  ~%workspace%/gerthbadge.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~ar2100.BCS~    ~%workspace%/gerthbadge.BAF~
EXTEND_BOTTOM ~ar2113.BCS~    ~%workspace%/gerthbadge.BAF~



//Add badge to Orrick store (did not convince Flozem to leave)
<<<<<<<< .../temp/orrickbadge
IF
	!Global("d2orrickcosmicwatcher","GLOBAL",1)
	!Global("SPRITE_IS_DEADFLOZEM","GLOBAL",0)    // if killed Flozem
	!Global("Flozem_Gone","GLOBAL",1)             // did not convince Flozem to leave
THEN
	RESPONSE #100
		SetGlobal("d2orrickcosmicwatcher","GLOBAL",1)
		AddStoreItem("KUORK0","%badge_cosmic%",1,1)
		Continue()
END
>>>>>>>>

COPY - ~.../temp/orrickbadge~  ~%workspace%/orrickbadge.BAF~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~ar2100.BCS~     ~%workspace%/orrickbadge.BAF~
EXTEND_BOTTOM ~AR2102.BCS~     ~%workspace%/orrickbadge.BAF~



//Add Cosmic Watcher Badge (convinced Flozem to leave)
<<<<<<<< .../temp/flozem_badge
ADD_TRANS_ACTION ~DFLOZEM~ BEGIN 4 END    // state list
                           BEGIN 0 1 END  // transition list
  ~CreateItem("%badge_cosmic%",0,0,0)~
>>>>>>>>

COPY - ~.../temp/flozem_badge~  ~%workspace%/flozem_badge.d~
  EVALUATE_BUFFER
COMPILE ~%workspace%/flozem_badge.d~



//add to store/container
  COPY_EXISTING ~SHLEHLAN.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_radiant%~ flags=~identified~ END
  BUT_ONLY

  COPY_EXISTING ~DE_LIB.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_pkeg%~ flags=~identified~ END
  BUT_ONLY

  COPY_EXISTING ~LDD_NYM.STO~  ~override~
    LPF ADD_STORE_ITEM_EX STR_VAR item_name=~%badge_saw%~ flags=~identified~ END
  BUT_ONLY

  COPY_EXISTING ~AR9101.ARE~ ~override~
    LPF ADD_AREA_ITEM INT_VAR container_to_add_to=2 STR_VAR item_to_add=~%badge_old%~ END
  BUT_ONLY


//HoW stores
  ACTION_IF (nohunterbadgesHoW = 0) BEGIN
    COPY_EXISTING ~KIERAN2.STO~  ~override~
      LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 stack=3 STR_VAR item_name=~%badge_radiant%~ flags=~identified~ END
      LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 stack=3 STR_VAR item_name=~%badge_spark%~ flags=~identified~ END
    BUT_ONLY

    COPY_EXISTING ~EDION.STO~  ~override~
      LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 stack=3 STR_VAR item_name=~%badge_cosmic%~ flags=~identified~ END
      LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 stack=3 STR_VAR item_name=~%badge_old%~ flags=~identified~ END
    BUT_ONLY

    COPY_EXISTING ~EMMERIC.STO~  ~override~
      LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 stack=3 STR_VAR item_name=~%badge_saw%~ flags=~identified~ END
      LPF ADD_STORE_ITEM_EX INT_VAR charge1=1 stack=3 STR_VAR item_name=~%badge_pkeg%~ flags=~identified~ END
    BUT_ONLY
  END
END


END


/*
*/