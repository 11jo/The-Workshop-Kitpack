//Warhound (d2wk2)


//Add passive effects
COPY ~%passives%/acbonus1.SPL~          ~override/%e1%.SPL~
  LPF ALTER_EFFECT INT_VAR parameter2=2 END
COPY ~%passives%/hitdamage1ranged.SPL~  ~override/%e2%.SPL~


//Add Shockwave
COPY ~%icons%/shockwave.BAM~      ~override/%s1%.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@102) STR_VAR bam=~%s1%~ RET icon END
COPY ~%abilities%/shockwave.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @102
  SAY UNIDENTIFIED_DESC @202
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%x~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/hiteffectranged.SPL~     ~override/%s1%a.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%c~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s0%x~ END

COPY - ~%abilities%/shockwave.PRO~ ~%workspace%/%s1%a.PRO~
ADD_PROJECTILE ~%workspace%/%s1%a.PRO~

COPY ~%abilities%/castspelltarget.EFF~     ~override/%s1%b.EFF~
  WRITE_ASCII 0x30 ~%s1%b~ #8
COPY ~%abilities%/shockwaveblast.SPL~      ~override/%s1%b.SPL~
  LPF ALTER_SPELL_HEADER INT_VAR projectile=~%s1%a~ END    // set projectile
COPY ~%abilities%/shockwavepush.EFF~       ~override/%s1%c.EFF~


//Add Incinerate
COPY ~%icons%/incinerate.BAM~      ~override/%s2%.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@103) STR_VAR bam=~%s2%~ RET icon END
COPY ~%abilities%/incinerate.SPL~  ~override/%s2%.SPL~
  SAY NAME1 @103
  SAY UNIDENTIFIED_DESC @203
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s2%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%x~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/hiteffectranged.SPL~     ~override/%s2%a.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s2%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s0%x~ END

COPY - ~%abilities%/incinerate.PRO~ ~%workspace%/%s2%a.PRO~
ADD_PROJECTILE ~%workspace%/%s2%a.PRO~

COPY ~%abilities%/castspelltarget.EFF~     ~override/%s2%b.EFF~
  WRITE_ASCII 0x30 ~%s2%b~ #8
COPY ~%abilities%/incineratecloud.SPL~     ~override/%s2%b.SPL~
  LPF ALTER_SPELL_HEADER INT_VAR projectile=~%s2%a~ END    // set projectile


//Remove effects
COPY ~%abilities%/castspellself.EFF~       ~override/%s0%x.EFF~
  WRITE_ASCII 0x30 ~%s0%x~ #8
COPY ~%abilities%/removespelleffects.SPL~  ~override/%s0%x.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s2%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s2%a~ END


//Add Incinerate to CLEARAIR.2DA
OUTER_SET incinerate = (~%%s2%a%~ - 1)    // projectl.ids number is 1 less than variable given by ADD_PROJECTILE
APPEND ~CLEARAIR.2DA~  ~d2incinerate %incinerate%~

COPY_EXISTING ~CLEARAIR.2DA~ ~override~
  PRETTY_PRINT_2DA
BUT_ONLY


//Add Evasion checks for IWDEE
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  LAF d2_evasion_check STR_VAR spell=~%s1%b~ END
  LAF d2_evasion_check STR_VAR spell=~%s2%b~ END
END


//Make clab table
COPY ~inlined/d2clab_fighter~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 multi=1 increment=5 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=3 passive=1 multi=1 increment=5 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=4 multi=1 increment=4 limit=20 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=4 multi=1 increment=4 limit=20 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 2
    mixed = RESOLVE_STR_REF (@100201)
    lower = RESOLVE_STR_REF (@100202)
    help = RESOLVE_STR_REF (@100203)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~         // Usable by all races, if base class is usable
    weapprof = ~1 1 5 1 1 1 1 5 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 5 5 5 5 5 1 1 1 1~
    clascolr = ~97 137 138 142 188~    // Totemic Druid colors
  RET 
    kit_id
END


//Gains archer effects from items
OUTER_TEXT_SPRINT kit_group ~archer~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%~

ACTION_PHP_EACH item_kit_effects AS item => class BEGIN
  ACTION_IF !(~%kit_group%~ STRING_CONTAINS_REGEXP ~%class%~) BEGIN
    ACTION_FOR_EACH res IN ~SPL~ ~ITM~ BEGIN
      ACTION_IF (FILE_EXISTS_IN_GAME ~%item%.%res%~) BEGIN
        COPY_EXISTING ~%item%.%res%~  ~override~
          PHP_EACH kit_ids_array AS class2 => eff_to_copy BEGIN
            PATCH_IF (~%class%~ STR_EQ ~%class2%~) BEGIN
              LPF CLONE_EFFECT 
                INT_VAR 
                  silent            = 1
                  match_opcode      = 177 
                  match_parameter1  = %eff_to_copy%
                  match_parameter2  = 9 
                  parameter1        = IDS_OF_SYMBOL (kit ~%ids_name%~)
              END
            END
          END
        BUT_ONLY
      END
    END
  END
END


//Set variables for external use
OUTER_TEXT_SPRINT warhound   ~%kitid%~
OUTER_TEXT_SPRINT shockwave  ~%s1%~
OUTER_TEXT_SPRINT incinerate ~%s2%~


/*
- op183 doesn't work on launchers
- can't give bonus when equipped with a launcher
*/