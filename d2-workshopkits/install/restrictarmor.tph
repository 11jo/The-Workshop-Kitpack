/*
armor restrictions
requires d2_restrict_armor.tpa
*/


//add kits to armor array
//will add to array, without removing previous entries

/*
ACTION_DEFINE_ASSOCIATIVE_ARRAY d2_restrict_armor_array BEGIN
  kit_internal_name => armor_type
END
*/

//armor_type (refers to highest wearable):
/*
splint
chain
studded
hide
leather
*/


ACTION_IF (noarmorrestrictions = 0) BEGIN

PRINT @9

COPY_EXISTING_REGEXP GLOB ~.*\.ITM~  ~override~

  PATCH_IF (SOURCE_SIZE > 0xa0) BEGIN    // skip if no effects
    LPF d2_restrict_item_flags RET droppable displayable restrictions END
    PATCH_IF ((%droppable%) AND (%displayable%)) BEGIN
      PATCH_IF (restrictions != 0) BEGIN    // skip anything with no race/class/alignment restrictions

        READ_LONG  0x8 stringref             // unidentified name (as string ref)
        READ_ASCII 0x22 itemtype (2) NULL    // equipped appearance

        PHP_EACH d2_restrict_armor_array AS kit => type BEGIN
          PATCH_IF (RESOURCE_CONTAINS ~kit.ids~  ~%kit%~) BEGIN
            TEXT_SPRINT scale ~scale~
            PATCH_IF (~%type%~ STR_EQ ~splint~) BEGIN  TEXT_SPRINT scale ~dragon~ END
            PATCH_IF (~%type%~ STR_EQ ~chain~)  BEGIN  TEXT_SPRINT scale ~dragon~ END
            LPF d2_scalearmor_check STR_VAR armor=~%scale%~ RET scalearmor END    // returns 1 if match (skip patching)

            PATCH_IF (scalearmor = 0) BEGIN
              LPF ~d2_restrict_armor_%type%~  STR_VAR kit_to_patch = ~%kit%~ END
            END
          END
        END

      END
    END
  END

BUT_ONLY

END


/*
*/