/*
All armor restrictions are now made from this file.
Do for all kits at once, instead of individually.
*/


//add kits to armor array
//will add to array, without removing previous entries

/*
ACTION_DEFINE_ASSOCIATIVE_ARRAY d2_restrict_armor_array BEGIN
  kit_internal_name => armor_type
END
*/

//armor_type (refers to highest wearable):
/*
splint
chain
studded
hide
leather
*/


ACTION_IF (noarmorrestrictions = 0) BEGIN

PRINT @9

COPY_EXISTING_REGEXP GLOB ~.*\.ITM~  ~override~
  PATCH_IF (!(~%SOURCE_RES%~ STRING_EQUAL_CASE ~secret05~) AND    // skip these armors
            !(~%SOURCE_RES%~ STRING_EQUAL_CASE ~d2tec05~) AND
            !(~%SOURCE_RES%~ STRING_EQUAL_CASE ~FISH~)            // skip a fish in IWD
  ) BEGIN

    LPF d2_restrict_armor_flags RET droppable displayable END
    PATCH_IF ((%droppable%) AND (%displayable%)) BEGIN

      READ_LONG  0x8 stringref             // unidentified name (as string ref)
      READ_ASCII 0x22 itemtype (2) NULL    // equipped appearance

      PHP_EACH d2_restrict_armor_array AS kit => type BEGIN
        PATCH_IF (RESOURCE_CONTAINS ~kit.ids~  ~%kit%~) BEGIN
          LPF ~d2_restrict_armor_%type%~  STR_VAR kit_to_patch = ~%kit%~ END
        END
      END

    END
  END
BUT_ONLY

END


/*
*/