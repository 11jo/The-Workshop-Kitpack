//Beast Hunter (d2wk1)

//Add passive effects
COPY ~%passives%/useeff.SPL~          ~override/%e1%.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%e1%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%e1%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%e1%c~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%e1%d~ END
  
COPY ~%passives%/attackvstype3.EFF~   ~override/%e1%a.EFF~
  WRITE_LONG 0x1c IDS_OF_SYMBOL (race ~demonic~)
COPY ~%passives%/damagevstype3.EFF~   ~override/%e1%b.EFF~
  WRITE_LONG 0x1c IDS_OF_SYMBOL (race ~demonic~)
COPY ~%passives%/attackvstype3.EFF~   ~override/%e1%c.EFF~
  WRITE_LONG 0x1c IDS_OF_SYMBOL (race ~lycanthrope~)
COPY ~%passives%/damagevstype3.EFF~   ~override/%e1%d.EFF~
  WRITE_LONG 0x1c IDS_OF_SYMBOL (race ~lycanthrope~)


//Add Hunter Tools (previously Fire Weapon)
COPY ~%icons%/fireweapon.BAM~      ~override/%s1%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@100) STR_VAR bam=~%s1%i~ RET icon END

COPY ~%icons%/huntertools.BAM~     ~override/%s1%.BAM~
COPY ~%abilities%/fireweapon.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @100
  SAY UNIDENTIFIED_DESC @200
  WRITE_ASCII 0x3a ~%s1%~ #8
  WRITE_ASCII 0x76 ~%s1%~ #8
  LPF ALTER_EFFECT INT_VAR match_duration=60 duration=72 END    // duration = 12 rounds
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=248 STR_VAR resource=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=22 parameter1=1 parameter2=0 END     // +1 Luck
  LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=325 parameter1=1 parameter2=0 END    // +1 Saving Throws
//LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=126 parameter1=2 parameter2=0 END    // +2 movement
//LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=190 parameter1=2 parameter2=0 END    // -2 Speed Factor

COPY ~%abilities%/fireweapon.EFF~  ~override/%s1%.EFF~
  WRITE_SHORT 0x22 1    // v3.0: change to acid damage


//Add Quicksilver
COPY ~%icons%/quicksilvericon.BAM~    ~override/%s2%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@101) STR_VAR bam=~%s2%i~ RET icon END

COPY ~%icons%/quicksilver.BAM~        ~override/%s2%.BAM~
COPY ~%abilities%/quicksilver.SPL~    ~override/%s2%.SPL~
  SAY NAME1 @101
  SAY UNIDENTIFIED_DESC @201
  WRITE_ASCII 0x3a ~%s2%~ #8    // icons
  WRITE_ASCII 0x76 ~%s2%~ #8
  LPF ALTER_EFFECT INT_VAR match_duration=60 duration=36 END    // duration = 6 rounds
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s2%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=249 STR_VAR resource=~%s2%a~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%c~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%d~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%e~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%f~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%g~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%h~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%i~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 STR_VAR resource=~%s2%j~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/quicksilverpiercing.EFF~  ~override/%s2%a.EFF~
COPY ~%abilities%/quicksilverarrows.EFF~    ~override/%s2%b.EFF~
COPY ~%abilities%/quicksilverarrows2.EFF~   ~override/%s2%c.EFF~
COPY ~%abilities%/quicksilverarrows3.EFF~   ~override/%s2%d.EFF~
COPY ~%abilities%/quicksilverbolts.EFF~     ~override/%s2%e.EFF~
COPY ~%abilities%/quicksilverbolts2.EFF~    ~override/%s2%f.EFF~
COPY ~%abilities%/quicksilverbolts3.EFF~    ~override/%s2%g.EFF~
COPY ~%abilities%/quicksilverbullets.EFF~   ~override/%s2%h.EFF~
COPY ~%abilities%/quicksilverbullets2.EFF~  ~override/%s2%i.EFF~
COPY ~%abilities%/quicksilverbullets3.EFF~  ~override/%s2%j.EFF~


//Clone base Fighter 2da file
COPY_EXISTING ~CLABFI01.2DA~ ~override/%kitid%.2DA~
  LPF 2da_find_row_delete STR_VAR row_name = ~QD_MULTI~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=4 limit=13 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 2
    mixed = RESOLVE_STR_REF (@100101)
    lower = RESOLVE_STR_REF (@100102)
    help = RESOLVE_STR_REF (@100103)
  STR_VAR
    kit_name = ~D2%kitname%~
    unusable = ~0x00010000~        // Stalker usability
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~     // Usable by all races, if base class is usable
    clascolr = ~28 93 91 91 93~    // Archer colors
  RET 
    kit_id
END


//Set variables for external use
OUTER_TEXT_SPRINT beasthunter  ~%kitid%~
OUTER_TEXT_SPRINT fireweapon   ~%s1%~
OUTER_TEXT_SPRINT quicksilver  ~%s2%~


/*
*/