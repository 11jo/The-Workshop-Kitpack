//Moonlight Knight (c/r)


ACTION_IF (nomoonlightknightcr = 0) BEGIN


//Prevent from getting the vanilla Holy Symbols
COPY ~%passives%/opcode206.SPL~  ~override/%e9%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~cdhlysym~ END


//Add Holy Symbol
COPY ~%files%/holysymbol_core.SPL~    ~override/%m9%.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~%m9%~ END
COPY ~%icons%/holysymbol_moon.BAM~    ~override/%m9%.BAM~
COPY ~%icons%/holysymbol_moon2.BAM~    ~override/%m9%d.BAM~

ACTION_IF (FILE_EXISTS_IN_GAME ~belt12.itm~) BEGIN
  COPY_EXISTING ~belt12.itm~          ~override/%m9%.ITM~    // clone Lathander symbol
    WRITE_LONG 0x1e (THIS BAND BNOT BIT1)                    // remove restrictions
    WRITE_BYTE 0x29 (THIS BAND BNOT BIT6)
END
ELSE BEGIN
  COPY ~%files%/holysymbol_core.ITM~  ~override/%m9%.ITM~    // or add new
END

COPY_EXISTING ~%m9%.ITM~  ~override~
  SAY NAME1 @70123 SAY UNIDENTIFIED_DESC @70223
  SAY NAME2 @70123 SAY DESC @70223
  WRITE_ASCII 0x3a ~%m9%~ #8     // icon
  WRITE_ASCII 0x58 ~%m9%d~ #8    // description image
BUT_ONLY


//FnP spheres
COPY ~%passives%/d5fpclsp.spl~  ~override/%e6%.SPL~
  LPF ALTER_EFFECT STR_VAR match_resource=~d5smwar#~ resource=~d5smmag#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5spben#~ resource=~d5spear#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5splif#~ resource=~d5smlif#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5spkno#~ resource=~d5spexp#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5spfir#~ resource=~d5smast#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5smtho#~ resource=~d5smdec#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5smdea#~ resource=~d5smben#~ END
  LPF ALTER_EFFECT INT_VAR parameter1=0 parameter2=2 END

COPY ~%passives%/d5fprnsp.spl~  ~override/%e7%.SPL~
  LPF ALTER_EFFECT STR_VAR match_resource=~d5sppla#~ resource=~d5smpla#~ END
  LPF ALTER_EFFECT INT_VAR parameter1=0 parameter2=2 END


//Multiclass import fix
LAF d2_mc_import_fix INT_VAR amount=1  STR_VAR spell=~%s1%~ new_spell=~%#1%~ END
LAF d2_mc_import_fix INT_VAR amount=2  STR_VAR spell=~%s1%~ new_spell=~%#2%~ END
LAF d2_mc_import_fix INT_VAR amount=3  STR_VAR spell=~%s1%~ new_spell=~%#3%~ END
LAF d2_mc_import_fix INT_VAR amount=4  STR_VAR spell=~%s1%~ new_spell=~%#4%~ END
LAF d2_mc_import_fix INT_VAR amount=5  STR_VAR spell=~%s1%~ new_spell=~%#5%~ END
LAF d2_mc_import_fix INT_VAR amount=6  STR_VAR spell=~%s1%~ new_spell=~%#6%~ END
LAF d2_mc_import_fix INT_VAR amount=7  STR_VAR spell=~%s1%~ new_spell=~%#7%~ END
LAF d2_mc_import_fix INT_VAR amount=8  STR_VAR spell=~%s1%~ new_spell=~%#8%~ END
LAF d2_mc_import_fix INT_VAR amount=9  STR_VAR spell=~%s1%~ new_spell=~%#9%~ END
LAF d2_mc_import_fix INT_VAR amount=10 STR_VAR spell=~%s1%~ new_spell=~%#a%~ END
LAF d2_mc_import_fix INT_VAR amount=11 STR_VAR spell=~%s1%~ new_spell=~%#b%~ END


//Array for import fix
ACTION_CLEAR_ARRAY ~import_fix_array~
ACTION_DEFINE_ASSOCIATIVE_ARRAY import_fix_array BEGIN
  ~%#1%~  => 7     // moonlight vortex
  ~%#2%~  => 11
  ~%#3%~  => 15
  ~%#4%~  => 19
  ~%#5%~  => 23
  ~%#6%~  => 27
  ~%#7%~  => 31
  ~%#8%~  => 35
  ~%#9%~  => 39
  ~%#a%~  => 43
  ~%#b%~  => 47
END


//Ability tables
COPY ~inlined/d2clab.2DA~  ~override/%moonlightknight%C.2DA~
  PATCH_IF (holysymbols > 0) BEGIN
    LPF d2_kit_ability_adder INT_VAR level=17 passive=1 STR_VAR resource=~%m9%~ END    // holy symbol (level 17)
  END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e9%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e6%~ END

COPY ~inlined/d2clab.2DA~  ~override/%moonlightknight%R.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=8 passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=16 passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=3 passive=1 STR_VAR resource=~%e3%~ END
  LPF d2_kit_ability_adder INT_VAR level=8 passive=1 STR_VAR resource=~%e4%~ END
  LPF d2_kit_ability_adder INT_VAR level=16 passive=1 STR_VAR resource=~%e5%~ END
  LPF d2_kit_ability_adder INT_VAR level=7 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e7%~ END

  //import fix (BG2, ToB, SoD)
  PHP_EACH import_fix_array AS file => level BEGIN
    LPF d2_kit_ability_adder INT_VAR level=level passive=1 STR_VAR resource=~%file%~ END
  END


//Set variable for proficiencies
ACTION_IF ((vanillaclericprofs = 0) OR
           (FILE_EXISTS_IN_GAME ~RASPHERE.marker~) OR
           (FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~) OR
           (MOD_IS_INSTALLED ~Faiths_and_Powers.tp2~ (ID_OF_LABEL ~Faiths_and_Powers.tp2~ ~D5_FNP_WEAPON_USABILITY~))
) BEGIN
  OUTER_TEXT_SPRINT multikitprofs  ~2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 3~
END ELSE BEGIN
  OUTER_TEXT_SPRINT multikitprofs  ~0 0 0 0 2 2 0 2 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 0 0 0 2 2 2 2 3~
END

/*ranged = 1 slot
  OUTER_TEXT_SPRINT multikitprofs  ~2 2 1 2 2 2 2 1 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 1 1 1 1 1 2 2 2 3~
END ELSE BEGIN
  OUTER_TEXT_SPRINT multikitprofs  ~0 0 0 0 2 2 0 1 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 0 0 0 1 2 2 2 3~
*/


//
LAF d2_get_mc_text STR_VAR class=ranger multi=cr RET baseclass multiclass END
OUTER_SET kit_note = RESOLVE_STR_REF(@353)
OUTER_SET base_kit = RESOLVE_STR_REF(@100403)
LAF d2_build_mc_desc INT_VAR desc1=base_kit desc2=kit_note mode=2 STR_VAR multi=cr RET kit_desc END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 18
    mixed = RESOLVE_STR_REF (@100401)
    lower = RESOLVE_STR_REF (@100402)
    help = kit_desc
  STR_VAR
    kit_name = ~D2%kitname%CR~
    clab_base_p = ~%moonlightknight%C.2DA~    // holy symbol added to Cleric table
    clab_base_r = ~%moonlightknight%R.2DA~    // kit abilities added to Ranger table
    clsrcreq = ~1 1 1 1 1 1 1~                // Usable by all races, if base class is usable
    alignmnt = ~1 1 1 1 1 1 1 1 1~
    weapprof = ~%multikitprofs%~
//    clasiskl = ~0 0 0 0 0 0 0~              // initial skills
//    thiefscl = ~0 0 0 50 50 0 0 0~          // skills percent modifier (using below method)
    clascolr = ~137 141 142 24 138~           // shaman colors
  RET 
    kit_id
END


//edit stealth skill
COPY_EXISTING ~thiefscl.2da~  ~override~
  LPF d2_edit_2da_entry STR_VAR col_name=~D2%kitname%CR~ row_name=MOVE_SILENTLY entry=50 END
  LPF d2_edit_2da_entry STR_VAR col_name=~D2%kitname%CR~ row_name=HIDE_IN_SHADOWS entry=50 END
BUT_ONLY


//DoF spheres
OUTER_TEXT_SPRINT dofclab     ~%kitid%C~
OUTER_TEXT_SPRINT dofkitname  ~D2%kitname%CR~
INCLUDE ~%install%/dofspheres.tph~

//ToF spheres
OUTER_TEXT_SPRINT tofkitname  ~D2%kitname%CR~
OUTER_TEXT_SPRINT tofspheres  ~all,animal,combat,divine,earth,evil,fey,good,healing,moon,necromantic,plant,protection~
INCLUDE ~%install%/tofspheres.tph~


END


/*
*/