//Moonlight Knight (c/r)


ACTION_IF (nomoonlightknightcr = 0) BEGIN


INCLUDE ~%MOD_FOLDER%/lib/a7#add_kit_ex.tpa~    // weidu error if this file isn't included here


//Prevent from getting the base cleric Holy Symbols
COPY ~%passives%/opcode206.SPL~  ~override/%e9%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~cdhlysym~ END


//Add Holy Symbol (only given in BG2EE)
COPY ~%files%/holysymbol_core.SPL~    ~override/%m1%.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=122 STR_VAR resource=~%m1%~ END
COPY ~%icons%/holysymbol_moon.BAM~    ~override/%m1%.BAM~

ACTION_IF (FILE_EXISTS_IN_GAME ~belt12.itm~) BEGIN
  COPY_EXISTING ~belt12.itm~          ~override/%m1%.ITM~    // clone Lathander symbol
    WRITE_LONG 0x1e (THIS BAND BNOT BIT1)                    // remove restrictions
    WRITE_BYTE 0x29 (THIS BAND BNOT BIT6)
END
ELSE BEGIN
  COPY ~%files%/holysymbol_core.ITM~  ~override/%m1%.ITM~    // or add new
END

COPY_EXISTING ~%m1%.ITM~  ~override~
  SAY NAME2 @70123
  SAY DESC @70223
  WRITE_ASCII 0x3a ~%m1%~ #8       // icon
  WRITE_ASCII 0x58 ~camul01~ #8    // description image
BUT_ONLY


//Ability tables (don't include base class abilities)
COPY ~%files%/kit6.2DA~  ~override/%moonlightknight%C.2DA~
  PATCH_IF (GAME_INCLUDES ~bg2~) BEGIN
    LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e9%~ END
    LPF d2_kit_ability_adder INT_VAR level=17 passive=1 STR_VAR resource=~%m1%~ END
  END

COPY ~%files%/kit0.2DA~  ~override/%moonlightknight%R.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 passive=1 STR_VAR resource=~%e3%~ END
  LPF d2_kit_ability_adder INT_VAR level=6 multi=1 increment=4 limit=14 STR_VAR resource=~%s1%~ END


//Set variable for proficiencies
ACTION_IF ((vanillaclericprofs = 0) OR
           (FILE_EXISTS_IN_GAME ~RASPHERE.marker~) OR
           (FILE_EXISTS_IN_GAME ~d5__fnp_usability.d5~) OR
           (MOD_IS_INSTALLED ~Faiths_and_Powers.tp2~ (ID_OF_LABEL ~Faiths_and_Powers.tp2~ ~D5_FNP_WEAPON_USABILITY~))
          ) BEGIN

  OUTER_TEXT_SPRINT multikitprofs  ~2 2 1 2 2 2 2 1 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 1 1 1 1 1 2 2 2 3~
END
ELSE BEGIN
  OUTER_TEXT_SPRINT multikitprofs  ~0 0 0 0 2 2 0 1 0 0 0 0 0 0 0 0 2 2 0 0 2 2 2 0 0 0 0 1 2 2 2 3~
END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 18
    mixed = RESOLVE_STR_REF (@100401)
    lower = RESOLVE_STR_REF (@100402)
    help = RESOLVE_STR_REF (@100403)
  STR_VAR
    kit_name = ~D2%kitname%CR~
    clab_base_p = ~%moonlightknight%C.2DA~    // no abilities added to Cleric table
    clab_base_r = ~%moonlightknight%R.2DA~    // kit abilities added to Ranger table
    clsrcreq = ~1 1 1 1 1 1 1~                // Usable by all races, if base class is usable
    alignmnt = ~1 1 1 1 1 1 1 1 1~
    weapprof = ~%multikitprofs%~
    clasiskl = ~0 0 0 0 0 0 0~
    thiefscl = ~0 0 0 0 0 0 0 0~
    clascolr = ~93 72 169 93 72~              // Undead Hunter colors
  RET 
    kit_id
END


//FnP spheres
COPY ~%passives%/d5fpclsp.spl~  ~override/%e9%c.SPL~
  LPF ALTER_EFFECT STR_VAR match_resource=~d5smwar#~ resource=~d5smmag#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5spben#~ resource=~d5spear#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5splif#~ resource=~d5smlif#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5spkno#~ resource=~d5spexp#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5spfir#~ resource=~d5smast#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5smtho#~ resource=~d5smdec#~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~d5smdea#~ resource=~d5smben#~ END
  LPF ALTER_EFFECT INT_VAR parameter1=IDS_OF_SYMBOL (kit ~D2%kitname%CR~) END
BUT_ONLY

COPY ~%passives%/d5fprnsp.spl~  ~override/%e9%r.SPL~
  LPF ALTER_EFFECT STR_VAR match_resource=~d5sppla#~ resource=~d5smpla#~ END
  LPF ALTER_EFFECT INT_VAR parameter1=IDS_OF_SYMBOL (kit ~D2%kitname%CR~) END
BUT_ONLY

COPY_EXISTING ~CLABPR01.2DA~  ~override~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e9%c~ END
BUT_ONLY
COPY_EXISTING ~CLABRN01.2DA~  ~override~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e9%r~ END
BUT_ONLY


//DoF variables
OUTER_TEXT_SPRINT kitid     ~%kitid%C~
OUTER_TEXT_SPRINT kitname   ~%kitname%CR~


END


/*
*/