//War Magician (d2wk7)


//Add passive effects
COPY ~%passives%/resistup5elements.SPL~  ~override/%e1%.SPL~    // 5% resists
COPY ~%passives%/resistup5elements.SPL~  ~override/%e2%.SPL~    // 10% resists
  LPF ALTER_EFFECT INT_VAR parameter1=10 END
COPY ~%passives%/fewerspells1.SPL~  ~override/%e3%.SPL~         // remove specialist bonus spells
COPY ~%passives%/opcode206.SPL~     ~override/%e4%.SPL~         // only relevant for 5e casting mod
  LPF ALTER_EFFECT STR_VAR resource=~d5z0slt~ END
  LPF CLONE_EFFECT STR_VAR resource=~D5ZMSLT~ END


//Add Unleash Magic
COPY ~%icons%/unleashmagic.BAM~      ~override/%s1%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@109) STR_VAR bam=~%s1%i~ RET icon END

COPY ~%abilities%/unleashwhorl.VVC~  ~override/%s1%.VVC~
  WRITE_ASCII 0x8 ~%s1%~ #8
COPY ~%abilities%/unleashwhorl.BAM~  ~override/%s1%.BAM~

COPY ~%abilities%/unleashmagic.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @109
  SAY UNIDENTIFIED_DESC @209
  LPF ALTER_EFFECT INT_VAR match_opcode=321 opcode=326 STR_VAR resource=~%s0%x~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=332 duration=26 END
  LPF ALTER_EFFECT INT_VAR match_opcode=215 STR_VAR resource=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF CLONE_EFFECT INT_VAR match_opcode=142 opcode=191 parameter1=2 parameter2=0 END    // v4.6: +2 caster level
  LPF ALTER_EFFECT INT_VAR resist_dispel=2 END    // not dispellable


//Add Twisted Barricade
COPY ~%icons%/twistedbarricade.BAM~      ~override/%s2%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@110) STR_VAR bam=~%s2%i~ RET icon END

COPY ~%abilities%/twistedbarricade.VVC~  ~override/%s2%.VVC~
  WRITE_ASCII 0x80 ~EFF_P105~ #8

COPY ~%abilities%/twistedbarricade.SPL~  ~override/%s2%.SPL~
  SAY NAME1 @110
  SAY UNIDENTIFIED_DESC @210
  LPF ALTER_EFFECT INT_VAR match_opcode=321 opcode=326 STR_VAR resource=~%s0%x~ END
//  LPF ALTER_EFFECT INT_VAR match_opcode=176 parameter2=5 END                          // movement (set mode 5)
  LPF ALTER_EFFECT INT_VAR match_opcode=215 STR_VAR resource=~%s2%~ END               // visual
  LPF DELETE_EFFECT INT_VAR match_opcode=272 END                                      // delete old visual
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s2%b~ insert=below END    // block normal missiles
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END
  LPF ALTER_EFFECT INT_VAR resist_dispel=2 END                                        // not dispellable

  // spell deflection (level 10+)
  LPF CLONE_EFFECT INT_VAR header=2 multi_match=1 STR_VAR resource=~%s2%c~ insert=below END
  LPF CLONE_EFFECT INT_VAR header=3 multi_match=1 STR_VAR resource=~%s2%c~ insert=below END
  LPF CLONE_EFFECT INT_VAR header=4 multi_match=1 STR_VAR resource=~%s2%c~ insert=below END


//subspell - protection from normal missiles
COPY_EXISTING ~SPWI311.SPL~  ~override/%s2%b.SPL~
  LPF d2_remove_level_headers END    // for IWDEE
  LPF d2_convert_spell_type END
  LPF d2_subspell_remove_effects END
  LPF d2_subspell_remove_visuals INT_VAR icon=0 END
  LPF DELETE_EFFECT INT_VAR match_opcode=156 END             // remove missile overlay
  LPF ALTER_EFFECT INT_VAR match_timing=0 duration=24 END    // 4 rounds
  LPF ALTER_EFFECT INT_VAR resist_dispel=2 END               // not dispellable


//subspell - spell deflection
COPY_EXISTING ~SPPR701.SPL~  ~override/%s2%c.SPL~
  LPF d2_remove_level_headers END
  LPF d2_convert_spell_type END
  LPF d2_subspell_remove_effects END
  LPF d2_subspell_remove_visuals END
  LPF DELETE_EFFECT INT_VAR match_opcode=233 END             // remove prof change
  LPF ALTER_EFFECT INT_VAR match_timing=0 duration=24 END    // 4 rounds
  LPF ALTER_EFFECT INT_VAR resist_dispel=2 END               // not dispellable

  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=201 parameter1=1000 STR_VAR resource=None END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=201 opcode=291 duration=25 parameter2=1 END

/*Alt version that protects from area attacks
  // change spell deflection from op201 to op102
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=201 opcode=102 parameter1=1 parameter2=0 END
  FOR (n = 2; n < 10; ++n) BEGIN
    LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=102 parameter1=n END
  END
  LPF DELETE_EFFECT INT_VAR match_opcode=201 END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_duration=24 opcode=142 parameter2=67 END    // icon
*/


//Remove effects
COPY ~%abilities%/removespelleffects.SPL~  ~override/%s0%x.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s1%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s2%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s2%b~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 STR_VAR resource=~%s2%c~ END


//Make clab table
COPY ~inlined/d2clab_mage~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR level=5 passive=1 multi=1 increment=5 limit=20 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=4 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e3%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e4%~ END
  LPF d2_kit_ability_adder END

  // if installed after 5e casting
  REPLACE_TEXTUALLY ~AP_D5ZMSLT~  ~****~


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 1
    mixed = RESOLVE_STR_REF (@100701)
    lower = RESOLVE_STR_REF (@100702)
    help = RESOLVE_STR_REF (@100703)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~    // Usable by all races, if base class is usable
    abclsmod = ~-2 0 -2 0 0 0~    // -2 to Str and Con
    clascolr = ~18 5 24 27 30~    // Helm colors
  RET 
    kit_id
END


//Set variables for external use
OUTER_TEXT_SPRINT warmagician       ~%kitid%~
OUTER_TEXT_SPRINT unleashmagic      ~%s1%~
OUTER_TEXT_SPRINT twistedbarricade  ~%s2%~


/*
*/