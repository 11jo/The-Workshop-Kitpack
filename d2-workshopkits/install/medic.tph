//Medic (d2wkd)


/*
//no gauntlets
COPY ~%passives%/noitemtype.SPL~  ~override/%e1%.SPL~
  LPF ALTER_EFFECT INT_VAR opcode=181 special=RESOLVE_STR_REF (@300) END    // "Cannot use item."
*/


//medic spells
ACTION_DEFINE_ASSOCIATIVE_ARRAY medicspells BEGIN
  SPPR212 => ~%s4%~    // slow poison
  SPPR317 => ~%s5%~    // cure disease
  sppr404 => ~%s6%~    // neutralize poison
  sppr607 => ~%s7%~    // heal
  sppr417 => ~%s8%~    // lesser restoration
END

ACTION_PHP_EACH medicspells AS spell => res BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%spell%.SPL~) BEGIN
    COPY_EXISTING ~%spell%.SPL~  ~override/%res%.SPL~
      LPF d2_convert_spell_type INT_VAR type=4 level=1 END
      WRITE_LONG 0x18 THIS | BIT14
      WRITE_LONG 0x18 THIS | BIT25                  // no vocal component
      WRITE_SHORT 0x22 0                            // no cast visuals
      LPF ALTER_SPELL_HEADER INT_VAR speed=0 END    // casting speed
      LPF ALTER_EFFECT INT_VAR power=0 END
      LPF ALTER_EFFECT INT_VAR silent=1 STR_VAR match_resource=~%SOURCE_RES%~ resource=~%DEST_RES%~ END
      LPF DELETE_EFFECT INT_VAR match_opcode=93 END              // remove fatigue
      LPF DELETE_EFFECT STR_VAR match_resource=~dvwinded~ END    // remove SR fatigue
  END
END


//replace slow poison
COPY ~%passives%/removespell.SPL~    ~override/%e4%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%s4%~ END

//replace cure disease
COPY ~%passives%/removespell.SPL~    ~override/%e5%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%s5%~ END


//
//Add Remedy projectiles
COPY - ~%abilities%/remedy7.PRO~ ~%workspace%/%s9%a.PRO~     // 7-ft. radius
ADD_PROJECTILE ~%workspace%/%s9%a.PRO~
COPY - ~%abilities%/remedy4.PRO~ ~%workspace%/%s9%b.PRO~     // 4-ft. radius
ADD_PROJECTILE ~%workspace%/%s9%b.PRO~
COPY - ~%abilities%/remedy15.PRO~ ~%workspace%/%s9%c.PRO~    // 15-ft. radius
ADD_PROJECTILE ~%workspace%/%s9%c.PRO~

OUTER_TEXT_SPRINT remedy7      ~%s9%a~
OUTER_TEXT_SPRINT remedy4      ~%s9%b~
OUTER_TEXT_SPRINT remedy15     ~%s9%c~


//
//Add Black Remedy
COPY ~%icons%/blackremedyicon.BAM~  ~override/%s1%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@122) STR_VAR bam=~%s1%i~ RET icon END

COPY ~%icons%/blackremedy.BAM~      ~override/%s1%.BAM~
COPY ~%abilities%/remedy.SPL~       ~override/%s1%.SPL~
  SAY NAME1 @122
  SAY UNIDENTIFIED_DESC @222
  WRITE_ASCII 0x3a ~%s1%~ #8
  WRITE_ASCII 0x76 ~%s1%~ #8
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%x~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=141 parameter2=20 END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/hiteffectall.SPL~     ~override/%s1%a.SPL~
  LPF ALTER_EFFECT INT_VAR duration=10 STR_VAR resource=~%s1%b~ END

COPY ~%abilities%/castspelltarget.EFF~  ~override/%s1%b.EFF~
  WRITE_ASCII 0x30 ~%s1%b~ #8
COPY ~%abilities%/remedyblack.SPL~      ~override/%s1%b.SPL~
  LPF ALTER_SPELL_HEADER INT_VAR projectile=~%remedy7%~ END    // set projectile
  LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%s1%b~ END


//
//Add White Remedy
ACTION_IF (GAME_IS ~bgee~)      BEGIN OUTER_SET sleep_string = 26371 END
ACTION_IF (GAME_IS ~bg2ee eet~) BEGIN OUTER_SET sleep_string = 14001 END
ACTION_IF (GAME_IS ~iwdee~)     BEGIN OUTER_SET sleep_string = 37613 END

COPY ~%icons%/whiteremedyicon.BAM~  ~override/%s2%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@123) STR_VAR bam=~%s2%i~ RET icon END
COPY ~%abilities%/remedy.SPL~       ~override/%s2%.SPL~
  SAY NAME1 @123
  SAY UNIDENTIFIED_DESC @223
  WRITE_ASCII 0x3a ~SPWI903B~ #8
  WRITE_ASCII 0x76 ~SPWI903B~ #8
  LPF ALTER_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s2%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=326 STR_VAR resource=~%s0%x~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=141 parameter2=30 END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/hiteffectall.SPL~     ~override/%s2%a.SPL~
  LPF ALTER_EFFECT INT_VAR duration=10 STR_VAR resource=~%s2%b~ END

COPY ~%abilities%/castspelltarget.EFF~  ~override/%s2%b.EFF~
  WRITE_ASCII 0x30 ~%s2%b~ #8
COPY ~%abilities%/remedywhite.SPL~      ~override/%s2%b.SPL~
  LPF ALTER_SPELL_HEADER INT_VAR projectile=~%remedy7%~ END    // set projectile
  LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%s2%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=~%sleep_string%~ END    // "Sleep"


//
//Remedy lighting (write into projectile)
COPY ~%abilities%/remedyvisual.VVC~  ~override/%s9%e.VVC~
ACTION_FOR_EACH projectile IN ~%remedy7%~ ~%remedy4%~ ~%remedy15%~ BEGIN
  COPY ~override/%projectile%.PRO~   ~override~
    WRITE_ASCII 0x21c  ~%s9%e~ #8
  BUT_ONLY
END

//
//Remove effects
COPY ~%abilities%/removespelleffects.SPL~  ~override/%s0%x.SPL~
  LPF ALTER_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s2%~ END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=321 STR_VAR resource=~%s2%a~ END


//
//Add Healing Rain
COPY - ~%abilities%/healingrain.PRO~ ~%workspace%/%s3%a.PRO~
ADD_PROJECTILE ~%workspace%/%s3%a.PRO~

COPY ~%icons%/healingrain.BAM~      ~override/%s3%.BAM~
COPY ~%abilities%/healingrain.SPL~  ~override/%s3%.SPL~
  SAY NAME1 @124
  SAY UNIDENTIFIED_DESC @224
  WRITE_ASCII 0x3a ~%s3%~ #8
  WRITE_ASCII 0x76 ~%s3%~ #8
  LPF ALTER_SPELL_HEADER INT_VAR projectile=~%s3%a~ END    // set projectile


//
//Make clab table
COPY ~inlined/d2clab_monk_nohealstun~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR multi=1 increment=5 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR multi=1 increment=5 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder INT_VAR level=9 multi=1 increment=3 limit=15 STR_VAR resource=~%s3%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 STR_VAR resource=~%s4%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 STR_VAR resource=~%s4%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 STR_VAR resource=~%s4%~ END
  LPF d2_kit_ability_adder INT_VAR level=7 STR_VAR resource=~%s5%~ END
  LPF d2_kit_ability_adder INT_VAR level=10 STR_VAR resource=~%s6%~ END
  LPF d2_kit_ability_adder INT_VAR level=10 STR_VAR resource=~%s6%~ END
  LPF d2_kit_ability_adder INT_VAR level=10 STR_VAR resource=~%s6%~ END
  LPF d2_kit_ability_adder INT_VAR level=14 STR_VAR resource=~%s7%~ END
  LPF d2_kit_ability_adder INT_VAR level=18 STR_VAR resource=~%s8%~ END
  LPF d2_kit_ability_adder INT_VAR level=18 STR_VAR resource=~%s8%~ END
  LPF d2_kit_ability_adder INT_VAR level=18 STR_VAR resource=~%s8%~ END
  LPF d2_kit_ability_adder INT_VAR level=10 passive=1 STR_VAR resource=~%e4%~ END    // remove slow poison
  LPF d2_kit_ability_adder INT_VAR level=14 passive=1 STR_VAR resource=~%e5%~ END    // remove cure disease
  LPF d2_kit_ability_adder END


//
//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 20
    mixed = RESOLVE_STR_REF (@101301)
    lower = RESOLVE_STR_REF (@101302)
    help = RESOLVE_STR_REF (@101303)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~      // Usable by all races, if base class is usable
    clascolr = ~115 24 25 24 72~    // enchanter colors
  RET 
    kit_id
END


//
//array for weapon restrictions
ACTION_DEFINE_ASSOCIATIVE_ARRAY medicweapontype BEGIN    // weapon => usable
  16 => 1    // daggers
  17 => 1    // maces, clubs
  18 => 1    // slings
  19 => 1    // small swords (short sword, wakizashi)
  20 => 0    // large swords (long sword, bastard sword, two handed sword, ninjato)
  24 => 1    // darts
  26 => 1    // quarterstaff (used for club by FnP)
END


//
//variables
OUTER_TEXT_SPRINT medic        ~%kitid%~
OUTER_TEXT_SPRINT blackremedy  ~%s1%~
OUTER_TEXT_SPRINT whiteremedy  ~%s2%~
OUTER_TEXT_SPRINT healingrain  ~%s3%~
OUTER_TEXT_SPRINT mediccure1   ~%s4%~
OUTER_TEXT_SPRINT mediccure2   ~%s5%~
OUTER_TEXT_SPRINT mediccure3   ~%s6%~
OUTER_TEXT_SPRINT mediccure4   ~%s7%~
OUTER_TEXT_SPRINT mediccure5   ~%s8%~


/*
*/