//Starjammer (d2wk8)


//Add passive effects
COPY ~%passives%/starjammer.SPL~   ~override/%e1%.SPL~
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_silence_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_deafness_arrays END

  PATCH_IF (GAME_IS ~bgee iwdee~) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=267 END
    LPF d2_string_immunities END
  END

  PATCH_IF (silence_immunity > 0) BEGIN 
    LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=38 opcode=328 parameter2=silence_immunity special=1 END
  END

COPY ~%passives%/fewerspells1.SPL~  ~override/%e2%.SPL~    // remove specialist bonus spells


//
//spell icons
COPY ~%icons%/riftstepicon1.BAM~       ~override/%w1%a.BAM~
COPY ~%icons%/riftstepicon2.BAM~       ~override/%w1%b.BAM~
COPY ~%icons%/steadycurrenticon1.BAM~  ~override/%w2%a.BAM~
COPY ~%icons%/steadycurrenticon2.BAM~  ~override/%w2%b.BAM~
COPY ~%icons%/swappericon1.BAM~        ~override/%w3%a.BAM~
COPY ~%icons%/swappericon2.BAM~        ~override/%w3%b.BAM~
COPY ~%icons%/pacifiericon1.BAM~       ~override/%w4%a.BAM~
COPY ~%icons%/pacifiericon2.BAM~       ~override/%w4%b.BAM~
COPY ~%icons%/jolticon1.BAM~           ~override/%w5%a.BAM~
COPY ~%icons%/jolticon2.BAM~           ~override/%w5%b.BAM~
COPY ~%icons%/electrocannonicon1.BAM~  ~override/%w6%a.BAM~
COPY ~%icons%/electrocannonicon2.BAM~  ~override/%w6%b.BAM~
COPY ~%icons%/revitalizericon1.BAM~    ~override/%w7%a.BAM~
COPY ~%icons%/revitalizericon2.BAM~    ~override/%w7%b.BAM~
COPY ~%icons%/riftgateicon1.BAM~       ~override/%w8%a.BAM~
COPY ~%icons%/riftgateicon2.BAM~       ~override/%w8%b.BAM~


//
//Add Riftstep
COPY - ~%abilities%/riftstepgibber.BAF~  ~%workspace%/%w1%g.BAF~    // add gibberlings
COMPILE ~%workspace%/%w1%g.BAF~
COPY ~%abilities%/riftstepgibber.ITM~    ~override/%w1%g.ITM~
COPY ~%abilities%/riftstepgibber.CRE~    ~override/%w1%g.CRE~
  SAY NAME1 @600
  SAY NAME2 @600
  REPLACE_CRE_ITEM ~%w1%g~ #0 #0 #0 ~none~ ~weapon1~ EQUIP
  WRITE_ASCII 0x250 ~%w1%g~ #8     // class script
  WRITE_ASCII 0x280 ~%w1%g~ #20    // script name
COPY ~%abilities%/summonportal.EFF~   ~override/%w1%g.EFF~
  WRITE_ASCII 0x30 ~%w1%g~ #8
  WRITE_LONG 0x20 2

COPY ~%abilities%/d2ddoor.VVC~           ~override~                 // add spell
COPY ~%abilities%/riftsteptimestop.SPL~  ~override/%w1%a.SPL~
COPY ~%abilities%/riftstep.SPL~          ~override/%w1%.SPL~
  SAY NAME1 @40108
  SAY UNIDENTIFIED_DESC @40208
  LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%w1%a~ END       // time stop
  LPF ALTER_EFFECT INT_VAR match_opcode=215 STR_VAR resource=~d2ddoor~ END     // visual
  LPF ALTER_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~%w1%g~ END       // gibberlings
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 END
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=177 END
  LPF ALTER_EFFECT INT_VAR match_opcode=265 STR_VAR resource=~d2riftst~ END    // variable for script action


//
//Add Steady Current
COPY - ~%abilities%/steadycurrent.PRO~  ~%workspace%/%w2%.PRO~
  WRITE_ASCII 0x104 ~%w2%~ #8    // BAM animation
ADD_PROJECTILE ~%workspace%/%w2%.PRO~

COPY ~%abilities%/steadycurrent.BAM~    ~override/%w2%.BAM~     // animation BAM
COPY ~%abilities%/steadycurrent.SPL~    ~override/%w2%.SPL~
  SAY NAME1 @40109
  SAY UNIDENTIFIED_DESC @40209
  WRITE_SHORT 0x98 ~%w2%~        // projectile

//function for Evasion (used later)
DEFINE_ACTION_FUNCTION steadycurrentevasion BEGIN
  ACTION_IF (FILE_EXISTS ~override/%steadycurrent%.SPL~) BEGIN
    COPY_EXISTING ~%steadycurrent%.SPL~  ~override~
      LPF d2_evasion_check END
      WRITE_LONG 0xbe THIS | BIT10     // secondary targets only
    BUT_ONLY
  END
END


//
//Add Gemini Swap
COPY ~%abilities%/swapper.SPL~  ~override/%w3%.SPL~
  SAY NAME1 @40110
  SAY UNIDENTIFIED_DESC @40210
  LPF ALTER_EFFECT INT_VAR match_opcode=215 STR_VAR resource=~d2ddoor~ END


//
//Add Pacifying Field
COPY - ~%abilities%/pacifier.PRO~  ~%workspace%/%w4%.PRO~
ADD_PROJECTILE ~%workspace%/%w4%.PRO~
COPY_EXISTING ~SPPR109.SPL~  ~override/%w4%.SPL~    // clone Sanctuary
  SAY NAME1 @40111
  SAY UNIDENTIFIED_DESC @40211
  
  LPF d2_remove_level_headers END
  LPF d2_convert_spell_type INT_VAR type=1 level=4 END    // convert to level 4 Wizard spell
  LPF ALTER_EFFECT INT_VAR target=2 power=0 END           // power=0 to bypass immunity to spell level
  LPF ALTER_EFFECT INT_VAR match_timing=0 duration=60 END
  WRITE_SHORT 0x98 ~%w4%~         // projectile

  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    LPF ALTER_EFFECT INT_VAR match_timing=4 duration=60 END
  END

  PATCH_IF (MOD_IS_INSTALLED ~setup-spell_rev.tp2~ (ID_OF_LABEL ~setup-spell_rev.tp2~ ~dv-spell_rev-main~)) BEGIN
    LPF ALTER_EFFECT INT_VAR match_timing=4 duration=60 END
  END


//
//Add Jolt

/*Raise Dead info
EEs have the effects on a subspell.
Spell revisions puts everything on the main spell.
*/

COPY_EXISTING ~SPPR504.SPL~   ~override/%w5%.SPL~         // clone Raise Dead
  SAY NAME1 @40112
  SAY UNIDENTIFIED_DESC @40212

  PATCH_IF (GAME_IS ~iwdee~) BEGIN
    LPF DELETE_EFFECT INT_VAR match_parameter2=15 END     // remove Elf immunity (IWDEE)
  END

  LPF d2_convert_spell_type INT_VAR type=1 level=5 END    // convert to level 5 Wizard spell
  LPF ALTER_EFFECT INT_VAR power=0 END                    // power=0 to bypass immunity to spell level
  WRITE_BYTE  0x25 8     // Alteration school
  WRITE_SHORT 0x22 15    // casting animation
  WRITE_SHORT 0x84 5     // casting speed
  WRITE_ASCII 0x10 CAS_M04 #8     // casting sound

  //EE edits  
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=326 STR_VAR resource=~%w5%a~ END
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=326 parameter2=113 STR_VAR resource=~%w5%b~ END
  LPF DELETE_EFFECT INT_VAR match_opcode=324 END

  //Spell Revisions edits
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=32 opcode=326 parameter1=3 parameter2=113 STR_VAR resource=~%w5%b~ END
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=32 opcode=318 parameter1=3 parameter2=113 timing=0 STR_VAR resource=~%w5%~ END
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=215 STR_VAR resource=~BDFAVSPI~ END
  LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=215 opcode=174 STR_VAR resource=~EFF_E06~ END

//
//Jolt subspells
ACTION_IF (FILE_EXISTS_IN_GAME ~SPPR504A.SPL~) BEGIN
  COPY_EXISTING ~SPPR504A.SPL~  ~override/%w5%a.SPL~        // clone EE subspell
    PATCH_IF (GAME_IS ~iwdee~) BEGIN
      LPF DELETE_EFFECT INT_VAR match_opcode=174 END        // remove IWDEE sound
    END

    LPF d2_convert_spell_type INT_VAR type=1 level=5 END    // convert to level 5 Wizard spell
    LPF ALTER_EFFECT INT_VAR power=0 END                    // power=0 to bypass immunity to spell level
    LPF ALTER_EFFECT INT_VAR match_opcode=215 STR_VAR resource=~BDFAVSPI~ END
    LPF CLONE_EFFECT INT_VAR match_opcode=215 opcode=174 STR_VAR resource=~EFF_E06~ END
    WRITE_BYTE  0x25 8    // Alteration school
END

ACTION_IF (FILE_EXISTS_IN_GAME ~sppr308.SPL~) BEGIN         // clone Remove Paralysis
  COPY_EXISTING  ~sppr308.SPL~  ~override/%w5%b.SPL~
    LPF d2_convert_spell_type INT_VAR type=1 level=5 END    // convert to level 5 Wizard spell
    LPF ALTER_EFFECT INT_VAR power=0 END                    // power=0 to bypass immunity to spell level
    WRITE_LONG 0x8 ~-1~
    WRITE_BYTE  0x25 8          // Alteration school
    WRITE_SHORT 0x22 0          // casting animation
    WRITE_ASCII 0x10 None #8    // casting sound

//visual
    LPF ALTER_SPELL_HEADER INT_VAR target=1 range=1000 speed=0 projectile=241 END
    LPF DELETE_EFFECT INT_VAR match_opcode=61 END
    LPF DELETE_EFFECT INT_VAR match_opcode=50 END
    LPF DELETE_EFFECT INT_VAR match_opcode=174 END
    LPF ALTER_EFFECT INT_VAR match_opcode=215 STR_VAR resource=~BDFAVSPI~ END
    LPF CLONE_EFFECT INT_VAR match_opcode=215 opcode=174 STR_VAR resource=~EFF_E06~ END

//spell effects
    LPF CLONE_EFFECT INT_VAR multi_match=1 timing=1 opcode=26 END     // break curse
    LPF CLONE_EFFECT INT_VAR multi_match=1 timing=1 opcode=2 END      // cure sleep
    LPF CLONE_EFFECT INT_VAR multi_match=1 timing=1 opcode=77 END     // cure feeblemind
    LPF CLONE_EFFECT INT_VAR multi_match=1 timing=1 opcode=164 END    // cure intoxication
    LPF CLONE_EFFECT INT_VAR match_opcode=2  opcode=240 parameter2=14 END
    LPF CLONE_EFFECT INT_VAR match_opcode=77 opcode=240 parameter2=48 END
END


//
//Add Electrocannon
COPY - ~%abilities%/electrocannon5.PRO~  ~%workspace%/%w6%.PRO~
  WRITE_ASCII 0x104 ~%w6%~ #8
ADD_PROJECTILE ~%workspace%/%w6%.PRO~
COPY ~%abilities%/electrocannon.BAM~     ~override/%w6%.BAM~

COPY ~%abilities%/electrocannon.SPL~     ~override/%w6%.SPL~
  SAY NAME1 @40113
  SAY UNIDENTIFIED_DESC @40213
  WRITE_SHORT 0x98 ~%w6%~    // projectile

  LPF CD_EXTEND-O-MATIC
    INT_VAR
      level_cap   = %spellcap%
      min_lev_alt = 12
  END

  LPF ALTER_EFFECT INT_VAR header=1 parameter1=26 dicenumber=13 END    // level 13
  LPF ALTER_EFFECT INT_VAR header=2 parameter1=28 dicenumber=14 END
  LPF ALTER_EFFECT INT_VAR header=3 parameter1=30 dicenumber=15 END
  LPF ALTER_EFFECT INT_VAR header=4 parameter1=32 dicenumber=16 END
  LPF ALTER_EFFECT INT_VAR header=5 parameter1=34 dicenumber=17 END
  LPF ALTER_EFFECT INT_VAR header=6 parameter1=36 dicenumber=18 END
  LPF ALTER_EFFECT INT_VAR header=7 parameter1=38 dicenumber=19 END
  LPF ALTER_EFFECT INT_VAR header=8 parameter1=40 dicenumber=20 END

  PATCH_IF (spellcap >= 30) BEGIN
    FOR (n = 1; n <= 10; ++n) BEGIN
      SET header = 8 + n
      SET damage = 40 + (n*2)
      SET dice   = 20 + n
      LPF ALTER_EFFECT INT_VAR header=header parameter1=damage dicenumber=dice END
    END
  END

  PATCH_IF (spellcap >= 40) BEGIN
    FOR (n = 11; n <= 20; ++n) BEGIN
      SET header = 8 + n
      SET damage = 40 + (n*2)
      SET dice   = 20 + n
      LPF ALTER_EFFECT INT_VAR header=header parameter1=damage dicenumber=dice END
    END
  END

  PATCH_IF (spellcap = 50) BEGIN
    FOR (n = 21; n <= 30; ++n) BEGIN
      SET header = 8 + n
      SET damage = 40 + (n*2)
      SET dice   = 20 + n
      LPF ALTER_EFFECT INT_VAR header=header parameter1=damage dicenumber=dice END
    END
  END


//
//Add Revitalizer
ACTION_IF (GAME_IS ~iwdee~) BEGIN
  OUTER_SET kitdesc = RESOLVE_STR_REF (@40314)    // for IWDEE
END
ELSE BEGIN
  OUTER_SET kitdesc = RESOLVE_STR_REF (@40214)    // for BGEE games
END

COPY - ~%abilities%/revitalizer.PRO~  ~%workspace%/%w7%.PRO~
ADD_PROJECTILE ~%workspace%/%w7%.PRO~
COPY_EXISTING ~%cure_subspell%.SPL~  ~override/%w7%.SPL~    // clone a Cure Wounds spell
  SAY NAME1 @40114
  WRITE_LONG 0x50 kitdesc

  LPF d2_remove_level_headers END
  LPF d2_convert_spell_type INT_VAR type=1 level=7 END    // convert to level 7 Wizard spell
  LPF ALTER_EFFECT INT_VAR power=0 END                    // power=0 to bypass immunity to spell level

  WRITE_SHORT 0x22 15             // casting animation
  WRITE_LONG 0x18 THIS | BIT24    // can target invisible/sanctuary

  LPF ALTER_SPELL_HEADER INT_VAR speed=1 range=30 projectile=~%w7%~ END
  LPF DELETE_EFFECT INT_VAR match_opcode=164 END
  LPF ALTER_EFFECT INT_VAR match_opcode=17 parameter1=20 dicenumber=14 dicesize=4 END
  LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=324 STR_VAR resource=~%w7%~ END    // update restrictions (if found)

  LPF CD_EXTEND-O-MATIC
    INT_VAR
      level_cap   = %spellcap%
      min_lev_alt = 14
  END

  LPF ALTER_EFFECT INT_VAR match_opcode=17 header=1 dicenumber=15 END    // level 15
  LPF ALTER_EFFECT INT_VAR match_opcode=17 header=2 dicenumber=16 END
  LPF ALTER_EFFECT INT_VAR match_opcode=17 header=3 dicenumber=17 END
  LPF ALTER_EFFECT INT_VAR match_opcode=17 header=4 dicenumber=18 END
  LPF ALTER_EFFECT INT_VAR match_opcode=17 header=5 dicenumber=19 END
  LPF ALTER_EFFECT INT_VAR match_opcode=17 header=6 dicenumber=20 END

  PATCH_IF (spellcap >= 30) BEGIN
    FOR (n = 1; n <= 10; ++n) BEGIN
      SET header = 6 + n
      SET dice   = 20 + n
      LPF ALTER_EFFECT INT_VAR match_opcode=17 header=header dicenumber=dice END
    END
  END

  PATCH_IF (spellcap >= 40) BEGIN
    FOR (n = 11; n <= 20; ++n) BEGIN
      SET header = 6 + n
      SET dice   = 20 + n
      LPF ALTER_EFFECT INT_VAR match_opcode=17 header=header dicenumber=dice END
    END
  END

  PATCH_IF (spellcap = 50) BEGIN
    FOR (n = 21; n <= 30; ++n) BEGIN
      SET header = 6 + n
      SET dice   = 20 + n
      LPF ALTER_EFFECT INT_VAR match_opcode=17 header=header dicenumber=dice END
    END
  END


//
//Add Riftgate golem
COPY - ~%abilities%/riftgategolem.BAF~  ~%workspace%/%w8%g.BAF~    // golem script
COMPILE ~%workspace%/%w8%g.BAF~
COPY ~%abilities%/riftgategolem.CRE~  ~override/%w8%g.CRE~         // add golem
  SAY NAME1 @601 
  SAY NAME2 @601
  REPLACE_CRE_ITEM ~helmnoan~ #0 #0 #0 ~none~ ~helmet~
  REPLACE_CRE_ITEM ~immune3~ #0 #0 #0 ~none~ ~lring~
  REPLACE_CRE_ITEM ~%w8%g~ #0 #0 #0 ~none~ ~weapon1~ EQUIP
  WRITE_ASCII 0x260 ~%w8%g~ #8     // general script
  WRITE_ASCII 0x280 ~%w8%g~ #20    // script name
  WRITE_BYTE  0x275 4              // v4.7: make immune to effects that instakill summons
COPY ~%abilities%/summonrift.EFF~     ~override/%w8%g.EFF~
  WRITE_ASCII 0x30 ~%w8%g~ #8

COPY_EXISTING ~goliro.ITM~  ~override/%w8%g.ITM~       // golem weapon (clone from iron golem)
  WRITE_LONG 0x60 4
  LPF ALTER_ITEM_HEADER INT_VAR range=2 dicesize=10 dicenumber=4 thac0_bonus=2 damage_bonus=2 END
  LPF ADD_ITEM_EFFECT INT_VAR opcode=326 target=2 timing=1 type=1 STR_VAR resource=~%w8%g~ END         // add stun effect
  LPF CLONE_EFFECT INT_VAR check_globals=0 opcode=12 parameter2=4194304 dicenumber=2 dicesize=4 END    // magic damage
  LPF CLONE_EFFECT INT_VAR match_opcode=292 opcode=176 parameter1=2 parameter2=0 END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=98 parameter1=2 parameter2=2 END    // v4.7.5: regen

  // immunities
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~SPPR720~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~ohbquake~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~SPOGRE01~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=292 opcode=101 parameter2=209 END
  LPF CLONE_EFFECT INT_VAR match_opcode=292 opcode=101 parameter2=213 END
  LPF CLONE_EFFECT INT_VAR match_opcode=292 opcode=101 parameter2=216 END
  LPF CLONE_EFFECT INT_VAR match_opcode=292 opcode=101 parameter2=151 END
  LPF CLONE_EFFECT INT_VAR match_opcode=292 opcode=101 parameter2=135 END
  LPF CLONE_EFFECT INT_VAR match_opcode=292 opcode=101 parameter2=166 END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_level_drain_arrays END

  PATCH_IF (GAME_IS ~bgee iwdee~) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=267 END
    LPF d2_string_immunities END
  END

  // immune to vorpal (not immune to opcode to allow anti-golem weapons)
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~planetar~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~devaevil~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~gith~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~ax1h15~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~balor~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~halb11~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~SW2H15~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~FINSOL01~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~FINSOL02~ END

COPY_EXISTING - ~STUNSTRG.EFF~ ~~    // get string ref for "Stunned" (this file is in every game)
  READ_LONG 0x1c stunstring
COPY ~%abilities%/riftgategolem.SPL~  ~override/%w8%g.SPL~    // stun effect (same as golem manual)
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=stunstring END


//
//Add Riftgate balor
COPY - ~%abilities%/riftgatebalor.BAF~  ~%workspace%/%w8%d.BAF~    // balor script
  EVALUATE_BUFFER
  PATCH_IF (GAME_IS ~bgee~) BEGIN
    REPLACE_TEXTUALLY ~CLERIC_STORM_OF_VENGEANCE~     ~CLERIC_UNHOLY_BLIGHT~
    REPLACE_TEXTUALLY ~CLERIC_SUMMON_FALLEN_DEVA~     ~CLERIC_AERIAL_SERVANT~
    REPLACE_TEXTUALLY ~CLERIC_AURA_OF_FLAMING_DEATH~  ~WIZARD_FIRE_SHIELD_RED~
    REPLACE_TEXTUALLY ~CLERIC_IMPLOSION~              ~WIZARD_CHROMATIC_ORB~
  END
COMPILE ~%workspace%/%w8%d.BAF~

COPY ~%abilities%/riftgatebalor.CRE~    ~override/%w8%d.CRE~       // add balor
  SAY NAME1 @602 
  SAY NAME2 @602
  REPLACE_CRE_ITEM ~%w8%y~ #0 #0 #0 ~none~ ~gloves~
  REPLACE_CRE_ITEM ~%w8%x~ #0 #0 #0 ~none~ ~amulet~
  REPLACE_CRE_ITEM ~%w8%v~ #0 #0 #0 ~none~ ~belt~
  REPLACE_CRE_ITEM ~%w8%u~ #0 #0 #0 ~none~ ~boots~
  REPLACE_CRE_ITEM ~%w8%d~ #0 #0 #0 ~none~ ~weapon1~ EQUIP
  WRITE_ASCII 0x250 ~%w8%d~ #8     // class script
  WRITE_ASCII 0x280 ~%w8%d~ #20    // script name
COPY ~%abilities%/summongate.EFF~     ~override/%w8%d.EFF~
  WRITE_ASCII 0x30 ~%w8%d~ #8

COPY ~%abilities%/riftgatebalorhp.ITM~  ~override/%w8%u.ITM~       // balor HP breaks
  LPF ALTER_EFFECT INT_VAR parameter1=280 END
COPY ~%abilities%/riftgatebalorhp.ITM~  ~override/%w8%v.ITM~
  LPF ALTER_EFFECT INT_VAR parameter1=180 END
COPY ~%abilities%/riftgatebalorhp.ITM~  ~override/%w8%x.ITM~
  LPF ALTER_EFFECT INT_VAR parameter1=80 END
COPY ~%abilities%/riftgatebalorhp.ITM~  ~override/%w8%y.ITM~
  LPF ALTER_EFFECT INT_VAR parameter1=1 END

COPY ~%abilities%/riftgatebalor.ITM~    ~override/%w8%d.ITM~       // balor weapon
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=98 parameter1=4 parameter2=2 END    // v4.7.5: regen
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~SPPR720~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~ohbquake~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=176 opcode=318 STR_VAR resource=~SPOGRE01~ END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_stun_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_slow_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_slay_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_kill_target_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_sleep_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_hold_special_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_hold_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_web_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_silence_arrays END
  LPF cd_apply_batch STR_VAR array_name = cd_immunity_level_drain_arrays END

  PATCH_IF (GAME_IS ~bgee iwdee~) BEGIN
    LPF DELETE_EFFECT INT_VAR match_opcode=267 END
    LPF d2_string_immunities END
  END
  ELSE BEGIN
    LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=40 opcode=267 parameter1=335 END
  END

  PATCH_IF (stun_immunity > 0) BEGIN 
    LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=45 opcode=328 parameter2=stun_immunity special=1 END
  END
  PATCH_IF (slow_immunity > 0) BEGIN 
    LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=40 opcode=328 parameter2=slow_immunity special=1 END
  END
  PATCH_IF (death_immunity > 0) BEGIN 
    LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=55 opcode=328 parameter2=death_immunity special=1 END
  END
  PATCH_IF (hold_immunity > 0) BEGIN 
    LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=175 opcode=328 parameter2=hold_immunity special=1 END
  END
  PATCH_IF (web_immunity > 0) BEGIN 
    LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=157 opcode=328 parameter2=web_immunity special=1 END
  END
  PATCH_IF (silence_immunity > 0) BEGIN 
    LPF CLONE_EFFECT INT_VAR match_opcode=101 match_parameter2=38 opcode=328 parameter2=silence_immunity special=1 END
  END


//
//Add Riftgate spell
COPY ~%icons%/riftgatedelay.BAM~  ~override/%w8%i.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@40115) STR_VAR bam=~%w8%i~ RET icon END

COPY ~%abilities%/riftgate.SPL~   ~override/%w8%.SPL~
  SAY NAME1 @40115
  SAY UNIDENTIFIED_DESC @40215
  LPF ALTER_EFFECT INT_VAR match_opcode=206 parameter1=RESOLVE_STR_REF (@303) STR_VAR resource=~%w8%~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~sumsunn~ resource=~%w8%g~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~sumzaam~ resource=~%w8%d~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=265 STR_VAR resource=~d2riftga~ END    // variable for script action
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END


//
//add spell icons
ACTION_FOR_EACH icon IN ~%w1%~ ~%w2%~ ~%w3%~ ~%w4%~ ~%w5%~ ~%w6%~ ~%w7%~ ~%w8%~ BEGIN
  COPY_EXISTING ~%icon%.SPL~  ~override~
    WRITE_ASCII 0x3a ~%icon%a~ #8
    LPF ALTER_SPELL_HEADER STR_VAR icon=~%icon%b~ END
  BUT_ONLY
END


//
//Add spells to spell.ids
ACTION_IF (%nowizardids% = 0) BEGIN
  OUTER_FOR (n = 1; n <= 8; ++n) BEGIN
    LAF ADD_SPELL_EX INT_VAR type=2 level=%n% min=51 max=99 hide=1 STR_VAR file=~override/%w%n%%.SPL~ name=~D2%kitname%_W%n%~ RET spell_res END
    OUTER_TEXT_SPRINT ~w%n%~  ~%spell_res%~
  END

  COPY ~override/%w7%.SPL~  ~override~
    LPF ALTER_EFFECT INT_VAR silent=1 match_opcode=324 STR_VAR resource=~%w7%~ END    // update restrictions (if found)
  BUT_ONLY
END


//
//Add Interference
COPY - ~%abilities%/interference.PRO~ ~%workspace%/%s1%.PRO~
ADD_PROJECTILE ~%workspace%/%s1%.PRO~

COPY ~%icons%/interference.BAM~      ~override/%s1%.BAM~
COPY ~%abilities%/interference.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @111
  SAY UNIDENTIFIED_DESC @211
  WRITE_ASCII 0x3a ~%s1%~ #8
  LPF ALTER_SPELL_HEADER INT_VAR projectile=~%s1%~ STR_VAR icon=~%s1%~ END

  LPF ALTER_EFFECT INT_VAR header=1 match_opcode=60 parameter1=30 END    // level 10 (casting failure)
  LPF ALTER_EFFECT INT_VAR header=2 match_opcode=60 parameter1=35 END
  LPF ALTER_EFFECT INT_VAR header=3 match_opcode=60 parameter1=40 END
  LPF ALTER_EFFECT INT_VAR header=4 match_opcode=60 parameter1=45 END
  LPF ALTER_EFFECT INT_VAR header=5 match_opcode=60 parameter1=50 END    // level 30
  LPF ALTER_EFFECT INT_VAR header=6 match_opcode=60 parameter1=55 END
  LPF ALTER_EFFECT INT_VAR header=7 match_opcode=60 parameter1=60 END
  LPF ALTER_EFFECT INT_VAR header=8 match_opcode=60 parameter1=65 END
  LPF ALTER_EFFECT INT_VAR header=9 match_opcode=60 parameter1=70 END    // level 50


//
//Make clab table
COPY ~inlined/d2clab_sorcerer~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=5 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%w1%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%w2%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%w3%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%w4%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%w5%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%w6%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%w7%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%w8%~ END
  LPF d2_kit_ability_adder END


//
//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 19
    mixed = RESOLVE_STR_REF (@100801)
    lower = RESOLVE_STR_REF (@100802)
    help = RESOLVE_STR_REF (@100803)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
//  source_kit = DRAGON_DISCIPLE    // use Dragon Disciple as base (v4.8: using Sorcerer instead)
    clsrcreq = ~1 1 1 1 1 1 1~      // Usable by all races, if base class is usable
    abclasrq = ~0 0 0 12 0 12~      // min stats
    abclsmod = ~0 -2 0 0 -2 0~      // -2 to Dex and Wis
    abdcscrq = ~0 0 0 15 0 15~      // not used (dual class), this is just for aesthetics
    abdcdsrq = ~0 0 0 17 0 17~      // not used (dual class)
    clascolr = ~72 67 169 93 72~    // Kensai colors
//  hpclass = ~HPWIZ~               // same HP as Wizard (needed if using DD as source kit)
  RET 
    kit_id
END


//
//Set variables for external use
OUTER_TEXT_SPRINT starjammer     ~%kitid%~
OUTER_TEXT_SPRINT interference   ~%s1%~
OUTER_TEXT_SPRINT riftgatebalor  ~%w8%d~
OUTER_TEXT_SPRINT riftstep       ~%w1%~
OUTER_TEXT_SPRINT steadycurrent  ~%w2%~
OUTER_TEXT_SPRINT swapper        ~%w3%~
OUTER_TEXT_SPRINT pacifier       ~%w4%~
OUTER_TEXT_SPRINT jolt           ~%w5%~
OUTER_TEXT_SPRINT electrocannon  ~%w6%~
OUTER_TEXT_SPRINT revitalizer    ~%w7%~
OUTER_TEXT_SPRINT riftgate       ~%w8%~


/*
*/