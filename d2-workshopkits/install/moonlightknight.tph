//Moonlight Knight (d2wk4)


//Add passive effects
COPY ~%passives%/disablebutton.SPL~  ~override/%e1%.SPL~
COPY ~%passives%/savebonus1all.SPL~  ~override/%e2%.SPL~
  LPF ALTER_EFFECT INT_VAR parameter1=2 END


//Add Torrent
COPY ~%passives%/hiteffect1melee.SPL~  ~override/%e3%.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%e3%a~ END
  LPF CLONE_EFFECT INT_VAR opcode=139 parameter1=RESOLVE_STR_REF (@316) END    // "Gained Passive Ability: Torrent"

COPY ~%abilities%/castspellself.EFF~  ~override/%e3%a.EFF~
  WRITE_ASCII 0x30 ~%e3%b~ #8
COPY_EXISTING ~SPWI305.SPL~           ~override/%e3%b.SPL~      // clone Haste
  LPF d2_remove_level_headers END
  LPF d2_subspell_remove_effects END
  LPF d2_convert_spell_type INT_VAR type=0 level=1 END           // convert to special ability
  LPF DELETE_EFFECT INT_VAR match_opcode=176 END                 // revert some sr changes
  LPF DELETE_EFFECT INT_VAR match_opcode=1 END
  LPF DELETE_EFFECT INT_VAR match_opcode=206 END
  LPF DELETE_EFFECT INT_VAR match_timing=4 END                   // remove fatigue
  LPF ALTER_EFFECT INT_VAR match_timing=0 duration=10 END
  LPF ALTER_EFFECT INT_VAR target=1 probability1=6 END
  LPF DELETE_EFFECT INT_VAR match_opcode=16 END                  // delete and re-add opcode 16
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_timing=0 opcode=16 parameter2=0 END

  LPF CD_EXTEND-O-MATIC                                  // clone spell header to level 20
    INT_VAR 
      step_dur=0 
      step_size=2 
      dur_special=1 
      min_lev_alt=2 
  END

  LPF ALTER_EFFECT INT_VAR header=1 probability1=7 END   // level 4
  LPF ALTER_EFFECT INT_VAR header=2 probability1=8 END
  LPF ALTER_EFFECT INT_VAR header=3 probability1=9 END
  LPF ALTER_EFFECT INT_VAR header=4 probability1=10 END
  LPF ALTER_EFFECT INT_VAR header=5 probability1=11 END
  LPF ALTER_EFFECT INT_VAR header=6 probability1=12 END
  LPF ALTER_EFFECT INT_VAR header=7 probability1=13 END
  LPF ALTER_EFFECT INT_VAR header=8 probability1=14 END
  LPF ALTER_EFFECT INT_VAR header=9 probability1=15 END  // level 20
BUT_ONLY


//Add Moonlight Vortex
COPY - ~%abilities%/moonlightspell.PRO~  ~%workspace%/%s9%a.PRO~     // projectile
  WRITE_ASCII 0x104 ~%s9%a~ #8    // BAM animation
ADD_PROJECTILE ~%workspace%/%s9%a.PRO~
COPY ~%abilities%/moonlightspell.BAM~   ~override/%s9%a.BAM~

COPY ~%abilities%/moonlightvortex.SPL~  ~override/%s1%.SPL~
  SAY NAME1 @105
  SAY UNIDENTIFIED_DESC @205
  LPF ALTER_SPELL_HEADER INT_VAR projectile=~%s9%a~ END    // set projectile
  LPF ALTER_EFFECT INT_VAR match_opcode=326 resist_dispel=1 STR_VAR resource=~%s1%a~ END    // apply damage (can resist)
  LPF CLONE_EFFECT INT_VAR match_opcode=326 resist_dispel=2 STR_VAR resource=~%s1%b~ END    // op177 effects (no resist)
  LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%s1%~ END     // ignore allies (prevents 'resisted' message)
  LPF ALTER_EFFECT INT_VAR match_opcode=146 STR_VAR resource=~%s1%e~ END    // magic resist (self)
  LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=326 parameter2=52 STR_VAR resource=~%s1%d~ insert=~first~ END

COPY ~%abilities%/moonlightspell.SPL~   ~override/%s1%a.SPL~    // damage
COPY ~%abilities%/moonlighttarget.SPL~  ~override/%s1%b.SPL~    // op177 effects (no resist)
  LPF ALTER_EFFECT STR_VAR resource=~%s1%b~ END
COPY ~%abilities%/applyeffects.EFF~     ~override/%s1%b.EFF~
  WRITE_ASCII 0x30 ~%s1%a~ #8

COPY ~%abilities%/moonlighttickle.SPL~  ~override/%s1%d.SPL~    // lighting effect
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@319) END    // "Tickled..."
COPY ~%abilities%/moonlightresist.SPL~  ~override/%s1%e.SPL~    // magic resist (self)
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%e~ END


//Make clab table
COPY ~inlined/d2clab_ranger~  ~override/%kitid%.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 passive=1 STR_VAR resource=~%e3%~ END
  LPF d2_kit_ability_adder INT_VAR level=6 multi=1 increment=4 limit=14 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 12
    mixed = RESOLVE_STR_REF (@100401)
    lower = RESOLVE_STR_REF (@100402)
    help = RESOLVE_STR_REF (@100403)
    fallen_notice = RESOLVE_STR_REF (@100404)
  STR_VAR
    kit_name = ~D2%kitname%~
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~         // Usable by all races, if base class is usable
    alignmnt = ~1 1 1 1 1 1 1 1 1~
    weapprof = ~2 2 1 2 2 2 2 1 2 2 2 2 2 2 2 2 2 2 2 2 2 2 2 1 1 1 1 1 2 2 2 3~
    clasiskl = ~0 0 0 0 0 0 0~
    thiefscl = ~0 0 0 0 0 0 0 0~
    clascolr = ~137 141 142 24 138~    // shaman colors
  RET 
    kit_id
END


//Set variables for external use
OUTER_TEXT_SPRINT moonlightknight  ~%kitid%~
OUTER_TEXT_SPRINT moonlightvortex  ~%s1%~
OUTER_TEXT_SPRINT fnpclass         ~ranger~
OUTER_TEXT_SPRINT fnpdeity         ~ranger_forest_spheres~


/*
*/