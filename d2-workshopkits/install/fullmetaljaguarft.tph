//Full Metal Jaguar FT


ACTION_IF (nofullmetaljaguarft = 0) BEGIN


//Multiclass import fix
LAF d2_mc_import_fix INT_VAR amount=1  STR_VAR spell=~%s1%~ new_spell=~%#1%~ END
LAF d2_mc_import_fix INT_VAR amount=2  STR_VAR spell=~%s1%~ new_spell=~%#2%~ END
LAF d2_mc_import_fix INT_VAR amount=3  STR_VAR spell=~%s1%~ new_spell=~%#3%~ END
LAF d2_mc_import_fix INT_VAR amount=4  STR_VAR spell=~%s1%~ new_spell=~%#4%~ END
LAF d2_mc_import_fix INT_VAR amount=5  STR_VAR spell=~%s1%~ new_spell=~%#5%~ END
LAF d2_mc_import_fix INT_VAR amount=6  STR_VAR spell=~%s1%~ new_spell=~%#6%~ END
LAF d2_mc_import_fix INT_VAR amount=7  STR_VAR spell=~%s1%~ new_spell=~%#7%~ END
LAF d2_mc_import_fix INT_VAR amount=8  STR_VAR spell=~%s1%~ new_spell=~%#8%~ END
LAF d2_mc_import_fix INT_VAR amount=9  STR_VAR spell=~%s1%~ new_spell=~%#9%~ END
LAF d2_mc_import_fix INT_VAR amount=10 STR_VAR spell=~%s1%~ new_spell=~%#a%~ END


//Array for import fix
ACTION_CLEAR_ARRAY ~import_fix_array~
ACTION_DEFINE_ASSOCIATIVE_ARRAY import_fix_array BEGIN
  ~%#1%~  => 5     // ghostwalker
  ~%#2%~  => 10
  ~%#3%~  => 15
  ~%#4%~  => 20
  ~%#5%~  => 25
  ~%#6%~  => 30
  ~%#7%~  => 35
  ~%#8%~  => 40
  ~%#9%~  => 45
  ~%#a%~  => 50
END


//Make 2da file (don't include base class abilities)
COPY ~%files%/kit6.2DA~  ~override/%fullmetaljaguar%T.2DA~
COPY ~%files%/kit0.2DA~  ~override/%fullmetaljaguar%F.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e2%~ END
  LPF d2_kit_ability_adder INT_VAR level=8 passive=1 STR_VAR resource=~%e3%~ END
  LPF d2_kit_ability_adder INT_VAR level=8 passive=1 STR_VAR resource=~%e4%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=5 STR_VAR resource=~%s1%~ END

  //import fix (BG2, ToB, SoD)
  PHP_EACH import_fix_array AS file => level BEGIN
    LPF d2_kit_ability_adder INT_VAR level=level passive=1 STR_VAR resource=~%file%~ END
  END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 9
    mixed = RESOLVE_STR_REF (@101501)
    lower = RESOLVE_STR_REF (@101502)
    help = RESOLVE_STR_REF (@101503)
  STR_VAR
    kit_name = ~D2%kitname%FT~
    clab_base_f = ~%fullmetaljaguar%F.2DA~    // kit abilities added to Fighter table
    clab_base_t = ~%fullmetaljaguar%T.2DA~    // no abilities added to Thief table
    clsrcreq = ~1 1 1 1 1 1 1~                // Usable by all races, if base class is usable
    clascolr = ~24 67 56 2 25~                // blade/jester colors
  RET 
    kit_id
END


//Usability - restrict plate mail
COPY_EXISTING_REGEXP GLOB ~.*\.ITM~  ~override~
  PATCH_IF (!(~%SOURCE_RES%~ STRING_EQUAL_CASE ~secret05~) AND    // skip Big Metal Unit
            !(~%SOURCE_RES%~ STRING_EQUAL_CASE ~d2tec05~)) BEGIN
    READ_ASCII 0x22 itemtype (2) NULL                             // equipped appearance
    PATCH_IF (~%itemtype%~ STRING_EQUAL_CASE ~4A~) BEGIN          // plate mail
      LPF ADD_ITEM_EQEFFECT
        INT_VAR
          opcode = 319
          target = 1
          parameter1 = IDS_OF_SYMBOL (kit ~D2%kitname%FT~)
          parameter2 = 9
          timing = 2
      END
    END
  END
BUT_ONLY


END


/*
*/