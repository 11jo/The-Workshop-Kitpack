//Crow Hunter (f/m/t)


ACTION_IF (nocrowhunterfmt = 0) BEGIN


//Remove specialist bonus slots
COPY ~%passives%/fewerspells1.SPL~  ~override/%e4%.SPL~
COPY ~%passives%/opcode206.SPL~     ~override/%e5%.SPL~    // only relevant for 5e casting mod
  LPF ALTER_EFFECT STR_VAR resource=~d5z0slt~ END
  LPF CLONE_EFFECT STR_VAR resource=~D5ZMSLT~ END


//Multiclass import fix
LAF d2_mc_import_fix INT_VAR amount=1  STR_VAR spell=~%s1%~ new_spell=~%#1%~ END
LAF d2_mc_import_fix INT_VAR amount=2  STR_VAR spell=~%s1%~ new_spell=~%#2%~ END
LAF d2_mc_import_fix INT_VAR amount=3  STR_VAR spell=~%s1%~ new_spell=~%#3%~ END
LAF d2_mc_import_fix INT_VAR amount=4  STR_VAR spell=~%s1%~ new_spell=~%#4%~ END
LAF d2_mc_import_fix INT_VAR amount=5  STR_VAR spell=~%s1%~ new_spell=~%#5%~ END
LAF d2_mc_import_fix INT_VAR amount=6  STR_VAR spell=~%s1%~ new_spell=~%#6%~ END
LAF d2_mc_import_fix INT_VAR amount=7  STR_VAR spell=~%s1%~ new_spell=~%#7%~ END
LAF d2_mc_import_fix INT_VAR amount=8  STR_VAR spell=~%s1%~ new_spell=~%#8%~ END
LAF d2_mc_import_fix INT_VAR amount=9  STR_VAR spell=~%s1%~ new_spell=~%#9%~ END
LAF d2_mc_import_fix INT_VAR amount=10 STR_VAR spell=~%s1%~ new_spell=~%#a%~ END
LAF d2_mc_import_fix INT_VAR amount=11 STR_VAR spell=~%s1%~ new_spell=~%#b%~ END
LAF d2_mc_import_fix INT_VAR amount=12 STR_VAR spell=~%s1%~ new_spell=~%#c%~ END

LAF d2_mc_import_fix INT_VAR amount=1  STR_VAR spell=~%s2%~ new_spell=~%!1%~ END
LAF d2_mc_import_fix INT_VAR amount=2  STR_VAR spell=~%s2%~ new_spell=~%!2%~ END
LAF d2_mc_import_fix INT_VAR amount=3  STR_VAR spell=~%s2%~ new_spell=~%!3%~ END


//Array for import fix
ACTION_CLEAR_ARRAY ~import_fix_array~
ACTION_DEFINE_ASSOCIATIVE_ARRAY import_fix_array BEGIN
  ~%#1%~  => 4     // splintering shot
  ~%#2%~  => 8
  ~%#3%~  => 12
  ~%#4%~  => 16
  ~%#5%~  => 20
  ~%#6%~  => 24
  ~%#7%~  => 28
  ~%#8%~  => 32
  ~%#9%~  => 36
  ~%#a%~  => 40
  ~%#b%~  => 44
  ~%#c%~  => 48
  ~%!1%~  => 11    // visceral attack
  ~%!2%~  => 15
  ~%!3%~  => 19
END


//Ability tables (don't include base class abilities)
COPY ~%files%/kit6.2DA~  ~override/%crowhunter%F.2DA~
COPY ~%files%/kit6.2DA~  ~override/%crowhunter%M.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e4%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e5%~ END

COPY ~%files%/kit0.2DA~  ~override/%crowhunter%T.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=4 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=11 multi=1 increment=4 limit=19 STR_VAR resource=~%s2%~ END

  //prevent gaining/using traps (multiclass kits)
  LPF d2_kit_ability_adder INT_VAR passive=1 multi=1 increment=1 STR_VAR resource=~%@2%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e6%~ END

  //import fix (BG2, ToB, SoD)
  PHP_EACH import_fix_array AS file => level BEGIN
    LPF d2_kit_ability_adder INT_VAR level=level passive=1 STR_VAR resource=~%file%~ END
  END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 10
    mixed = RESOLVE_STR_REF (@101001)
    lower = RESOLVE_STR_REF (@101002)
    help = RESOLVE_STR_REF (@101003)
  STR_VAR
    kit_name = ~D2%kitname%FMT~
    clab_base_f = ~%crowhunter%F.2DA~           // no abilities added to Fighter table
    clab_base_m = ~%crowhunter%M.2DA~           // -1 spell slots added to Mage table
    clab_base_t = ~%crowhunter%T.2DA~           // kit abilities added to Thief table
    clsrcreq = ~1 1 1 1 1 1 1~                  // Usable by all races, if base class is usable
    thiefscl = ~100 100 100 100 100 100 0 0~    // no points in set traps
    thiefskl = ~40 20~                          // start with 40, gain 20 per level
    clascolr = ~93 182 136 184 93~              // use Stalker colors
  RET 
    kit_id
END


//Give 100 Set Traps when using HLA traps

/*
Works when used from EFF file.
Does not work from SPL file (if set to 0 in thiefscl.2DA)
*/

ACTION_FOR_EACH ~trap~ IN ~SPCL910~ ~SPCL911~ ~SPCL912~ ~RR#THL04~ BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~%trap%.SPL~) BEGIN
    COPY_EXISTING ~%trap%.SPL~  ~override~
      LPF CLONE_EFFECT
        INT_VAR
          multi_match   = 1
          opcode        = 177
          target        = 1
          timing        = 1
          parameter1    = IDS_OF_SYMBOL (kit ~D2%kitname%FMT~)
          parameter2    = 9
        STR_VAR
          resource      = ~%e3%~
      END
    BUT_ONLY
  END

END


//Gains stalker effects from items
OUTER_TEXT_SPRINT kit_group ~stalker~
OUTER_TEXT_SPRINT ids_name  ~D2%kitname%FMT~

ACTION_PHP_EACH item_kit_effects AS item => class BEGIN
  ACTION_IF !(~%kit_group%~ STRING_CONTAINS_REGEXP ~%class%~) BEGIN
    ACTION_FOR_EACH res IN ~SPL~ ~ITM~ BEGIN
      ACTION_IF (FILE_EXISTS_IN_GAME ~%item%.%res%~) BEGIN
        COPY_EXISTING ~%item%.%res%~  ~override~
          PHP_EACH kit_ids_array AS class2 => eff_to_copy BEGIN
            PATCH_IF (~%class%~ STR_EQ ~%class2%~) BEGIN
              LPF CLONE_EFFECT 
                INT_VAR 
                  silent            = 1
                  match_opcode      = 177 
                  match_parameter1  = %eff_to_copy%
                  match_parameter2  = 9 
                  parameter1        = IDS_OF_SYMBOL (kit ~%ids_name%~)
              END
            END
          END
        BUT_ONLY
      END
    END
  END
END


END


/*
*/