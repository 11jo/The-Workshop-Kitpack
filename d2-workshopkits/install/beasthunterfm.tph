//Beast Hunter (f/m)


ACTION_IF (nobeasthunterfm = 0) BEGIN


//Remove specialist bonus slots
COPY ~%passives%/fewerspells1.SPL~  ~override/%e4%.SPL~
COPY ~%passives%/opcode206.SPL~     ~override/%e5%.SPL~    // only relevant for 5e casting mod
  LPF ALTER_EFFECT STR_VAR resource=~d5z0slt~ END
  LPF CLONE_EFFECT STR_VAR resource=~D5ZMSLT~ END


//Multiclass import fix
LAF d2_mc_import_fix INT_VAR amount=1  STR_VAR spell=~%s1%~ new_spell=~%#1%~ END
LAF d2_mc_import_fix INT_VAR amount=2  STR_VAR spell=~%s1%~ new_spell=~%#2%~ END
LAF d2_mc_import_fix INT_VAR amount=3  STR_VAR spell=~%s1%~ new_spell=~%#3%~ END
LAF d2_mc_import_fix INT_VAR amount=4  STR_VAR spell=~%s1%~ new_spell=~%#4%~ END
LAF d2_mc_import_fix INT_VAR amount=5  STR_VAR spell=~%s1%~ new_spell=~%#5%~ END
LAF d2_mc_import_fix INT_VAR amount=6  STR_VAR spell=~%s1%~ new_spell=~%#6%~ END
LAF d2_mc_import_fix INT_VAR amount=7  STR_VAR spell=~%s1%~ new_spell=~%#7%~ END
LAF d2_mc_import_fix INT_VAR amount=8  STR_VAR spell=~%s1%~ new_spell=~%#8%~ END
LAF d2_mc_import_fix INT_VAR amount=9  STR_VAR spell=~%s1%~ new_spell=~%#9%~ END
LAF d2_mc_import_fix INT_VAR amount=10 STR_VAR spell=~%s1%~ new_spell=~%#a%~ END
LAF d2_mc_import_fix INT_VAR amount=11 STR_VAR spell=~%s1%~ new_spell=~%#b%~ END
LAF d2_mc_import_fix INT_VAR amount=12 STR_VAR spell=~%s1%~ new_spell=~%#c%~ END
LAF d2_mc_import_fix INT_VAR amount=13 STR_VAR spell=~%s1%~ new_spell=~%#d%~ END

LAF d2_mc_import_fix INT_VAR amount=1  STR_VAR spell=~%s2%~ new_spell=~%!1%~ END
LAF d2_mc_import_fix INT_VAR amount=2  STR_VAR spell=~%s2%~ new_spell=~%!2%~ END
LAF d2_mc_import_fix INT_VAR amount=3  STR_VAR spell=~%s2%~ new_spell=~%!3%~ END


//Array for import fix
ACTION_CLEAR_ARRAY ~import_fix_array~
ACTION_DEFINE_ASSOCIATIVE_ARRAY import_fix_array BEGIN
  ~%#1%~  => 2     // hunter tools
  ~%#2%~  => 6
  ~%#3%~  => 10
  ~%#4%~  => 14
  ~%#5%~  => 18
  ~%#6%~  => 22
  ~%#7%~  => 26
  ~%#8%~  => 30
  ~%#9%~  => 34
  ~%#a%~  => 38
  ~%#b%~  => 42
  ~%#c%~  => 46
  ~%#d%~  => 50
  ~%!1%~  => 5     // quicksilver
  ~%!2%~  => 9
  ~%!3%~  => 13
END


//Ability tables (don't include base class abilities)
COPY ~inlined/d2clab.2DA~  ~override/%beasthunter%F.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e1%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=4 limit=13 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder INT_VAR level=8 passive=1 STR_VAR resource=~%e2%~ END

  // import fix (BG2, ToB, SoD)
  PHP_EACH import_fix_array AS file => level BEGIN
    LPF d2_kit_ability_adder INT_VAR level=level passive=1 STR_VAR resource=~%file%~ END
  END

COPY ~inlined/d2clab.2DA~  ~override/%beasthunter%M.2DA~
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e4%~ END
  LPF d2_kit_ability_adder INT_VAR passive=1 STR_VAR resource=~%e5%~ END
  LPF tof_denote_spellschool STR_VAR type=~evocation~ END    // tof denote hla


//
LAF d2_get_mc_text STR_VAR class=fighter multi=fm RET baseclass multiclass END
OUTER_SET kit_note = RESOLVE_STR_REF(@353)
OUTER_SET base_kit = RESOLVE_STR_REF(@100103)
LAF d2_build_mc_desc INT_VAR desc1=base_kit desc2=kit_note mode=2 STR_VAR multi=fm RET kit_desc END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 7
    mixed = RESOLVE_STR_REF (@100101)
    lower = RESOLVE_STR_REF (@100102)
    help = kit_desc
  STR_VAR
    kit_name = ~D2%kitname%FM~
    clab_base_f = ~%beasthunter%F.2DA~    // kit abilities added to Fighter table
    clab_base_m = ~%beasthunter%M.2DA~    // -1 spell slots added to Mage table
    clsrcreq = ~1 1 1 1 1 1 1~            // Usable by all races, if base class is usable
    clascolr = ~28 93 91 91 93~           // Archer colors
  RET 
    kit_id
END


//hla table
OUTER_TEXT_SPRINT ids_name ~D2%kitname%FM~
OUTER_TEXT_SPRINT abbrev   ~%kitid%1~
OUTER_TEXT_SPRINT class    ~FIGHTER_MAGE~
LAF d2_hla_baseclass STR_VAR kit=~%ids_name%~ class=~%class%~ END
LAF d2_hla_tableres STR_VAR kit=~%ids_name%~ abbrev=~%abbrev%~ END


//armor restriction
OUTER_TEXT_SPRINT ids_name ~D2%kitname%FM~
OUTER_TEXT_SPRINT armor_type ~studded~
OUTER_TEXT_SPRINT $d2_restrict_armor_array (~%ids_name%~) ~%armor_type%~

OUTER_TEXT_SPRINT baseclass ~fm~
OUTER_TEXT_SPRINT $d2_restrict_armor_baseclass (~%ids_name%~) ~%baseclass%~


//Set Variable
OUTER_SET beasthunterfm = 1

END ELSE BEGIN

OUTER_SET beasthunterfm = 0

END


/* 
*/