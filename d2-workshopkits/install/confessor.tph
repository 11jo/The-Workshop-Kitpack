//Confessor (d2wk5)

//Clone mage spells
ACTION_IF (GAME_IS ~iwdee~) BEGIN 
  OUTER_TEXT_SPRINT blind ~SPWI226~
  OUTER_SET blindness = RESOLVE_STR_REF (@40303)      // IWDEE
  OUTER_SET blur = RESOLVE_STR_REF (@40304)
END
ELSE BEGIN
  ACTION_IF (MOD_IS_INSTALLED ~setup-spell_rev.tp2~ (ID_OF_LABEL ~setup-spell_rev.tp2~ ~dv-spell_rev-main~)) BEGIN
    OUTER_TEXT_SPRINT blind ~SPWI106~ 
    OUTER_SET blindness = RESOLVE_STR_REF (@40403)    // spell revisions
    OUTER_SET blur = RESOLVE_STR_REF (@40404)
  END
  ELSE BEGIN
    OUTER_TEXT_SPRINT blind ~SPWI106~ 
    OUTER_SET blindness = RESOLVE_STR_REF (@40203)    // BGEE games
    OUTER_SET blur = RESOLVE_STR_REF (@40204)
  END
END

LAF d2_convert_spell_type INT_VAR type=2 level=1 STR_VAR spell=~%blind%~ newspell=~%p1%~ END
LAF d2_convert_spell_type INT_VAR type=2 level=2 STR_VAR spell=~SPWI201~ newspell=~%p2%~ END
LAF d2_convert_spell_type INT_VAR type=2 level=3 STR_VAR spell=~SPWI314~ newspell=~%p3%~ END
LAF d2_convert_spell_type INT_VAR type=2 level=4 STR_VAR spell=~SPWI419~ newspell=~%p4%~ END

COPY_EXISTING ~%p1%.SPL~ ~override~ WRITE_LONG 0x50 %blindness% BUT_ONLY
COPY_EXISTING ~%p2%.SPL~ ~override~ WRITE_LONG 0x50 %blur% BUT_ONLY


//Add spells to spell.ids
ACTION_IF (%nopriestids% = 0) BEGIN
  OUTER_FOR (n = 1; n <= 4; ++n) BEGIN
    LAF ADD_SPELL_EX INT_VAR type=1 level=%n% min=51 max=99 hide=1 STR_VAR file=~override/%p%n%%.SPL~ name=~D2%kitname%_P%n%~ RET spell_res END
    OUTER_TEXT_SPRINT ~p%n%~  ~%spell_res%~
  END
END


//Add Rooting Shot
COPY ~%icons%/calledshot.BAM~               ~override/%s1%.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@106) STR_VAR bam=~%s1%~ RET icon END
COPY ~%abilities%/rootingshot.SPL~          ~override/%s1%.SPL~
  SAY NAME1 @106
  SAY UNIDENTIFIED_DESC @206
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR match_resource=~archmov~ resource=~%s1%a~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR match_resource=~archmov~ resource=~%s1%b~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR match_resource=~archmov~ resource=~%s1%c~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/castspelltarget.EFF~      ~override/%s1%a.EFF~    // entangle 6 rounds
    WRITE_ASCII 0x30 ~%s1%a~ #8
COPY ~%abilities%/rootingshotentangle.SPL~  ~override/%s1%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_duration=18 duration=36 END
  LPF ALTER_EFFECT INT_VAR savingthrow=1 END
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@301) END    // "Entangled"
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=206 duration=18 STR_VAR resource=~%s1%b~ END

COPY ~%abilities%/castspelltarget.EFF~      ~override/%s1%b.EFF~    // entangle 3 rounds
    WRITE_ASCII 0x30 ~%s1%b~ #8
COPY ~%abilities%/rootingshotentangle.SPL~  ~override/%s1%b.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@301) END    // "Entangled"
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%b~ END
  LPF DELETE_EFFECT INT_VAR match_opcode=206 END

COPY ~%abilities%/castspellself.EFF~        ~override/%s1%c.EFF~    // remove effect
  WRITE_ASCII 0x30 ~%s1%c~ #8
COPY ~%abilities%/removespelleffects.SPL~   ~override/%s1%c.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%s1%~ END


//add Rooting Shot to spells that block Entangle (base-game only has Free Action)
ACTION_BASH_FOR ~override~ ~.*\.SPL~ BEGIN
  COPY_EXISTING ~%BASH_FOR_FILESPEC%~  ~override~        // check override files (i.e. mods)
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=154 opcode=206 parameter2=0 STR_VAR resource=~%s1%a~ END
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=154 opcode=206 parameter2=0 STR_VAR resource=~%s1%b~ END
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 opcode=321 parameter2=0 timing=1 STR_VAR match_resource=~%s1%a~ insert=first END
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 opcode=321 parameter2=0 timing=1 STR_VAR match_resource=~%s1%b~ insert=first END
  BUT_ONLY
END

ACTION_IF !(FILE_EXISTS ~override/sppr403.SPL~) BEGIN    // if Free Action not patched
  COPY_EXISTING ~sppr403.SPL~  ~override~
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=154 opcode=206 parameter2=0 STR_VAR resource=~%s1%a~ END
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=154 opcode=206 parameter2=0 STR_VAR resource=~%s1%b~ END
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 opcode=321 parameter2=0 timing=1 STR_VAR match_resource=~%s1%a~ insert=first END
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 opcode=321 parameter2=0 timing=1 STR_VAR match_resource=~%s1%b~ insert=first END
  BUT_ONLY
END


//add Rooting Shot to items that block Entangle
ACTION_BASH_FOR ~override~ ~.*\.ITM~ BEGIN
  COPY_EXISTING ~%BASH_FOR_FILESPEC%~  ~override~        // check override files (i.e. mods)
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=154 opcode=206 parameter2=0 STR_VAR resource=~%s1%a~ END
    LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=154 opcode=206 parameter2=0 STR_VAR resource=~%s1%b~ END
  BUT_ONLY
END

ACTION_FOR_EACH noentangle IN    // files from BGEE, BG2EE, IWDEE
  ~ABAZRING~
  ~ALDETH~
  ~BAZPATRG~
  ~bdmisc1b~
  ~bdmisc1c~
  ~bdring09~
  ~bdringge~
  ~bdringgh~
  ~BEHOLDER~
  ~BLUN30~
  ~BOOTMOA~
  ~CIGBARD~
  ~CIIGEN~
  ~dragring~
  ~FREEACT~
  ~FREERING~
  ~FSSPIR~
  ~GORCHR~
  ~GVALOR1~
  ~HOLDRING~
  ~INVULNER~
  ~JWSUPER~
  ~LABELT~
  ~MAGE05~
  ~NPPLAT~
  ~OBJRING~
  ~OHBDRKRG~
  ~ohrboot1~
  ~PALRING~
  ~PLYWYVRN~
  ~RING09~
  ~RING97~
  ~ROSSRING~
  ~SHALT01~
  ~SPER12~
  ~SPRNGB03~
  ~SPRNGB04~
  ~SPRNGL03~
  ~SPRNGL04~
  ~SPRNGS01~
  ~SPRNGS02~
  ~SPRNGS03~
  ~SPRNGS04~
  ~SPRNGZ05~
  ~SUREHP1~
  ~SW2H06~
  ~SW2H13~
BEGIN

  ACTION_IF (FILE_EXISTS_IN_GAME ~%noentangle%.ITM~) BEGIN
    ACTION_IF !(FILE_EXISTS ~override/%noentangle%.ITM~) BEGIN    // skip if already patched
      COPY_EXISTING ~%noentangle%.ITM~  ~override~
        LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=154 opcode=206 parameter2=0 STR_VAR resource=~%s1%a~ END
        LPF CLONE_EFFECT INT_VAR silent=1 match_opcode=101 match_parameter2=154 opcode=206 parameter2=0 STR_VAR resource=~%s1%b~ END
      BUT_ONLY
    END
  END

END


//Add Bloodletter
COPY ~%icons%/bloodletter.BAM~      ~override/%s2%.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@107) STR_VAR bam=~%s2%~ RET icon END
COPY ~%abilities%/bloodletter.SPL~  ~override/%s2%.SPL~
  SAY NAME1 @107
  SAY UNIDENTIFIED_DESC @207
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s2%~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~archhit~ resource=~%s2%a~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~archmov~ resource=~%s2%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/castspellself.EFF~    ~override/%s2%a.EFF~    // healing
    WRITE_ASCII 0x30 ~%s2%a~ #8
COPY ~%abilities%/bloodletterheal.SPL~  ~override/%s2%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@307) END    // "Healed"

COPY ~%icons%/bleed.BAM~                ~override/%s2%b.BAM~    // bleeding
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@302) STR_VAR bam=~%s2%b~ RET icon END
COPY ~%abilities%/castspelltarget.EFF~  ~override/%s2%b.EFF~
    WRITE_ASCII 0x30 ~%s2%b~ #8
COPY ~%abilities%/bleed10rounds.SPL~    ~override/%s2%b.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@302) END    // "Bleeding"
  LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%s2%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~%s2%c~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=206 STR_VAR resource=~%s2%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/castspelltarget.EFF~  ~override/%s2%c.EFF~    // bleed damage
    WRITE_ASCII 0x30 ~%s2%c~ #8
COPY ~%abilities%/bleeddamage2.SPL~     ~override/%s2%c.SPL~


//Bleeding removed by Heal or Greater Restoration
COPY_EXISTING_REGEXP GLOB ~.*\.SPL~  ~override~
  READ_LONG   0x8 stringref
  READ_STRREF 0x8 spellname
  PATCH_IF ((~%SOURCE_RES%~ STRING_EQUAL_CASE ~ohbresto~) OR    // if Greater Restoration
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~sppr713~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~spwish07~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~spwish46~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~devaheal~) OR    // if Heal
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~senspihe~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~spin679~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~spin711~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~sppr607~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~spwish28~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~spwish39~) OR
            (~%SOURCE_RES%~ STRING_EQUAL_CASE ~spwm168~) OR
            (stringref = 26310) OR (stringref = 35603) OR (stringref = 36401) OR    // match stringref
            (stringref = 25931) OR (stringref = 8786) OR
            (~%spellname%~ STRING_EQUAL_CASE ~Greater Restoration~) OR              // match spellname (English only)
            (~%spellname%~ STRING_EQUAL_CASE ~Heal~)
           ) BEGIN
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 match_timing=1 opcode=321 parameter2=0 STR_VAR resource=~%s2%b~ END
  END
BUT_ONLY


//Clone base paladin clab
COPY_EXISTING ~CLABPA01.2DA~ ~override/%kitid%.2DA~
  REPLACE_TEXTUALLY ~GA_SPCL211~ ~****~    // remove Lay On Hands
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=4 limit=13 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%p1%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%p2%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%p3%~ END
  LPF d2_kit_ability_adder STR_VAR resource=~%p4%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 6
    mixed = RESOLVE_STR_REF (@100501)
    lower = RESOLVE_STR_REF (@100502)
    help = RESOLVE_STR_REF (@100503)
    fallen_notice = RESOLVE_STR_REF (@100504)
  STR_VAR
    kit_name = ~D2%kitname%~
    unusable = ~0x00010000~        // Stalker usability (does not restrict paladin items)
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~     // Usable by all races, if base class is usable
    clascolr = ~25 83 66 28 23~    // Priest of Talos colors
  RET 
    kit_id
END


//Set variables for external use
OUTER_TEXT_SPRINT confessor       ~%kitid%~
OUTER_TEXT_SPRINT rootingshot     ~%s1%~
OUTER_TEXT_SPRINT entangle36      ~%s1%a~
OUTER_TEXT_SPRINT entangle18      ~%s1%b~
OUTER_TEXT_SPRINT bloodletter     ~%s2%~
OUTER_TEXT_SPRINT confessorbleed  ~%s2%b~
OUTER_TEXT_SPRINT fnpclass        ~zealot~
OUTER_TEXT_SPRINT fnpdeity        ~auril_spheres~
OUTER_TEXT_SPRINT confessor1      ~%p1%~
OUTER_TEXT_SPRINT confessor2      ~%p2%~
OUTER_TEXT_SPRINT confessor3      ~%p3%~
OUTER_TEXT_SPRINT confessor4      ~%p4%~


/*
*/