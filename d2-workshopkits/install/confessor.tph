//Confessor (d2wk5)

//Clone mage spells
ACTION_IF (GAME_IS ~iwdee~) BEGIN 
  OUTER_TEXT_SPRINT blind ~SPWI226~
  OUTER_SET blindness = RESOLVE_STR_REF (@40303)      // IWDEE
  OUTER_SET blur = RESOLVE_STR_REF (@40304)
END
ELSE BEGIN
  ACTION_IF (MOD_IS_INSTALLED ~setup-spell_rev.tp2~ (ID_OF_LABEL ~setup-spell_rev.tp2~ ~dv-spell_rev-main~)) BEGIN
    OUTER_TEXT_SPRINT blind ~SPWI106~ 
    OUTER_SET blindness = RESOLVE_STR_REF (@40403)    // spell revisions
    OUTER_SET blur = RESOLVE_STR_REF (@40404)
  END
  ELSE BEGIN
    OUTER_TEXT_SPRINT blind ~SPWI106~ 
    OUTER_SET blindness = RESOLVE_STR_REF (@40203)    // BGEE games
    OUTER_SET blur = RESOLVE_STR_REF (@40204)
  END
END

LAF d2_convert_spell_type INT_VAR type=2 level=1 STR_VAR spell=~%blind%~ newspell=~%p1%~ END
LAF d2_convert_spell_type INT_VAR type=2 level=2 STR_VAR spell=~SPWI201~ newspell=~%p2%~ END
LAF d2_convert_spell_type INT_VAR type=2 level=3 STR_VAR spell=~SPWI314~ newspell=~%p3%~ END
LAF d2_convert_spell_type INT_VAR type=2 level=4 STR_VAR spell=~SPWI419~ newspell=~%p4%~ END

COPY_EXISTING ~%p1%.SPL~ ~override~ WRITE_LONG 0x50 %blindness% BUT_ONLY
COPY_EXISTING ~%p2%.SPL~ ~override~ WRITE_LONG 0x50 %blur% BUT_ONLY


//Add spells to spell.ids
ACTION_IF (%nopriestids% = 0) BEGIN
  OUTER_FOR (n = 1; n <= 4; ++n) BEGIN
    LAF ADD_SPELL_EX INT_VAR type=1 level=%n% min=51 max=99 hide=1 STR_VAR file=~override/%p%n%%.SPL~ name=~D2%kitname%_P%n%~ RET spell_res END
    OUTER_TEXT_SPRINT ~p%n%~  ~%spell_res%~
  END
END


//Add Rooting Shot
COPY ~%icons%/calledshot.BAM~               ~override/%s1%.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@106) STR_VAR bam=~%s1%~ RET icon END
COPY ~%abilities%/rootingshot.SPL~          ~override/%s1%.SPL~
  SAY NAME1 @106
  SAY UNIDENTIFIED_DESC @206
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR match_resource=~archmov~ resource=~%s1%a~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR match_resource=~archmov~ resource=~%s1%b~ END
  LPF ALTER_EFFECT INT_VAR multi_match=1 STR_VAR match_resource=~archmov~ resource=~%s1%c~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/castspelltarget.EFF~      ~override/%s1%a.EFF~    // entangle 6 rounds
    WRITE_ASCII 0x30 ~%s1%a~ #8
COPY ~%abilities%/rootingshotentangle.SPL~  ~override/%s1%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_duration=18 duration=36 END
  LPF ALTER_EFFECT INT_VAR savingthrow=1 END
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@301) END    // "Entangled"
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=206 duration=18 STR_VAR resource=~%s1%b~ END

COPY ~%abilities%/castspelltarget.EFF~      ~override/%s1%b.EFF~    // entangle 3 rounds
    WRITE_ASCII 0x30 ~%s1%b~ #8
COPY ~%abilities%/rootingshotentangle.SPL~  ~override/%s1%b.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@301) END    // "Entangled"
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%a~ END
  LPF CLONE_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s1%b~ END
  LPF DELETE_EFFECT INT_VAR match_opcode=206 END

COPY ~%abilities%/castspellself.EFF~        ~override/%s1%c.EFF~    // remove effect
  WRITE_ASCII 0x30 ~%s1%c~ #8
COPY ~%abilities%/removespelleffects.SPL~   ~override/%s1%c.SPL~
  LPF ALTER_EFFECT STR_VAR resource=~%s1%~ END


//add Rooting Shot to items/spells that remove Entangle

/*
currently only checks for a few official names
won't catch mod items/spells with unique names
*/

COPY_EXISTING_REGEXP GLOB ~.*\.SPL~  ~override~    // check for Free Action
  READ_STRREF 0x8 spellname
  PATCH_IF !(~%spellname%~ STRING_MATCHES_REGEXP ~.*Free Action.*~) BEGIN    // if spellname DOES contain the text
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 match_timing=0 opcode=206 STR_VAR resource=~%s1%a~ END
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 match_timing=0 opcode=206 STR_VAR resource=~%s1%b~ END
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 opcode=321 parameter2=0 timing=1 STR_VAR match_resource=~%s1%a~ insert=first END
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 opcode=321 parameter2=0 timing=1 STR_VAR match_resource=~%s1%b~ insert=first END
  END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~.*\.ITM~  ~override~    // check for item names
  READ_STRREF 0xc itemname
  PATCH_IF (!(~%itemname%~ STRING_MATCHES_REGEXP ~.*edventar's gift.*~) OR    // if itemname DOES contain the text
            !(~%itemname%~ STRING_MATCHES_REGEXP ~.*ring of free action.*~) OR
            !(~%itemname%~ STRING_MATCHES_REGEXP ~.*spider's bane.*~) OR
             (~%SOURCE_RES%~ STRING_EQUAL_CASE ~gvalor1~) OR    // check for specific filenames
             (~%SOURCE_RES%~ STRING_EQUAL_CASE ~bootmoa~) OR
             (~%SOURCE_RES%~ STRING_EQUAL_CASE ~plywyvrn~) OR
             (~%SOURCE_RES%~ STRING_EQUAL_CASE ~ohrboot1~)) BEGIN
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode=206 target=1 timing=2 STR_VAR resource=~%s1%a~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode=206 target=1 timing=2 STR_VAR resource=~%s1%b~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode=321 target=1 timing=2 STR_VAR resource=~%s1%a~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode=321 target=1 timing=2 STR_VAR resource=~%s1%b~ END
  END
BUT_ONLY


//Add Bloodletter
COPY ~%icons%/bloodletter.BAM~      ~override/%s2%.BAM~
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@107) STR_VAR bam=~%s2%~ RET icon END
COPY ~%abilities%/bloodletter.SPL~  ~override/%s2%.SPL~
  SAY NAME1 @107
  SAY UNIDENTIFIED_DESC @207
  LPF ALTER_EFFECT INT_VAR match_opcode=321 STR_VAR resource=~%s2%~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~archhit~ resource=~%s2%a~ END
  LPF ALTER_EFFECT STR_VAR match_resource=~archmov~ resource=~%s2%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/castspellself.EFF~    ~override/%s2%a.EFF~    // healing
    WRITE_ASCII 0x30 ~%s2%a~ #8
COPY ~%abilities%/bloodletterheal.SPL~  ~override/%s2%a.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@307) END    // "Healed"

COPY ~%icons%/bleed.BAM~                ~override/%s2%b.BAM~    // bleeding
LAF d2_portrait_icon INT_VAR name=RESOLVE_STR_REF (@302) STR_VAR bam=~%s2%b~ RET icon END
COPY ~%abilities%/castspelltarget.EFF~  ~override/%s2%b.EFF~
    WRITE_ASCII 0x30 ~%s2%b~ #8
COPY ~%abilities%/bleed10rounds.SPL~    ~override/%s2%b.SPL~
  LPF ALTER_EFFECT INT_VAR match_opcode=139 parameter1=RESOLVE_STR_REF (@302) END    // "Bleeding"
  LPF ALTER_EFFECT INT_VAR match_opcode=318 STR_VAR resource=~%s2%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=177 STR_VAR resource=~%s2%c~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=206 STR_VAR resource=~%s2%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=%icon% END

COPY ~%abilities%/castspelltarget.EFF~  ~override/%s2%c.EFF~    // bleed damage
    WRITE_ASCII 0x30 ~%s2%c~ #8
COPY ~%abilities%/bleeddamage1.SPL~     ~override/%s2%c.SPL~


//Bleeding removed by Heal or Greater Restoration
COPY_EXISTING_REGEXP GLOB ~.*\.SPL~  ~override~
  READ_STRREF 0x8 spellname
  PATCH_IF ((~%spellname%~ STRING_EQUAL_CASE ~Greater Restoration~) OR
            (~%spellname%~ STRING_EQUAL_CASE ~Heal~)) BEGIN
    LPF CLONE_EFFECT INT_VAR silent=1 multi_match=1 match_timing=1 match_parameter2=0 opcode=321 STR_VAR resource=~%s2%b~ END
  END
BUT_ONLY


//Clone base paladin clab
COPY_EXISTING ~CLABPA01.2DA~ ~override/%kitid%.2DA~
  REPLACE_TEXTUALLY ~GA_SPCL211~ ~****~    // remove Lay On Hands
  LPF d2_kit_ability_adder INT_VAR level=3 STR_VAR resource=~%p1%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 STR_VAR resource=~%p2%~ END
  LPF d2_kit_ability_adder INT_VAR level=7 STR_VAR resource=~%p3%~ END
  LPF d2_kit_ability_adder INT_VAR level=9 STR_VAR resource=~%p4%~ END
  LPF d2_kit_ability_adder INT_VAR level=2 multi=1 increment=4 STR_VAR resource=~%s1%~ END
  LPF d2_kit_ability_adder INT_VAR level=5 multi=1 increment=4 limit=13 STR_VAR resource=~%s2%~ END
  LPF d2_kit_ability_adder END


//Kit function
LAF ADD_KIT_EX
  INT_VAR
    kit_class = 6
    mixed = RESOLVE_STR_REF (@100501)
    lower = RESOLVE_STR_REF (@100502)
    help = RESOLVE_STR_REF (@100503)
    fallen_notice = RESOLVE_STR_REF (@100504)
  STR_VAR
    kit_name = ~D2%kitname%~
    unusable = ~0x00010000~        // Stalker usability (does not restrict paladin items)
    clab_path = ~%kitid%~
    clsrcreq = ~1 1 1 1 1 1 1~     // Usable by all races, if base class is usable
    clascolr = ~25 83 66 28 23~    // Priest of Talos colors
  RET 
    kit_id
END


//Set variables for external use
OUTER_TEXT_SPRINT confessor       ~%kitid%~
OUTER_TEXT_SPRINT rootingshot     ~%s1%~
OUTER_TEXT_SPRINT bloodletter     ~%s2%~
OUTER_TEXT_SPRINT confessorbleed  ~%s2%b~
OUTER_TEXT_SPRINT fnpclass        ~zealot~
OUTER_TEXT_SPRINT fnpdeity        ~auril_spheres~
OUTER_TEXT_SPRINT confessor1      ~%p1%~
OUTER_TEXT_SPRINT confessor2      ~%p2%~
OUTER_TEXT_SPRINT confessor3      ~%p3%~
OUTER_TEXT_SPRINT confessor4      ~%p4%~


/*

*/