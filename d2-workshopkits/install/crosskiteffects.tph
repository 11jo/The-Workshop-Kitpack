/*
Misc patching (spell interactions, etc.)
anything that's not directly for making the kit work
*/


//Drifter is immune to Rooting Shot, Dust Explosion (negative effects only)
ACTION_IF (nodrifter = 0) BEGIN

  ACTION_IF (FILE_EXISTS ~override/%rootingshot%.SPL~) BEGIN
    COPY_EXISTING ~%drifter_immunities%.SPL~  ~override~
      LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=318 parameter2=0 STR_VAR resource=~%confessorhold%~ END
    BUT_ONLY
  END

  ACTION_IF (FILE_EXISTS ~override/%dustexplosion%.SPL~) BEGIN
    COPY_EXISTING ~%cloud_immunities%.SPL~  ~override~
      PATCH_FOR_EACH status IN ~%dustblind%~ ~%dustslow%~ BEGIN
        LPF CLONE_EFFECT INT_VAR multi_match=1 opcode=318 parameter2=0 STR_VAR resource=~%status%~ END
      END
    BUT_ONLY
  END

END


//powder keg: set target = 7 for song changing abilities
//cast instantly, no casting animation, cast when paused

ACTION_FOR_EACH ability IN 
    ~%blastingsong%~    // song switching
    ~%boomingsong%~
    ~%dustexplosion%~
    ~%frenzyingflame%~
    ~%enhancedbardsong%~
    ~%rrenhancedsong%~    // rr songs
    ~%rrlingeringsong%~
BEGIN

  COPY_EXISTING ~%ability%.SPL~  ~override~
    LPF ALTER_SPELL_HEADER INT_VAR target=7 END
  IF_EXISTS BUT_ONLY

END


//change ToB starting armor for some kits
ACTION_IF (GAME_INCLUDES ~tob~) BEGIN
  COPY_EXISTING ~25STWEAP.2da~  ~override~
    PATCH_FOR_EACH kit IN ~d2beasthunter~ ~d2beasthunterfc~ BEGIN
      LPF d2_edit_2da_entry STR_VAR col_name=~%kit%~ row_name=armor entry=LEAT20 END    // changed to hide armor
    END

    PATCH_FOR_EACH kit IN ~d2confessor~ BEGIN
      LPF d2_edit_2da_entry STR_VAR col_name=~%kit%~ row_name=armor entry=leat14 END    // changed to leather armor
    END
  BUT_ONLY
END


//
//