/*
functions for armor restrictions
*/


//check item flags
DEFINE_PATCH_FUNCTION d2_restrict_item_flags  RET droppable displayable restrictions BEGIN
  READ_LONG 0x18 item_flags
  SET droppable = %item_flags% & BIT2
  SET displayable = %item_flags% & BIT3
  READ_LONG 0x1e restrictions    // 0 = no race/class/alignment restrictions
END

/*usage:
  LPF d2_restrict_item_flags RET droppable displayable END
  PATCH_IF ((%droppable%) AND (%displayable%)) BEGIN
    patching...
  END
*/



//
//specific armor restrictions

/*
variable "kit_to_patch" is the kit.IDS label (ex. BERSERKER)
*/

//no plate mail (full metal jaguar, noble scion)
DEFINE_PATCH_FUNCTION d2_restrict_armor_splint
  STR_VAR  kit_to_patch  = ~~
BEGIN
    PATCH_IF ((~%itemtype%~ STRING_EQUAL_CASE ~4A~)
    ) BEGIN
      LPF d2_restrict_armor_effect STR_VAR kit_to_patch = ~%kit_to_patch%~ END
    END
END

//no splint or plate (grave warden, spell fencer)
DEFINE_PATCH_FUNCTION d2_restrict_armor_chain
  STR_VAR  kit_to_patch  = ~~
BEGIN
    PATCH_IF ((stringref = 6677) OR                    // no splint mail
              (stringref = 6678) OR
              (~%itemtype%~ STRING_EQUAL_CASE ~4A~)    // no plate mail
    ) BEGIN
      LPF d2_restrict_armor_effect STR_VAR kit_to_patch = ~%kit_to_patch%~ END
    END
END

//no chain, splint, or plate (beast hunter, confessor)
DEFINE_PATCH_FUNCTION d2_restrict_armor_studded
  STR_VAR  kit_to_patch  = ~~
BEGIN
    PATCH_IF ((~%itemtype%~ STRING_EQUAL_CASE ~3A~) OR    // no chain or splint
              (~%itemtype%~ STRING_EQUAL_CASE ~4A~)       // no plate mail
    ) BEGIN
      LPF d2_restrict_armor_effect STR_VAR kit_to_patch = ~%kit_to_patch%~ END
    END
END

//only leather or hide (drifter)
DEFINE_PATCH_FUNCTION d2_restrict_armor_hide
  STR_VAR  kit_to_patch  = ~~
BEGIN
    PATCH_IF ((stringref = 6664) OR (stringref = 6665) OR (stringref = 6666) OR    // no studded leather
              (~%itemtype%~ STRING_EQUAL_CASE ~3A~) OR                             // no chain or splint
              (~%itemtype%~ STRING_EQUAL_CASE ~4A~)                                // no plate mail
    ) BEGIN
      LPF d2_restrict_armor_effect STR_VAR kit_to_patch = ~%kit_to_patch%~ END
    END
END

//only leather (nurse)
DEFINE_PATCH_FUNCTION d2_restrict_armor_leather
  STR_VAR  kit_to_patch  = ~~
BEGIN
    PATCH_IF ((stringref = 6664) OR (stringref = 6665) OR (stringref = 6666) OR       // no studded leather
              (stringref = 22714) OR (stringref = 22715) OR (stringref = 40488) OR    // no hide armor
              (~%itemtype%~ STRING_EQUAL_CASE ~3A~) OR                                // no chain or splint
              (~%itemtype%~ STRING_EQUAL_CASE ~4A~)                                   // no plate mail
    ) BEGIN
      LPF d2_restrict_armor_effect STR_VAR kit_to_patch = ~%kit_to_patch%~ END
    END
END



//
//add op319 effect (restrict item)

/*
this is automatically run when using one of the above functions
*/

DEFINE_PATCH_FUNCTION d2_restrict_armor_effect  
  STR_VAR  kit_to_patch  = ~~
BEGIN 
  LPF ADD_ITEM_EQEFFECT
    INT_VAR
      opcode = 319
      target = 1
      parameter1 = IDS_OF_SYMBOL (kit ~%kit_to_patch%~)
      parameter2 = 9
      timing = 2
  END
END



//
//add checks for scale armor

/*
function is only relevant for resource from array (can add more if scanning ITMs)
RET "scalearmor" variable, 1 = match "armor", 0 = not match
matching only needs to contain the "armor" string (armor = dragon will match all armors)
*/

ACTION_DEFINE_ASSOCIATIVE_ARRAY d2_scalearmor_array BEGIN
//bg2
  ~LEAT19~    => ~dragonscale~    // shadow dragon
  ~CHAN17~    => ~dragonscale~    // ashen scales
  ~CHAN20~    => ~dragonscale~    // white dragon
  ~PLAT18~    => ~dragonplate~    // red dragon
  ~PLAT20~    => ~dragonplate~    // blue dragon
  ~ohdarmor~  => ~dragonplate~    // silver dragon
//sod
  ~bdleat04~  => ~dragonscale~    // changeling vest
  ~bdleat05~  => ~dragonscale~    // dragonscale
//iwd
  ~cdscale~   => ~dragonscale~    // white dragon
  ~DRAGARM~   => ~dragonscale~    // dragonscale
  ~KING~      => ~dragonplate~    // winter plate
//mod
END

DEFINE_PATCH_FUNCTION d2_scalearmor_check STR_VAR armor=scale RET scalearmor BEGIN
  SET scalearmor = 0
  PHP_EACH d2_scalearmor_array AS res => check BEGIN
    PATCH_IF (~%SOURCE_RES%~ STR_EQ ~%res%~) BEGIN
      PATCH_IF !(~%check%~ STRING_CONTAINS_REGEXP ~%armor%~) BEGIN    // if "check" DOES contain "armor"
        SET scalearmor = 1
      END
    END
  END
END


/*
*/