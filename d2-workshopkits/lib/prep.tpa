//misc stuff
//


//fix #BONECIR.SPL (wrong signature)
ACTION_IF (FILE_EXISTS_IN_GAME ~#BONECIR.SPL~) BEGIN
  COPY_EXISTING ~#BONECIR.SPL~  ~override~
    WRITE_ASCII 0x0 ~SPL ~ #4
  BUT_ONLY
END


//number => id
ACTION_DEFINE_ASSOCIATIVE_ARRAY array_for_import_fix BEGIN
  1   => 1
  2   => 2
  3   => 3
  4   => 4
  5   => 5
  6   => 6
  7   => 7
  8   => 8
  9   => 9
  10  => a
  11  => b
  12  => c
  13  => d
  14  => e
  15  => f
  16  => g
  17  => h
  18  => i
  19  => j
  20  => k
END


//some Beamdog items have kit-specific effects
ACTION_DEFINE_ASSOCIATIVE_ARRAY item_kit_effects BEGIN
  bdamul02 => archer
  bdbelt02 => blade
  bdbrac05 => stalker
  bdbrd04  => jester
  bdsw1h04 => kensai
//bdsw2h01 => berserk         // not needed (effect based on STATE_ENRAGED spell state)
  BDAMUL11 => shapeshifter
  bdboot03 => wizardslayer
  bdsw1h25 => wizardslayer
  bdamul06 => helm
  bdshld03 => lathander
  dtkamul1 => archer          // sod2bg2 mod
  dtkboot2 => wizardslayer    // sod2bg2 mod
END

//
ACTION_DEFINE_ASSOCIATIVE_ARRAY kit_ids_array BEGIN
  barbarian      => 1073741824
  wildmage       => 2147483648
  wild_mage      => 2147483648
  berserk        => 16385
  wizslayer      => 16386
  wizardslayer   => 16386
  wizard_slayer  => 16386
  kensai         => 16387
  cavalier       => 16388
  inquisitor     => 16389
  undeadhunter   => 16390
  undead_hunter  => 16390
  archer         => 16391
  feralan        => 16391
  stalker        => 16392
  beastmaster    => 16393
  beast_master   => 16393
  assassin       => 16394
  assasin        => 16394
  bounty         => 16395
  swash          => 16396
  blade          => 16397
  jester         => 16398
  skald          => 16399
  totemic        => 16400
  shapeshifter   => 16401
  avenger        => 16402
  beastfriend    => 16402
  talos          => 16403
  helm           => 16404
  lathander      => 16405
  blackguard     => 16416
  shadowdancer   => 16417
  dwarvendef     => 16418
  dwarven_def    => 16418
  dragondisc     => 16419
  dragon_disc    => 16419
  darkmoon       => 16420
  dark_moon      => 16420
  sunsoul        => 16421
  sun_soul       => 16421
  tyr            => 16424
  tempus         => 16425
END



/*
stuff for armor restrictions
*/

DEFINE_PATCH_FUNCTION d2_restrict_armor_effect  
  STR_VAR  kit_to_patch  = ~~
BEGIN 
  LPF ADD_ITEM_EQEFFECT
    INT_VAR
      opcode = 319
      target = 1
      parameter1 = IDS_OF_SYMBOL (kit ~%kit_to_patch%~)
      parameter2 = 9
      timing = 2
  END
END

//no plate mail (full metal jaguar, noble scion)
DEFINE_PATCH_FUNCTION d2_restrict_armor_splint
  STR_VAR  kit_to_patch  = ~~
BEGIN
    PATCH_IF ((~%itemtype%~ STRING_EQUAL_CASE ~4A~)
      ) BEGIN
      LPF d2_restrict_armor_effect STR_VAR kit_to_patch = ~%kit_to_patch%~ END
    END
END

//no splint or plate (grave warden, spell fencer)
DEFINE_PATCH_FUNCTION d2_restrict_armor_chain
  STR_VAR  kit_to_patch  = ~~
BEGIN
    PATCH_IF ((stringref = 6677) OR                    // no splint mail
              (stringref = 6678) OR
              (~%itemtype%~ STRING_EQUAL_CASE ~4A~)    // no plate mail
      ) BEGIN
      LPF d2_restrict_armor_effect STR_VAR kit_to_patch = ~%kit_to_patch%~ END
    END
END

//no chain, splint, or plate (beast hunter multi)
DEFINE_PATCH_FUNCTION d2_restrict_armor_studded
  STR_VAR  kit_to_patch  = ~~
BEGIN
    PATCH_IF ((~%itemtype%~ STRING_EQUAL_CASE ~3A~) OR    // no chain or splint
              (~%itemtype%~ STRING_EQUAL_CASE ~4A~)       // no plate mail
      ) BEGIN
      LPF d2_restrict_armor_effect STR_VAR kit_to_patch = ~%kit_to_patch%~ END
    END
END

//only leather or hide (drifter)
DEFINE_PATCH_FUNCTION d2_restrict_armor_hide
  STR_VAR  kit_to_patch  = ~~
BEGIN
    PATCH_IF ((stringref = 6664) OR (stringref = 6665) OR (stringref = 6666) OR    // no studded leather
              (~%itemtype%~ STRING_EQUAL_CASE ~3A~) OR                             // no chain or splint
              (~%itemtype%~ STRING_EQUAL_CASE ~4A~)                                // no plate mail
      ) BEGIN
      LPF d2_restrict_armor_effect STR_VAR kit_to_patch = ~%kit_to_patch%~ END
    END
END

//only leather (nurse)
DEFINE_PATCH_FUNCTION d2_restrict_armor_leather
  STR_VAR  kit_to_patch  = ~~
BEGIN
    PATCH_IF ((stringref = 6664) OR (stringref = 6665) OR (stringref = 6666) OR       // no studded leather
              (stringref = 22714) OR (stringref = 22715) OR (stringref = 40488) OR    // no hide armor
              (~%itemtype%~ STRING_EQUAL_CASE ~3A~) OR                                // no chain or splint
              (~%itemtype%~ STRING_EQUAL_CASE ~4A~)                                   // no plate mail
      ) BEGIN
      LPF d2_restrict_armor_effect STR_VAR kit_to_patch = ~%kit_to_patch%~ END
    END
END


/*
*/