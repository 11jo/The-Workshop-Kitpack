//Simple spell converter
//can change spell type, level, and location
//location is automatically set to Spell or Ability if not specified


/*functions:
d2_convert_spell_type		PATCH or ACTION
*/

////////////////////////////////////////////////////////////

//

DEFINE_PATCH_FUNCTION d2_convert_spell_type

    INT_VAR type = 0          // 0=special, 1=wizard, 2=priest, 3=psionic, 4=innate, 5=bardsong
            level = ~-1~      // set spell level, must be 0-10 or no change occurs
            location = 0      // 1=weapon, 2=spell, 3=item, 4=ability (0 = spell or ability, based on type)
            update_res = 1    // resource fields set to original resource are changed to the new resource
		
BEGIN

  PATCH_IF ((type < 1) OR (type > 5))          BEGIN SET type = 0 END
  PATCH_IF ((level < 0) OR (level > 10))       BEGIN SET level = ~-1~ END
  PATCH_IF ((location < 1) OR (location > 4))  BEGIN SET location = 0 END

  WRITE_SHORT 0x1c type    // set spell type

  PATCH_IF (level >= 0) BEGIN
    WRITE_LONG 0x34 level    // set spell level
  END
  ELSE BEGIN
    READ_LONG 0x34 level     // or read level field
  END

  LPF ALTER_EFFECT INT_VAR power=level END    // set effects level

  PATCH_IF (location = 0) BEGIN
    PATCH_IF ((type = 1) OR (type = 2)) BEGIN 
      SET location = 2    // spell
    END
    ELSE BEGIN
      SET location = 4    // ability
    END
  END

  LPF ALTER_SPELL_HEADER INT_VAR location=location END    // set spell location

  PATCH_IF (update_res > 0) BEGIN
    LPF ALTER_EFFECT INT_VAR silent=1 STR_VAR match_resource=EVAL ~%SOURCE_RES%~ resource=EVAL ~%DEST_RES%~ END
  END

END

////////////////////////////////////////////////////////////

//newspell is created in the override
//If resource for %spell% doesn't exist, than nothing happens

DEFINE_ACTION_FUNCTION d2_convert_spell_type

    INT_VAR type = 0          // 0=special, 1=wizard, 2=priest, 3=psionic, 4=innate, 5=bardsong
            level = ~-1~      // set spell level, must be 0-10 or no change occurs
            location = 0      // 1=weapon, 2=spell, 3=item, 4=ability (0 = spell or ability, based on type)
            update_res = 1    // resource fields set to original resource are changed to the new resource
	
    STR_VAR spell = ~~        // SPL file to clone
            newspell = ~~     // SPL file to create
	
BEGIN

ACTION_IF (FILE_EXISTS_IN_GAME ~%spell%.SPL~) BEGIN

  COPY_EXISTING ~%spell%.SPL~ ~override/%newspell%.SPL~
    PATCH_IF ((type < 1) OR (type > 5))          BEGIN SET type = 0 END
    PATCH_IF ((level < 0) OR (level > 10))       BEGIN SET level = ~-1~ END
    PATCH_IF ((location < 1) OR (location > 4))  BEGIN SET location = 0 END

    WRITE_SHORT 0x1c type    // set spell type

    PATCH_IF (level >= 0) BEGIN
      WRITE_LONG 0x34 level    // set spell level
    END
    ELSE BEGIN
      READ_LONG 0x34 level     // or read level field
    END

    LPF ALTER_EFFECT INT_VAR power=level END    // set effects level

    PATCH_IF (location = 0) BEGIN
      PATCH_IF ((type = 1) OR (type = 2)) BEGIN 
        SET location = 2    // spell
      END
      ELSE BEGIN
        SET location = 4    // ability
      END
    END

    LPF ALTER_SPELL_HEADER INT_VAR location=location END    // set spell location

    PATCH_IF (update_res > 0) BEGIN
      LPF ALTER_EFFECT INT_VAR silent=1 STR_VAR match_resource=EVAL ~%SOURCE_RES%~ resource=EVAL ~%DEST_RES%~ END
    END

  BUT_ONLY
END

END

////////////////////////////////////////////////////////////